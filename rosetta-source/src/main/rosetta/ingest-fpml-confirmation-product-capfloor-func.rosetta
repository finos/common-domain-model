namespace cdm.ingest.fpml.confirmation.product.capfloor: <"FpML CapFloor product mapping functions.">
version "${project.version}"

import cdm.base.staticdata.party.*
import cdm.ingest.fpml.confirmation.common.*
import cdm.ingest.fpml.confirmation.party.*
import cdm.ingest.fpml.confirmation.product.swap.*
import cdm.observable.asset.*
import cdm.product.template.*

import fpml.confirmation.* as fpml

func MapCapFloorCounterpartyList:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
    output:
        counterpartyList Counterparty (0..2)

    add counterpartyList:
        MapPayerReceiverModelToCounterpartyList(
                fpmlCapFloor -> capFloorStream -> payerReceiverModel
            )

func MapCapFloorAncillaryPartyList:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
    output:
        ancillaryPartyList AncillaryParty (0..*)

func MapCapFloorNonTransferableProduct:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        nonTransferableProduct NonTransferableProduct (0..1)

    set nonTransferableProduct:
        NonTransferableProduct {
            identifier: MapProductIdentifierList(fpmlCapFloor -> productModel),
            taxonomy: MapProductTaxonomyList(fpmlCapFloor -> productModel),
            economicTerms: MapCapFloorEconomicTerms(fpmlCapFloor, cdmCounterpartyList)
        }

func MapCapFloorEconomicTerms:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        economicTerms EconomicTerms (0..1)
    set economicTerms:
        EconomicTerms {
            payout: MapSwapPayout(fpmlCapFloor -> capFloorStream, cdmCounterpartyList),
            terminationProvision: MapTerminationProvision(
                        fpmlCapFloor -> earlyTerminationProvision,
                        empty,
                        empty,
                        cdmCounterpartyList
                    ),
            ...
        }

func MapCapFloorPriceQuantityList:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
    output:
        priceQuantityList PriceQuantity (0..*)

    add priceQuantityList: fpmlCapFloor -> capFloorStream extract MapSwapPriceQuantity

func MapCapFloorAccountPartyReference:
    inputs:
        fpmlCapFloor fpml.CapFloor (0..1)
        fpmlAccount fpml.Account (0..1)
    output:
        partyReference Party (0..1)
            [metadata reference]

    set partyReference:
        MapPayerReceiverToAccountPartyReference(
                fpmlAccount,
                fpmlCapFloor -> capFloorStream -> payerReceiverModel
            )
