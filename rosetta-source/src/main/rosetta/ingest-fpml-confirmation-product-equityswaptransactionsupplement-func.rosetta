namespace cdm.ingest.fpml.confirmation.product.equityswaptransactionsupplement: <"FpML EquitySwapTransactionSupplement product mapping functions.">
version "${project.version}"

import cdm.base.staticdata.party.*
import cdm.ingest.fpml.confirmation.common.*
import cdm.ingest.fpml.confirmation.datetime.*
import cdm.ingest.fpml.confirmation.party.*
import cdm.ingest.fpml.confirmation.product.returnswap.*
import cdm.observable.asset.*
import cdm.product.template.*

import fpml.consolidated.* as fpml
import fpml.consolidated.* as fpml
import fpml.consolidated.accumulator.* as fpml
import fpml.consolidated.asset.* as fpml
import fpml.consolidated.bond.option.* as fpml
import fpml.consolidated.business.events.* as fpml
import fpml.consolidated.business.events.* as fpml
import fpml.consolidated.cd.* as fpml
import fpml.consolidated.clearing.processes.* as fpml
import fpml.consolidated.com.* as fpml
import fpml.consolidated.confirmation.processes.* as fpml
import fpml.consolidated.correlation.swaps.* as fpml
import fpml.consolidated.credit.event.notification.* as fpml
import fpml.consolidated.dividend.swaps.* as fpml
import fpml.consolidated.doc.* as fpml
import fpml.consolidated.eq.shared.* as fpml
import fpml.consolidated.eqd.* as fpml
import fpml.consolidated.fpmlenum.* as fpml
import fpml.consolidated.fpmlenum.* as fpml
import fpml.consolidated.fpmlreturn.swaps.* as fpml
import fpml.consolidated.fpmlstandard.* as fpml
import fpml.consolidated.fx.* as fpml
import fpml.consolidated.fx.accruals.* as fpml
import fpml.consolidated.fx.targets.* as fpml
import fpml.consolidated.generic.* as fpml
import fpml.consolidated.ird.* as fpml
import fpml.consolidated.loan.* as fpml
import fpml.consolidated.main.* as fpml
import fpml.consolidated.mktenv.* as fpml
import fpml.consolidated.msg.* as fpml
import fpml.consolidated.option.shared.* as fpml
import fpml.consolidated.recordkeeping.processes.* as fpml
import fpml.consolidated.reg.fpmlreporting.product.* as fpml
import fpml.consolidated.reg.fpmlreporting.shared.* as fpml
import fpml.consolidated.repo.* as fpml
import fpml.consolidated.riskdef.* as fpml
import fpml.consolidated.sec.lending.* as fpml
import fpml.consolidated.shared.* as fpml
import fpml.consolidated.valuation.* as fpml
import fpml.consolidated.valuation.fpmlreporting.* as fpml
import fpml.consolidated.variance.swaps.* as fpml
import fpml.consolidated.volatility.swaps.* as fpml
import fpml.custom.*


func MapEquitySwapTransactionSupplementCounterpartyList:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
    output:
        counterpartyList Counterparty (0..2)

    add counterpartyList:
        MapPayerReceiverModelToCounterpartyList(
                fpmlEquitySwapTransactionSupplement -> returnSwapLeg first -> payerReceiverModel
            )

func MapEquitySwapTransactionSupplementAncillaryPartyList:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
    output:
        ancillaryPartyList AncillaryParty (0..*)

func MapEquitySwapTransactionSupplementNonTransferableProduct:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        nonTransferableProduct NonTransferableProduct (0..1)

    set nonTransferableProduct:
        NonTransferableProduct {
            identifier: MapProductIdentifierList(
                        fpmlEquitySwapTransactionSupplement -> productModel
                    ),
            taxonomy: MapProductTaxonomyList(
                        fpmlEquitySwapTransactionSupplement -> productModel
                    ),
            economicTerms: MapEquitySwapTransactionSupplementEconomicTerms(
                        fpmlEquitySwapTransactionSupplement,
                        cdmCounterpartyList
                    )
        }

func MapEquitySwapTransactionSupplementEconomicTerms:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        economicTerms EconomicTerms (0..1)

    set economicTerms:
        EconomicTerms {
            effectiveDate: MapAdjustableOrRelativeDate(
                        fpmlEquitySwapTransactionSupplement -> returnSwapLeg -> effectiveDate
                            distinct
                            only-element
                    ),
            terminationDate: MapAdjustableOrRelativeDate(
                        fpmlEquitySwapTransactionSupplement -> returnSwapLeg -> terminationDate
                            distinct
                            only-element
                    ),
            payout: MapReturnSwapLegListToPayoutList(
                        fpmlEquitySwapTransactionSupplement -> returnSwapLeg,
                        cdmCounterpartyList
                    ),
            ...
        }

func MapReturnSwapLegListToPayoutList:
    inputs:
        fpmlDirectionalLegList fpml.DirectionalLeg (0..*)
        cdmCounterpartyList Counterparty (0..2)
    output:
        payoutList Payout (0..*)

    add payoutList:
        fpmlDirectionalLegList
            extract item switch
                fpml.InterestLeg then MapInterestLegToInterestRatePayout(item, cdmCounterpartyList),
                default empty

    add payoutList:
        fpmlDirectionalLegList
            extract item switch
                fpml.ReturnLeg then MapReturnLegToPerformancePayout(item, cdmCounterpartyList),
                default empty

func MapEquitySwapTransactionSupplementPriceQuantityList:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
    output:
        priceQuantityList PriceQuantity (0..*)

    alias returnLeg:
        (fpmlEquitySwapTransactionSupplement -> returnSwapLeg
            extract item switch
                fpml.ReturnLeg then item,
                default empty)
        then only-element

    add priceQuantityList:
        fpmlEquitySwapTransactionSupplement -> returnSwapLeg
            extract item switch
                fpml.InterestLeg then MapInterestLegToPriceQuantity(item, returnLeg),
                fpml.ReturnLeg then MapReturnLegToPriceQuantity,
                default empty

func MapEquitySwapTransactionSupplementAccountPartyReference:
    inputs:
        fpmlEquitySwapTransactionSupplement fpml.EquitySwapTransactionSupplement (0..1)
        fpmlAccount fpml.Account (0..1)
    output:
        partyReference Party (0..1)
            [metadata reference]

    set partyReference:
        MapBuyerSellerToAccountPartyReference(
                fpmlAccount,
                fpmlEquitySwapTransactionSupplement -> buyerSellerModel
            )
