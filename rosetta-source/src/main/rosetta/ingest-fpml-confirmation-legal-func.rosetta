namespace cdm.ingest.fpml.confirmation.legal
version "${project.version}"

import cdm.base.staticdata.party.*
import cdm.event.common.*
import cdm.ingest.fpml.confirmation.other.*
import cdm.legaldocumentation.common.*
import cdm.legaldocumentation.master.*
import cdm.product.collateral.*

import fpml.consolidated.* as fpml
import fpml.consolidated.* as fpml
import fpml.consolidated.accumulator.* as fpml
import fpml.consolidated.asset.* as fpml
import fpml.consolidated.bond.option.* as fpml
import fpml.consolidated.business.events.* as fpml
import fpml.consolidated.business.events.* as fpml
import fpml.consolidated.cd.* as fpml
import fpml.consolidated.clearing.processes.* as fpml
import fpml.consolidated.com.* as fpml
import fpml.consolidated.confirmation.processes.* as fpml
import fpml.consolidated.correlation.swaps.* as fpml
import fpml.consolidated.credit.event.notification.* as fpml
import fpml.consolidated.dividend.swaps.* as fpml
import fpml.consolidated.doc.* as fpml
import fpml.consolidated.eq.shared.* as fpml
import fpml.consolidated.eqd.* as fpml
import fpml.consolidated.fpmlenum.* as fpml
import fpml.consolidated.fpmlenum.* as fpml
import fpml.consolidated.fpmlreturn.swaps.* as fpml
import fpml.consolidated.fpmlstandard.* as fpml
import fpml.consolidated.fx.* as fpml
import fpml.consolidated.fx.accruals.* as fpml
import fpml.consolidated.fx.targets.* as fpml
import fpml.consolidated.generic.* as fpml
import fpml.consolidated.ird.* as fpml
import fpml.consolidated.loan.* as fpml
import fpml.consolidated.main.* as fpml
import fpml.consolidated.mktenv.* as fpml
import fpml.consolidated.msg.* as fpml
import fpml.consolidated.option.shared.* as fpml
import fpml.consolidated.recordkeeping.processes.* as fpml
import fpml.consolidated.reg.fpmlreporting.product.* as fpml
import fpml.consolidated.reg.fpmlreporting.shared.* as fpml
import fpml.consolidated.repo.* as fpml
import fpml.consolidated.riskdef.* as fpml
import fpml.consolidated.sec.lending.* as fpml
import fpml.consolidated.shared.* as fpml
import fpml.consolidated.valuation.* as fpml
import fpml.consolidated.valuation.fpmlreporting.* as fpml
import fpml.consolidated.variance.swaps.* as fpml
import fpml.consolidated.volatility.swaps.* as fpml
import fpml.custom.*
import fpml.custom.*

func MapContractDetails:
    inputs:
        fpmlTrade fpml.Trade (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        contractDetails ContractDetails (0..1)

    set contractDetails:
        ContractDetails {
            documentation: MapLegalAgreementList(
                        fpmlTrade -> documentation,
                        cdmCounterpartyList
                    ),
            governingLaw: MapGoverningLaw(fpmlTrade -> governingLaw)
        }

func MapLegalAgreementList:
    inputs:
        fpmlDocumentation fpml.Documentation (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreementList LegalAgreement (0..*)

    add legalAgreementList:
        if fpmlDocumentation -> masterAgreement exists
        then MapMasterAgreement(fpmlDocumentation -> masterAgreement, cdmCounterpartyList)
    add legalAgreementList:
        if fpmlDocumentation -> masterConfirmation exists
        then MapMasterConfirmation(
                    fpmlDocumentation -> masterConfirmation,
                    cdmCounterpartyList
                )
    add legalAgreementList:
        if fpmlDocumentation -> creditSupportAgreement exists
        then MapCreditSupportAgreement(
                    fpmlDocumentation -> creditSupportAgreement,
                    cdmCounterpartyList
                )
    add legalAgreementList:
        if fpmlDocumentation -> contractualDefinitions exists
                or fpmlDocumentation -> contractualMatrix exists
                or fpmlDocumentation -> contractualTermsSupplement exists
        then MapConfirmationLegalAgreement(fpmlDocumentation, cdmCounterpartyList)
    add legalAgreementList:
        if fpmlDocumentation -> otherAgreement exists
        then fpmlDocumentation -> otherAgreement
            extract MapOtherAgreements(item, cdmCounterpartyList)

func MapMasterAgreement:
    inputs:
        fpmlMasterAgreement fpml.MasterAgreement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreement LegalAgreement (0..1)

    alias cdmAgreementName:
        AgreementName {
            masterAgreementType: MapMasterAgreementTypeEnumWithScheme(
                        fpmlMasterAgreement -> masterAgreementType
                    ),
            agreementType: MasterAgreement,
            ...
        }

    set legalAgreement:
        MapLegalAgreement(
                fpmlMasterAgreement -> masterAgreementDate -> date,
                cdmCounterpartyList,
                cdmAgreementName,
                fpmlMasterAgreement -> masterAgreementVersion -> value to-int
            )

func MapMasterAgreementTypeEnumWithScheme:
    inputs:
        fpmlMasterAgreementType fpml.MasterAgreementType (0..1)
    output:
        masterAgreementTypeEnum MasterAgreementTypeEnum (0..1)
            [metadata scheme]

    set masterAgreementTypeEnum:
        MapMasterAgreementTypeEnum(fpmlMasterAgreementType -> value) with-meta {
            scheme: fpmlMasterAgreementType -> masterAgreementTypeScheme
        }

func MapMasterConfirmation:
    inputs:
        fpmlMasterConfirmation fpml.MasterConfirmation (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreement LegalAgreement (0..1)

    alias cdmAgreementName:
        AgreementName {
            masterConfirmationType: MapMasterConfirmationTypeEnumWithScheme(
                        fpmlMasterConfirmation -> masterConfirmationType
                    ),
            masterConfirmationAnnexType: MapMasterConfirmationAnnexTypeEnumWithScheme(
                        fpmlMasterConfirmation -> masterConfirmationAnnexType
                    ),
            agreementType: MasterConfirmation,
            ...
        }

    set legalAgreement:
        MapLegalAgreement(
                fpmlMasterConfirmation -> masterConfirmationDate -> date,
                cdmCounterpartyList,
                cdmAgreementName,
                empty
            )

func MapMasterConfirmationTypeEnumWithScheme:
    inputs:
        fpmlMasterConfirmationType fpml.MasterConfirmationType (0..1)
    output:
        masterConfirmationTypeEnum MasterConfirmationTypeEnum (0..1)
            [metadata scheme]

    set masterConfirmationTypeEnum:
        MapMasterConfirmationTypeEnum(fpmlMasterConfirmationType -> value) with-meta {
            scheme: fpmlMasterConfirmationType -> masterConfirmationTypeScheme
        }

func MapMasterConfirmationAnnexTypeEnumWithScheme:
    inputs:
        fpmlMasterConfirmationAnnexType fpml.MasterConfirmationAnnexType (0..1)
    output:
        masterConfirmationAnnexType MasterConfirmationAnnexTypeEnum (0..1)
            [metadata scheme]

    set masterConfirmationAnnexType:
        MapMasterConfirmationAnnexTypeEnum(fpmlMasterConfirmationAnnexType -> value) with-meta {
            scheme: fpmlMasterConfirmationAnnexType -> masterConfirmationAnnexTypeScheme
        }

func MapCreditSupportAgreement:
    inputs:
        fpmlCreditSupportAgreement fpml.CreditSupportAgreement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreement LegalAgreement (0..1)

    alias cdmAgreementName:
        AgreementName {
            creditSupportAgreementType: MapCreditSupportAgreementTypeEnumWithScheme(
                        fpmlCreditSupportAgreement -> ^type
                    ),
            agreementType: CreditSupportAgreement,
            ...
        }

    set legalAgreement:
        MapLegalAgreement(
                fpmlCreditSupportAgreement -> date -> date,
                cdmCounterpartyList,
                cdmAgreementName,
                empty
            )

func MapCreditSupportAgreementTypeEnumWithScheme:
    inputs:
        fpmlCreditSupportAgreementType fpml.CreditSupportAgreementType (0..1)
    output:
        creditSupportAgreementTypeEnum CreditSupportAgreementTypeEnum (0..1)
            [metadata scheme]

    set creditSupportAgreementTypeEnum:
        MapCreditSupportAgreementTypeEnum(fpmlCreditSupportAgreementType -> value) with-meta {
            scheme: fpmlCreditSupportAgreementType -> creditSupportAgreementTypeScheme
        }

func MapConfirmationLegalAgreement:
    inputs:
        fpmlDocumentation fpml.Documentation (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreement LegalAgreement (0..1)

    alias cdmAgreementName:
        AgreementName {
            contractualDefinitionsType: fpmlDocumentation -> contractualDefinitions
                    extract MapContractualDefinitionsEnumWithScheme,
            contractualMatrix: fpmlDocumentation -> contractualMatrix
                    extract
                        ContractualMatrix {
                            matrixType: MapMatrixTypeEnumWithScheme(matrixType),
                            matrixTerm: MapMatrixTermEnumWithScheme(matrixTerm)
                        },
            contractualTermsSupplement: fpmlDocumentation -> contractualTermsSupplement
                    extract
                        ContractualTermsSupplement {
                            contractualTermsSupplementType: MapContractualSupplementTypeEnumWithScheme(
                                        ^type
                                    ),
                            publicationDate: publicationDate -> date
                        },
            agreementType: Confirmation,
            ...
        }

    set legalAgreement:
        MapLegalAgreement(empty, cdmCounterpartyList, cdmAgreementName, empty)

func MapContractualDefinitionsEnumWithScheme:
    inputs:
        fpmlContractualDefinitions fpml.ContractualDefinitions (0..1)
    output:
        contractualDefinitionsEnum ContractualDefinitionsEnum (0..1)
            [metadata scheme]

    set contractualDefinitionsEnum:
        MapContractualDefinitionsEnum(fpmlContractualDefinitions -> value) with-meta {
            scheme: fpmlContractualDefinitions -> contractualDefinitionsScheme
        }

func MapMatrixTypeEnumWithScheme:
    inputs:
        fpmlMatrixType fpml.MatrixType (0..1)
    output:
        matrixTypeEnum MatrixTypeEnum (0..1)
            [metadata scheme]

    set matrixTypeEnum: MapMatrixTypeEnum(fpmlMatrixType -> value) with-meta {
        scheme: fpmlMatrixType -> matrixTypeScheme
    }

func MapMatrixTermEnumWithScheme:
    inputs:
        fpmlMatrixTerm fpml.MatrixTerm (0..1)
    output:
        matrixTermEnum MatrixTermEnum (0..1)
            [metadata scheme]

    set matrixTermEnum: MapMatrixTermEnum(fpmlMatrixTerm -> value) with-meta {
        scheme: fpmlMatrixTerm -> matrixTermScheme
    }

func MapContractualSupplementTypeEnumWithScheme:
    inputs:
        fpmlContractualSupplement fpml.ContractualSupplement (0..1)
    output:
        contractualSupplementTypeEnum ContractualSupplementTypeEnum (0..1)
            [metadata scheme]

    set contractualSupplementTypeEnum:
        MapContractualSupplementTypeEnum(fpmlContractualSupplement -> value) with-meta {
            scheme: fpmlContractualSupplement -> contractualSupplementScheme
        }

func MapOtherAgreements:
    inputs:
        fpmlOtherAgreement fpml.OtherAgreement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        legalAgreement LegalAgreement (0..1)

    alias agreementName:
        AgreementName {
            otherAgreement: fpmlOtherAgreement -> ^type -> value,
            agreementType: Other,
            ...
        }
    alias vintage: fpmlOtherAgreement -> version -> value to-int

    set legalAgreement:
        MapLegalAgreement(
                fpmlOtherAgreement -> date -> date,
                cdmCounterpartyList,
                agreementName,
                vintage
            )

func MapLegalAgreement:
    inputs:
        fpmlAgreementDate date (0..1)
        cdmCounterpartyList Counterparty (0..2)
        cdmAgreementName AgreementName (0..1)
        cdmVintage int (0..1)
    output:
        legalAgreement LegalAgreement (0..1)

    set legalAgreement:
        LegalAgreement {
            agreementDate: fpmlAgreementDate,
            contractualParty: MapContractualParty(cdmCounterpartyList),
            legalAgreementIdentification:
                LegalAgreementIdentification {
                    governingLaw: empty,
                    agreementName: cdmAgreementName,
                    publisher: empty,
                    vintage: cdmVintage,
                },
            ...
        }

func MapContractualParty:
    inputs:
        cdmCounterpartyList Counterparty (0..2)
    output:
        contractualPartyList Party (0..2)
            [metadata reference]

    add contractualPartyList: cdmCounterpartyList -> partyReference

func MapGoverningLaw:
    inputs:
        fpmlGoverningLaw fpml.GoverningLaw (0..1)
    output:
        governingLawEnum GoverningLawEnum (0..1)
            [metadata scheme]

    set governingLawEnum: fpmlGoverningLaw -> value to-enum GoverningLawEnum
/*
 * func MapAgreementTerms:
 *     inputs:
 *         fpmlTrade fpml.Trade (0..1)
 *         fpmlPartiesAndAccountsModel fpml.PartiesAndAccountsModel (0..1)
 *     output:
 *         agreementTerms AgreementTerms (0..1)
 *     set agreementTerms:
 *         AgreementTerms {
 *             agreement: MapAgreement(fpmlTrade, fpmlPartiesAndAccountsModel),
 *             clauseLibrary: empty,
 *             counterparty: MapCounterpartyList(fpmlTrade),
 *         }

 * func MapUmbrellaAgreement:
 *     inputs:
 *         fpmlTrade fpml.Trade (0..1)
 *         fpmlPartiesAndAccountsModel fpml.PartiesAndAccountsModel (0..1)
 *     output:
 *         umbrellaAgreement UmbrellaAgreement (0..1)
 *     set umbrellaAgreement:
 *         UmbrellaAgreement {
 *             isApplicable: empty,
 *             language: empty,
 *             parties: MapUmbrellaAgreementEntityList(fpmlTrade, fpmlPartiesAndAccountsModel),
 *         }
 */
