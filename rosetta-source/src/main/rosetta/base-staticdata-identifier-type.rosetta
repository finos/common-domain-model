namespace cdm.base.staticdata.identifier : <"Basic identifier and assigned identifier concepts that are applicable across the model.">
version "${project.version}"

import cdm.base.*
import cdm.base.staticdata.party.*
import cdm.observable.asset.*

type AssignedIdentifier: <"A class to specify the identifier value and its associated version.">

    identifier string (1..1) <"The identifier value.">
        [metadata scheme]
    version int (0..1) <"The identifier version, which is specified as an integer and is meant to be incremented each time the transaction terms (whether contract or event) change. This version is made option to support the use case where the identifier is referenced without the version. The constraint that a contract and a lifecycle event need to have an associated version is enforced through data rules.">

type Identifier: <"A class to specify a generic identifier, applicable to CDM artefacts such as executions, contracts, lifecycle events and legal documents. An issuer can be associated with the actual identifier value as a way to properly qualify it.">
    [metadata key]
	[docReference ICMA GMRA namingConvention "Identifier"
		provision "As referenced in GMRA paragraph 3(b) Securities may be identified using identifying numbers such as CUSIP or ISIN"]

    issuerReference Party (0..1) <"The identifier issuer, when specified by reference to a party specified as part of the transaction.">
        [metadata reference]
    issuer string (0..1) <"The identifier issuer, when specified explicitly alongside the identifier value (instead of being specified by reference to a party).">
        [metadata scheme]
    assignedIdentifier AssignedIdentifier (1..*) <"The identifier value. This level of indirection between the issuer and the identifier and its version provides the ability to associate multiple identifiers to one issuer, consistently with the FpML PartyTradeIdentifier.">

    condition IssuerChoice: <"The identifier issuer is specified either explicitly or by reference to one of the parties.">
        required choice issuerReference, issuer

type IdentifiedList: <"Attaches an identifier to a collection of objects, when those objects themselves can each be represented by an identifier. One use case is the representation of package transactions, where each component is a separate trade with its own identifier, and those trades are linked together as a package with its own identifier. The data type has been named generically rather than referring to 'packages' as it may have a number of other uses.">
    [metadata key]

    listId Identifier (1..1) <"The identifier for the list. In the case of a package transaction, this would be the package identifier. This attribute is mandatory to allow the list itself to be identified.">
    componentId Identifier (1..*) <"Identifier(s) for each component of the list. Since the data type is used to link multiple identified objects together, at least 1 component is required in the list at the inception of the Trade. As the trade progresses, this element is expected to contain multiple component Ids.">
    price Price (0..1) <"The price of the package.">

type LocationIdentifier extends Identifier: <"Specifies a location identifier. An issuer and an identifier type can be associated with the actual identifier value as a way to properly qualify it.">

    locationIdentifierType CommodityLocationIdentifierTypeEnum (0..1) <"Specifies the nature of a location identifier.">

    condition IdentifierType: <"The element specifying the nature of a location identifier can only be applied to a single identifier.">
        if locationIdentifierType exists
        then assignedIdentifier count = 1
