namespace cdm.ingest.fpml.confirmation.product.dividendswaptransactionsupplement: <"FpML DividendSwapTransactionSupplement product mapping functions.">
version "${project.version}"

import cdm.base.math.*
import cdm.base.staticdata.party.*
import cdm.ingest.fpml.confirmation.common.*
import cdm.ingest.fpml.confirmation.datetime.*
import cdm.ingest.fpml.confirmation.party.*
import cdm.ingest.fpml.confirmation.pricequantity.*
import cdm.ingest.fpml.confirmation.settlement.*
import cdm.observable.asset.*
import cdm.product.asset.*
import cdm.product.common.settlement.*
import cdm.product.template.*

import fpml.consolidated.* as fpml
import fpml.consolidated.asset.* as fpml
import fpml.consolidated.dividend.swaps.* as fpml
import fpml.consolidated.shared.* as fpml
import fpml.consolidated.option.shared.* as fpml

func MapDividendSwapTransactionSupplementCounterpartyList:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
    output:
        counterpartyList Counterparty (0..2)

    add counterpartyList:
        MapPayerReceiverModelToCounterpartyList(
                fpmlDividendSwapTransactionSupplement -> dividendLeg -> payerReceiverModel
            )

func MapDividendSwapTransactionSupplementAncillaryPartyList:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
    output:
        ancillaryPartyList AncillaryParty (0..*)

func MapDividendSwapTransactionSupplementNonTransferableProduct:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        nonTransferableProduct NonTransferableProduct (0..1)

    set nonTransferableProduct:
        NonTransferableProduct {
            identifier: MapProductIdentifierList(
                        fpmlDividendSwapTransactionSupplement -> productModel
                    ),
            taxonomy: MapProductTaxonomyList(
                        fpmlDividendSwapTransactionSupplement -> productModel
                    ),
            economicTerms: MapDividendSwapTransactionSupplementEconomicTerms(
                        fpmlDividendSwapTransactionSupplement,
                        cdmCounterpartyList
                    )
        }

func MapDividendSwapTransactionSupplementEconomicTerms:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        economicTerms EconomicTerms (0..1)

    set economicTerms:
        EconomicTerms {
            payout: MapDividendSwapTransactionSupplementPayoutList(
                        fpmlDividendSwapTransactionSupplement,
                        cdmCounterpartyList
                    ),
            ...
        }

func MapDividendSwapTransactionSupplementPayoutList:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        payoutList Payout (0..*)

    add payoutList:
        MapDividendLegToPerformancePayout(
                fpmlDividendSwapTransactionSupplement -> dividendLeg,
                cdmCounterpartyList
            )

    add payoutList:
        MapFixedPriceWithLocation(fpmlDividendSwapTransactionSupplement -> fixedLeg, empty)
            extract
                MapFixedPaymentLegToFixedPricePayout(
                        fpmlDividendSwapTransactionSupplement -> fixedLeg,
                        fpmlDividendSwapTransactionSupplement -> dividendLeg,
                        cdmCounterpartyList,
                        empty,
                        empty,
                        empty,
                        item,
                        fpmlDividendSwapTransactionSupplement
                    )

func MapDividendLegToPerformancePayout:
    inputs:
        fpmlDividendLeg fpml.DividendLeg (0..1)
        cdmCounterpartyList Counterparty (0..2)
    output:
        payout Payout (0..1)

    set payout:
        Payout {
            PerformancePayout:
                PerformancePayout {
                    payerReceiver: MapPayerReceiver(
                                fpmlDividendLeg -> payerReceiverModel,
                                cdmCounterpartyList
                            ),
                    // REMEMBER - Java needed to have priceQuantity populate with multiple dividendPeriods and FixedPayments as no ID's to correctly match
                    paymentDates: empty,
                    valuationDates: empty,
                    settlementTerms: MapOptionSettlementModelToSettlementTerms(
                                fpmlDividendLeg -> optionSettlementModel
                            ),
                    underlier:
                        Underlier {
                            Observable: MapUnderlyerToObservableWithAddress(
                                        fpmlDividendLeg -> underlyer
                                    ),
                            ...
                        },
                    fxFeature: fpmlDividendLeg -> fxFeature extract MapFxFeature,
                    returnTerms:
                        ReturnTerms {
                            dividendReturnTerms: MapSwapTransactionSupplementDividendReturnTerms(
                                        fpmlDividendLeg -> underlyer,
                                        fpmlDividendLeg -> dividendPeriod,
                                        empty,
                                        fpmlDividendLeg
                                    ),
                            ...
                        },
                    ...
                },
            ...
        }

func MapSwapTransactionSupplementDividendReturnTerms:
    inputs:
        fpmlUnderlyer fpml.Underlyer (0..1)
        fpmlDividendPeriodPaymentList fpml.DividendPeriodPayment (0..*)
        fpmlAdjustableOrRelativeDate fpml.AdjustableOrRelativeDate (0..1)
        fpmlDividendLeg fpml.DividendLeg (0..1)
    output:
        dividendReturnTerms DividendReturnTerms (0..1)

    set dividendReturnTerms:
        DividendReturnTerms {
            dividendPayoutRatio: if fpmlUnderlyer -> basket exists
                    then fpmlUnderlyer -> basket -> basketConstituent
                    extract
                        DividendPayoutRatio {
                            totalRatio: dividendPayout -> dividendPayoutSequence -> dividendPayoutRatio,
                            basketConstituent: MapBasketConstituentWithAddress,
                            cashRatio: fpmlDividendLeg -> declaredCashAndCashEquivalentDividendPercentageModel -> declaredCashDividendPercentage,
                            nonCashRatio: fpmlDividendLeg -> declaredCashAndCashEquivalentDividendPercentageModel -> declaredCashEquivalentDividendPercentage,
                        }
                else if fpmlUnderlyer -> singleUnderlyer exists
                then DividendPayoutRatio {
                        totalRatio: fpmlUnderlyer -> singleUnderlyer -> dividendPayout -> dividendPayoutSequence -> dividendPayoutRatio,
                        cashRatio: fpmlDividendLeg -> declaredCashAndCashEquivalentDividendPercentageModel -> declaredCashDividendPercentage,
                        nonCashRatio: fpmlDividendLeg -> declaredCashAndCashEquivalentDividendPercentageModel -> declaredCashEquivalentDividendPercentage,
                        ...
                    },
            dividendPeriod: fpmlDividendPeriodPaymentList
                    extract
                        DividendPeriod {
                            startDate: MapUnadjustedDateToDividendPaymentDate(
                                        unadjustedStartDate
                                    ),
                            endDate: MapUnadjustedDateToDividendPaymentDate(
                                        unadjustedEndDate
                                    ),
                            dividendPaymentDate: MapAdjustableOrRelativeDateToDividendPaymentDate(
                                        paymentDate
                                    ),
                            dateAdjustments: MapBusinessDayAdjustments(dateAdjustments),
                            dividendValuationDate: MapAdjustableOrRelativeDate(
                                        valuationDate
                                    ), // REMEMBER
                            ...
                        },
            ...
        }

func MapFixedPaymentLegToFixedPricePayout:
    inputs:
        fpmlFixedPaymentLeg fpml.FixedPaymentLeg (0..1)
        fpmlDividendLeg fpml.DividendLeg (0..1)
        cdmCounterpartyList Counterparty (0..2)
        fpmlRelativeDateOffset fpml.RelativeDateOffset (0..1)
        fpmlOptionSettlementModel fpml.OptionSettlementModel (0..1)
        fpmlLeg fpml.Leg (0..1)
        quantitySchedule1 NonNegativeQuantitySchedule (0..1)
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
    output:
        payout Payout (0..1)

    set payout:
        Payout {
            FixedPricePayout:
                FixedPricePayout {
                    payerReceiver: MapPayerReceiver(
                                fpmlFixedPaymentLeg -> payerReceiverModel,
                                cdmCounterpartyList
                            ),
                    priceQuantity:
                        ResolvablePriceQuantity {
                            quantitySchedule: quantitySchedule1,
                            ...
                        },
                    // REMEMBER - Java needed to have these populate with multiple dividendPeriods and FixedPayments as no ID's to correctly match
                    settlementTerms: MapFixedLegSettlementTerms(
                    fpmlFixedPaymentLeg,
                    fpmlOptionSettlementModel,
                    fpmlRelativeDateOffset,
                    empty,
                    empty
                    ),
                    paymentDates: empty,
                    // REMEMBER - Known issue that we don't know how to populate as the synonym's artificially creates payer/receiver, and on the fixedPayment(s) there is no payer/receiver or ID's that can be used.
                    fixedPrice:
                        FixedPrice {
                            price: fpmlDividendLeg -> dividendPeriod
                                    extract
                                        MapFixedPriceToFixedPricePayout(
                                                fpmlDividendSwapTransactionSupplement
                                            )
                        },
                    ...
                },
            ...
        }

func MapFixedPriceToFixedPricePayout:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
    output:
        price PriceSchedule (0..1)

    alias fixedPrice:
        if fpmlDividendSwapTransactionSupplement -> dividendLeg -> dividendPeriod -> fixedStrike exists
        then MapDividendFixedPriceToPriceSchedule(fpmlDividendSwapTransactionSupplement)

    set price: fixedPrice

func MapFixedLegSettlementTerms:
    inputs:
        fpmlFixedPaymentLeg fpml.FixedPaymentLeg (0..1)
        fpmlOptionSettlementModel fpml.OptionSettlementModel (0..1)
        fpmlRelativeDateOffset fpml.RelativeDateOffset (0..1)
        fpmlUnadjustedDate date (0..1)
        fpmlAdjustableDate fpml.AdjustableDate (0..1)
    output:
        settlementTerms SettlementTerms (0..1)

    set settlementTerms:
        SettlementTerms {
            settlementType: fpmlOptionSettlementModel -> settlementType to-enum SettlementTypeEnum,
            settlementCurrency: MapCurrency(
                        fpmlOptionSettlementModel -> settlementAmountOrCurrencyModel -> settlementCurrency
                    ),
            settlementDate:
                SettlementDate {
                    adjustableOrRelativeDate: MapAdjustableOrAdjustedOrRelativeDate(
                                fpmlUnadjustedDate,
                                fpmlAdjustableDate,
                                fpmlRelativeDateOffset
                            ),
                    ...
                },
            ...
        }

func MapDividendSwapTransactionSupplementPriceQuantityList:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
    output:
        priceQuantityList PriceQuantity (0..*)

    add priceQuantityList:
        MapFixedPaymentLegToPriceQuantity(fpmlDividendSwapTransactionSupplement -> fixedLeg)

    add priceQuantityList:
        MapDividendLegToPriceQuantity(fpmlDividendSwapTransactionSupplement -> dividendLeg)

func MapFixedPaymentLegToPriceQuantity:
    inputs:
        fpmlFixedPaymentLeg fpml.FixedPaymentLeg (0..1)
    output:
        priceQuantity PriceQuantity (0..1)

    set priceQuantity:
        PriceQuantity {
            quantity: fpmlFixedPaymentLeg -> fixedPayment
                    extract MapQuantityFixedPayment(item, fpmlFixedPaymentLeg),
            ...
        }

func MapQuantityFixedPayment:
    inputs:
        fpmlFixedPaymentAmount fpml.FixedPaymentAmount (0..1)
        fpmlLeg fpml.Leg (0..1)
    output:
        priceQuantity NonNegativeQuantitySchedule (0..1)
            [metadata location]

    set priceQuantity:
        MapFixedPaymentAmountQuantityWithLocation(fpmlFixedPaymentAmount, fpmlLeg) then item

func MapFixedPaymentAmountQuantityWithLocation:
    inputs:
        fpmlFixedPaymentAmount fpml.FixedPaymentAmount (0..1)
        fpmlLeg fpml.Leg (0..1)
    output:
        nonNegativeQuantitySchedules NonNegativeQuantitySchedule (0..1)
            [metadata location]

    set nonNegativeQuantitySchedules:
        MapNotionalAmountWithIdToQuantityWithLocation(
                fpmlFixedPaymentAmount -> paymentAmount -> amount,
                empty,
                fpmlFixedPaymentAmount -> paymentAmount -> currency,
                fpmlFixedPaymentAmount -> id,
                empty
            )

func MapQuantity:
    inputs:
        fpmlFixedPaymentLeg fpml.FixedPaymentLeg (0..1)
    output:
        priceQuantity NonNegativeQuantitySchedule (0..*)

    add priceQuantity:
        MapFixedPaymentAmountListToQuantityListWithLocation(
                fpmlFixedPaymentLeg -> fixedPayment,
                fpmlFixedPaymentLeg
            )
            then item

func MapFixedPaymentAmountListToQuantityListWithLocation:
    inputs:
        fpmlFixedPaymentAmount fpml.FixedPaymentAmount (0..*)
        fpmlLeg fpml.Leg (0..1)
    output:
        nonNegativeQuantitySchedules NonNegativeQuantitySchedule (0..*)
            [metadata location]

    add nonNegativeQuantitySchedules:
        MapNotionalAmountToQuantityWithLocation( // TODO handle multi-cardinality as a schedule
                fpmlFixedPaymentAmount -> paymentAmount -> amount,
                empty,
                fpmlFixedPaymentAmount -> paymentAmount -> currency,
                fpmlLeg
            )

func MapDividendLegToPriceQuantity:
    inputs:
        fpmlDividendLeg fpml.DividendLeg (0..1)
    output:
        priceQuantity PriceQuantity (0..1)

    set priceQuantity:
        PriceQuantity {
            observable: MapUnderlyerToObservableWithLocation(fpmlDividendLeg -> underlyer),
            quantity: MapSingleUnderlyerToNonNegativeQuantitySchedule(
                        fpmlDividendLeg -> underlyer -> singleUnderlyer
                    ),
            ...
        }

func MapSingleUnderlyerToNonNegativeQuantitySchedule:
    inputs:
        singleUnderlyer fpml.SingleUnderlyer (0..1)
    output:
        nonNegativeQuantitySchedule NonNegativeQuantitySchedule (0..1)

    set nonNegativeQuantitySchedule:
        NonNegativeQuantitySchedule {
            value: singleUnderlyer -> openUnits,
            unit: if singleUnderlyer -> openUnits exists
                    then UnitType {
                        financialUnit: IndexUnit,
                        ...
                    },
            ...
        }

func MapDividendSwapTransactionSupplementAccountPartyReference:
    inputs:
        fpmlDividendSwapTransactionSupplement fpml.DividendSwapTransactionSupplement (0..1)
        fpmlAccount fpml.Account (0..1)
    output:
        partyReference Party (0..1)
            [metadata reference]

    set partyReference:
        MapPayerReceiverToAccountPartyReference(
                fpmlAccount,
                fpmlDividendSwapTransactionSupplement -> dividendLeg -> payerReceiverModel
            )
