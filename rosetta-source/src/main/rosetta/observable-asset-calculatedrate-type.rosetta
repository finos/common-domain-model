namespace cdm.observable.asset.calculatedrate : <"Floating amount calculations for calculated rates.">
version "${project.version}"

import cdm.base.datetime.*
import cdm.base.staticdata.asset.rates.*
import cdm.observable.asset.fro.*

type FloatingRateCalculationParameters: <"Defines the structures needed to represent the calculation parameters for daily averaged and compounded modular rates as defined in the 2021 ISDA Definitions in Section 7. This type is used to represent modular computed rates in interestRatePayouts.">
       [deprecated]  // to be replaced by new type FloatingRateCalculationTerms
    calculationMethod CalculationMethodEnum (1..1) <"calculation type (averaging or compounding).">
    observationShiftCalculation ObservationShiftCalculation (0..1) <"any obervation shift parameters if applicable.">
    lookbackCalculation OffsetCalculation (0..1) <"any lookback  parameters if applicable.">
    lockoutCalculation OffsetCalculation (0..1) <"any lockout  parameters if applicable.">
    applicableBusinessDays BusinessCenters (0..1) <"the business days that are applicable for the calculation.">
    observationParameters ObservationParameters (0..1) <" any applicable observation parameters, such as daily caps or floors.">

type ScreenRate:
    screenRateStyle ScreenRateIndexStyleEnum (0..1) <"Second level ISDA FRO category : Screen Rate Style (which corresponds to ISDA Def 2021 .6.6.3).">
    bespokeScreenRate string (0..1) <"To describe the nature of the ScreenRate when not referring to any other standard kind of style enumerated in the list.">

    condition : one-of

type CalculatedRate:
    calculatedRateStyle CalculatedRateStyleEnum (0..1) <"Second level ISDA FRO category : Calculated Rate Style (which corresponds to ISDA Def 2021 .6.6.4)">
    calculationMethod FloatingRateCalculationMethodEnum (0..1) <"3rd level ISDA FRO category about calculation methods applied to Calculation Rate other than Compounded Index (which corresponds to ISDA Def 2021 7.3. and .7.4.)">
    compoundedIndexCalculationMethod FloatingIndexCalculationMethodEnum (0..1) <"3rd level ISDA FRO category about calculation methods applied to Calculation Rate when ot style Compounded Index (which means an Index Method is specified, per ISDA Def 2021 .6.6.4.(iii), which Enum values corresponds to ISDA Def 2021 .7.7.)">
    bespokeIndexMethod string (0..1) <"To describe the nature of the Index Method when not referring to any other standard kind of style enumerated in the list.">
    observationShiftCalculation ObservationShiftCalculation (0..1) <"any obervation shift parameters if applicable.">
    applicableBusinessDays BusinessCenters (0..1) <"the business days that are applicable for the calculation.">
    businessDaysOffset int (0..1) <"The number of business days offset to apply by reference to the applicable days of the Calculation Period in accordance with the Calculation Method (e.g. lookback or lockout or observation shift) which further specifies how to apply such offset in calculations.">

    condition AlternateIndexMethod: <"Ensure that alternateIndexMethod only exists for the purpose of describing the nature of the IndexMethod when not referring to any other standard kind of style enumerated in the list.">
    if bespokeIndexMethod exists
    then compoundedIndexCalculationMethod = FloatingIndexCalculationMethodEnum -> AlternateMethod

    condition IndexCalculationMethod: <"Ensure that specific parameters related to CompoundedIndex style are only set when such particular value exists.">
    if calculatedRateStyle = CalculatedRateStyleEnum -> CompoundedIndex
    then compoundedIndexCalculationMethod exists and calculationMethod is absent
    else calculationMethod exists and compoundedIndexCalculationMethod is absent

    condition ObservationShiftCalculation: <"Ensure that specific parameters related to ObservationShift method are only set when such method exists.">
    if observationShiftCalculation exists
    then compoundedIndexCalculationMethod = FloatingIndexCalculationMethodEnum -> CompoundedObservationShift
    or calculationMethod = FloatingRateCalculationMethodEnum -> CompoundingObservationShift
    or calculationMethod = FloatingRateCalculationMethodEnum -> AveragingObservationShift

    condition BusinessDaysOffset:<"Ensure Offset exists when required by the method at stake, that is when calculation involves lookback or lockout or observation shift.">
    if calculationMethod exists
    and calculationMethod <> FloatingRateCalculationMethodEnum -> OISCompounding
    or compoundedIndexCalculationMethod = FloatingIndexCalculationMethodEnum -> CompoundedObservationShift
    then businessDaysOffset exists

type RateDeterminationMethod:
    screenRateStyle ScreenRate (0..1)
    calculatedRateStyle CalculatedRate (0..1)
    bespokeRateStyle string (0..1)

       condition : one-of

type FloatingRateCalculationTerms: <"Defines the structures needed to represent the calculation parameters for daily averaged and compounded modular rates as defined in the 2021 ISDA Definitions in Section 7. This type is used to represent modular computed rates in interestRatePayouts.">
    rateDeterminationMethod RateDeterminationMethod (1..1) < "Top level ISDA FRO category (which corresponds to ISDA Def 2021 ยง.6.6.1. when unsing the Floating Rate Matrix, else to ยง.6.6.5. when not using Floating Rate Matrix.) ">
    dailyCapFloor ObservationParameters (0..1) <" any applicable observation parameters, such as daily caps or floors.">

type FallbackRateParameters: <"Defines the structure needed to represent fallback rate parameters. This type is used to represent modular computed rates in interestRatePayouts.">
    floatingRateIndex FloatingRateIndexEnum (1..1) <"The floating rate index that is used as the basis of the fallback rate.">
    overnightCalculationMethod FloatingRateCalculationTerms (0..1) <"Support for modular calculated rates.">
    spreadAdjustment number (0..1) <"The economic spread applied to the underlying fallback rate to replicate the original risky rate.">
    temporaryNonPublicationBespokeTerms string (0..1) <"Describe bespoke terms which relate to Temporary Non-Publication provisionsn e.g. trigger, administrator, fallbacks, etc.">
    permanentCessationBespokeTerms string (0..1) <"Describe bespoke terms which relate to Temporary Non-Publication provisionsn e.g. trigger, administrator, fallbacks, etc. ">
    calculationParameters FloatingRateCalculationParameters (0..1) <"Support for modular calculated rates, such such as lockout compound calculations.">
        [deprecated] // to be replaced by overnightCalculationMethod 
    effectiveDate date (0..1) <"The date the fallback rate takes effect.">


type OffsetCalculation: <"Defines business day shifts for daily componded or averaged rates.  This type is used for lookback and lockout rates. This type is used to represent modular computed rates in interestRatePayouts.">
    offsetDays int (0..1) <"The number of business days offset.">

type ObservationShiftCalculation: <"Parameters to describe the observation shift for a daily compounded or averaged floating rate. This type is used to represent modular computed rates in interestRatePayouts.">
    offsetDays int (0..1) <"The number of days of observation shift.">
    calculationBase ObservationPeriodDatesEnum (0..1) <"Whether the rate is calculated in advance, in arrears, or relative to a reset date.">
    additionalBusinessDays BusinessCenters (0..1) <"Any additional business days that be applicable.">

type ObservationParameters: <"Parameters on daily observed computed rates, specifically daily caps and floors. This type is used to represent modular computed rates in interestRatePayouts.">
    observationCapRate number (0..1) <"A daily observation cap rate.">
    observationFloorRate number (0..1) <"A daily observation floor rate.">

type CalculatedRateDetails: <"Type for reporting details of calculated rates, including the observations that went into the final reported rate.">
    observations CalculatedRateObservations (0..1) <"The observation dates and weights for each observation date.">
    weightedRates number (0..*) <"The weighted value of each observation.">
    growthFactor number (0..*) <"The daily growth factors, showing the weighted rates divided by the day count basis plus one, giving how much the value grows for each step in the calculation.">
    compoundedGrowth number (0..*) <"The compounding curve, showing how the initial value grew during the calculation period.">
    aggregateValue number (0..1) <"The total sum or product of all the individual terms that went into the calculated rate.">
    aggregateWeight number (0..1) <"The total weight of all the terms that went into the calculated rate.">
    calculatedRate number (0..1) <"The resulting calculated weight.">

type CalculatedRateObservations: <"Type for reporting observations that went into the final reported rate.">
    observationDates date (0..*) <"The observation date upon which the rate is observed.">
    weights number (0..*) <"The corresponding weight for each date.">
    observedRates number (0..*) <"The value observed for that date">
    processedRates number (0..*) <"The value after any processing, such as application of caps or floors.">

type CalculatedRateObservationDatesAndWeights: <"Type for reporting the observations dates and the corresponding weights going into a daily calculated rate">
    observationDates date (0..*) <"The observation date upon which the rate is observed.">
    weights number (0..*) <"The corresponding weight for each date.">
