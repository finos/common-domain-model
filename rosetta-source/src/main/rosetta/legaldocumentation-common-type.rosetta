namespace cdm.legaldocumentation.common : <"Common legal agreement concepts.">
version "${project.version}"

import cdm.base.staticdata.identifier.*
import cdm.base.staticdata.party.*
import cdm.legaldocumentation.csa.*
import cdm.legaldocumentation.master.*
import cdm.legaldocumentation.transaction.*
import cdm.mapping.config.*
import cdm.product.collateral.*

type ContractualMatrix:

    matrixType MatrixTypeEnum (1..1) <"Identifies the form of applicable matrix.">
        [metadata scheme]
    matrixTerm MatrixTermEnum (0..1) <"Defines any applicable key into the relevant matrix. For example, the Transaction Type would be the single term required for the Credit Derivatives Physical Settlement Matrix. This element should be omitted in the case of the 2000 ISDA Definitions Settlement Matrix for Early Termination and Swaptions.">
        [metadata scheme]

type ContractualTermsSupplement: <"A contractual supplement (such as those published by ISDA) and its publication date that will apply to the trade.">

    contractualTermsSupplementType ContractualSupplementTypeEnum (1..1) <"Identifies the form of applicable contractual supplement.">
        [metadata scheme]
    publicationDate date (0..1) <"Specifies the publication date of the applicable version of the contractual supplement.">

type OtherAgreement: <"A class for defining an agreement executed between parties.">

    identifier string (0..1) <"An identifier that has been created to identify the agreement.">
        [metadata scheme]
    otherAgreementType string (1..1) <"The agreement executed between the parties and intended to govern product-specific derivatives transactions between those parties.">
        [metadata scheme]
    version string (0..1) <"The version of the agreement.">
        [metadata scheme]
    date date (0..1) <"The date on which the agreement was signed.">

type Resource: <"Describes the resource that contains the media representation of a business event (i.e used for stating the Publicly Available Information). For example, can describe a file or a URL that represents the event. This type is an extended version of a type defined by RIXML (www.rixml.org).  Rosetta restricts the FpML implementation by not providing the ability to associated a document in hexadecimalBinary or base64Binary until such time that actual use cases will come up.">

    resourceId string (1..1) <"The unique identifier of the resource within the event. FpML specifies this element of type resourceIdScheme but with no specified value.">
        [metadata scheme]
    resourceType ResourceTypeEnum (0..1) <"A description of the type of the resource, e.g. a confirmation.">
        [metadata scheme]
    language string (0..1) <"Indicates the language of the resource, described using the ISO 639-2/T Code.">
        [metadata scheme]
    sizeInBytes number (0..1) <"Indicates the size of the resource in bytes. It could be used by the end user to estimate the download time and storage needs.">
    length ResourceLength (0..1) <"Indicates the length of the resource. For example, if the resource were a PDF file, the length would be in pages.">
    mimeType string (0..1) <"Indicates the type of media used to store the content. mimeType is used to determine the software product(s) that can read the content. MIME Types are described in RFC 2046.">
        [metadata scheme]
    name string (0..1) <"The name of the resource.  It is specified as a NormalizedString in FpML.">
    comments string (0..1) <"Any additional comments that are deemed necessary. For example, which software version is required to open the document? Or, how does this resource relate to the others for this event?">
    string string (0..1) <"Provides extra information as string. In case the extra information is in XML format, a CDATA section must be placed around the source message to prevent its interpretation as XML content.">
    url string (0..1) <"Indicates where the resource can be found, as a URL that references the information on a web server accessible to the message recipient.">

    condition ResourceChoice: <"Choice rule to represent an FpML choice construct. Note that FpML also provides the ability to have hexadecimalBinary or base64Binary, which have not been implemented in Rosetta until we see actual use cases.">
        optional choice string, url

type ResourceLength: <"A class to indicate the length of the resource.">

    lengthUnit LengthUnitEnum (1..1) <"The length unit of the resource. For example, pages (pdf, text documents) or time (audio, video files).">
    lengthValue number (1..1) <"The length value of the resource.">

type AgreementTerms: <"Specification of the content of a legal agreement.">

    agreement Agreement (1..1) <"Specification of the standard set of terms that define a legal agreement.">
    clauseLibrary boolean (0..1) <"Specification of whether the agreement terms have been negotiated using the clause library methodology.">
    counterparty Counterparty (2..2) <"Specification of the roles of the counterparties to the agreement.">

type LegalAgreement extends LegalAgreementBase: <"The specification of a legal agreement between two parties, being negotiated or having been executed. This includes the baseline information and the optional specialised elections">
    [metadata key]
    [rootType]

    agreementTerms AgreementTerms (0..1) <"Specification of the content of the legal agreement.">
    relatedAgreements LegalAgreement (0..*) <"Specifies the agreement(s) that govern the agreement, either as a reference to such agreements when specified as part of the CDM, or through identification of some of the key terms of those agreements, such as the type of agreement, the publisher, the vintage, the agreement identifier and the agreement date.">
    umbrellaAgreement UmbrellaAgreement (0..1) <"Specifies a set of legal entities which are part of a legal agreement beyond the two contracting parties to that agreement.">
    
    condition ConsistentlyExecutedAgreements: <"An executed agreement can only point to executed related agreements if any.">
        if relatedAgreements exists and agreementDate exists
        then relatedAgreements -> agreementDate exists

    condition AgreementVerification: <"A validation rule to ensure that the agreement elections are associated with the correct legal agreement type as specified.">
        if agreementTerms -> agreement -> securityAgreementElections exists
        then legalAgreementIdentification -> agreementName -> agreementType = LegalAgreementTypeEnum -> SecurityAgreement
        else if agreementTerms -> agreement -> creditSupportAgreementElections exists
        then legalAgreementIdentification -> agreementName -> creditSupportAgreementType = CreditSupportAgreementTypeEnum -> CreditSupportAnnex
                or legalAgreementIdentification -> agreementName -> creditSupportAgreementType = CreditSupportAgreementTypeEnum -> CreditSupportDeed
        else if agreementTerms -> agreement -> collateralTransferAgreementElections exists
        then legalAgreementIdentification -> agreementName -> creditSupportAgreementType = CreditSupportAgreementTypeEnum -> CollateralTransferAgreement
        else if agreementTerms -> agreement -> masterAgreementSchedule exists
        then legalAgreementIdentification -> agreementName -> agreementType = LegalAgreementTypeEnum -> MasterAgreement

type LegalAgreementBase: <"Specifies the legal agreement baseline information, being negotiated or having been executed. It excludes specialized elections">
    agreementDate date (0..1) <"The date on which the legal agreement has been agreed between the parties. This corresponds to the Date of Deed in an English Law document.">
    effectiveDate date (0..1) <"The date on which, or as of which, the agreement is effective, if different from the agreement date. It is expected that it will most often correspond to the agreement date, although there could be situations where the parties will explicitly agree on a distinct effective date.">
    identifier Identifier (0..*) <"The legal agreement identifier. Several identifiers can be specified.">
    legalAgreementIdentification LegalAgreementIdentification (1..1) <"The type of legal agreement, identified via a set of composable attributes: agreementName, publisher, governing law and version, e.g. ISDA 2013 Standard Credit Support Annex English Law.">
    contractualParty Party (2..2) <"The two contractual parties to the legal agreement, which reference information is positioned as part of the partyInformation attribute.">
        [metadata reference]
    otherParty PartyRole (0..*) <"The role(s) that other party(ies) may have in relation to the legal agreement, further to the contractual parties.">
    attachment Resource (0..*) <"A human readable document, for example a confirmation.">

type LegalAgreementIdentification: <"Specifies the type of legal agreement, identified via a set of composable attributes: agreementName, publisher, governing law and version, e.g. ISDA 2013 Standard Credit Support Annex English Law.">

    governingLaw GoverningLawEnum (0..1) <"The law governing the legal agreement, e.g. English Law, New York Law or Japanese Law.">
        [synonym AcadiaSoft_AM_1_0 value "governingLaw"]
    agreementName AgreementName (1..1) <"The legal agreement name, e.g. Credit Support Annex for Variation Margin.">
        [synonym AcadiaSoft_AM_1_0 value "documentName"]
    publisher LegalAgreementPublisherEnum (0..1) <"The legal agreement publisher, e.g. ISDA.">
        [synonym AcadiaSoft_AM_1_0 value "publisher"]
    vintage int (0..1) <"In the case where successive definitions of the legal agreement have been developed, the vintage identification. This is typically (but not necessarily) done by referencing the year, e.g. 2013 in the case of the ISDA 2013 Standard Credit Support Annex.">
        [synonym AcadiaSoft_AM_1_0 value "csaVersion"]

    condition CSAMarginType: <"A condition to ensure that CSA margin type is only specified if a credit support agreemnt type is specified and its published vintage year is equal to or after 2016.">
        if agreementName -> creditSupportAgreementMarginType exists
        then agreementName -> creditSupportAgreementType exists and vintage >= 2016

type AgreementName: <"Specifies the agreement name through an agreement type and optional detailed sub agreement type.">
    agreementType LegalAgreementTypeEnum (1..1) <"Specification of the legal agreement type.">
    creditSupportAgreementType CreditSupportAgreementTypeEnum (0..1) <"Specification of the credit support agreement type.">
        [metadata scheme]
    creditSupportAgreementMarginType CollateralMarginTypeEnum (0..1) <"specifies the type of margin for which a legal agreement is named.">
    contractualDefinitionsType ContractualDefinitionsEnum (0..*) <"The definitions such as those published by ISDA that will define the terms of the trade.">
        [metadata scheme]
    contractualTermsSupplement ContractualTermsSupplement (0..*) <"A contractual supplement (such as those published by ISDA) that will apply to the trade.">
    contractualMatrix ContractualMatrix (0..*) <"A reference to a contractual matrix of elected terms/values (such as those published by ISDA) that shall be deemed to apply to the trade. The applicable matrix is identified by reference to a name and optionally a publication date. Depending on the structure of the matrix, an additional term (specified in the matrixTerm element) may be required to further identify a subset of applicable terms/values within the matrix.">
    masterAgreementType MasterAgreementTypeEnum (0..1) <"Specification of the master agreement type.">
        [metadata scheme]
    masterConfirmationType MasterConfirmationTypeEnum (0..1) <"The type of master confirmation executed between the parties.">
        [metadata scheme]
    masterConfirmationAnnexType MasterConfirmationAnnexTypeEnum (0..1) <"The type of master confirmation annex executed between the parties.">
        [metadata scheme]
    otherAgreement string (0..1) <"Definition of an agreement that is not enumerated in the CDM.">
    brokerConfirmationType BrokerConfirmationTypeEnum (0..1)

    condition AgreementType: <"A condition to ensure that the agreement type specified is consistent with the detailed documentation identified.">
        if agreementType <> LegalAgreementTypeEnum -> Other
        then otherAgreement is absent
        else if agreementType <> LegalAgreementTypeEnum -> MasterAgreement
        then masterAgreementType is absent
        else if agreementType <> LegalAgreementTypeEnum -> MasterConfirmation
        then masterConfirmationType is absent and masterConfirmationAnnexType is absent
        else if agreementType <> LegalAgreementTypeEnum -> Confirmation
        then contractualDefinitionsType is absent
                and contractualTermsSupplement is absent
                and contractualMatrix is absent

    condition CreditSupportAgreement: <"A condition to ensure that the credit supoort agreement type is specified when required.">
        if agreementType = LegalAgreementTypeEnum -> CreditSupportAgreement
        then creditSupportAgreementType exists

    condition MasterConfirmation: <"If a master confirmation annex type is specified a master confirmation type must exist.">
        if masterConfirmationAnnexType exists
        then masterConfirmationType exists

    condition CSAMarginType: <"A condition to ensure that CSA margin type is only specified if a credit support agreemnt type is specified.">
        if creditSupportAgreementMarginType exists
        then creditSupportAgreementType exists

type UmbrellaAgreement: <"Specifies a set of parties which are part of a legal agreement beyond the two contracting parties to that agreement.">
    agreementSet UmbrellaAgreementSet (1..*) <"The language associated with the umbrella agreement, and which applies to all the parties to the umbrella agreement.">
    
type UmbrellaAgreementParty: <"Specifies the entities that are part of the umbrella agreement.">
    party Party (0..1) <"Specifies the party to the umbrella agreement">
    actingAs CounterpartyRoleEnum (0..1) <"Specifies the CounterpartyRoleEnum, e.g. either Party1 or Party2, that is associated to the party.">
    umbrellaPartyrole UmbrellaPartyRoleEnum (0..1) <"Represents the legal role that each defined entity to the agreement performs within the structure of that agreement.">
    nonLegalEntity NonLegalEntity (0..1) <"Specifies a non legal entity that exists in the agreement with a defined relationship to a Legal entity to the agreement.">
    parentParty Party (0..1) <"Represents the identification of a parent child relationship between two entities in the Umbrella agreement, allowing non-legal entities to be linked to the appropriate legal entity and allowing funds, portfolio or managed accounts to be linked to their applicable Investment Manager or Agent within the agreement.">

    condition ParentParty: <"Specifies that certain defined roles require that a parent party is identified.">
        if umbrellaPartyrole = UmbrellaPartyRoleEnum -> Sleeve then parentParty exists

    condition UmbrellaParty: <"Only a Party or a Non Legal entity can be set.">
        optional choice party,nonLegalEntity

    condition NonLegalEntityParent: <"Non Legal entity must have a Parent Party.">
        if nonLegalEntity exists then parentParty exists

type NonLegalEntity: <"Specifies a non legal entity that exists in the agreement with a defined relationship to a Legal entity to the agreement">
    identifier string (1..1) <"Specifies the unique identifier value for a non legal entity such as a sleeve, or sub account, that is a party to the agreement and has a relationship with a legal entity defined on the agreement.">
    identifierType string (1..1) <"Specifies the Identifier type for a non Legal entity to the agreement, for example the use of an alert code to uniquely identify a non Legal entity.">

type UmbrellaAgreementSet: <"Represents the groups of agreement term elections that exist within the agreement and to which entities that particular group of elections apply. Where all entities under the umbrella have the same set of elections only one agreement set will exist. At least one agreement set must be defined for an Umbrella agreement">
    agreement Agreement (1..1) <"Specification of the standard set of terms that define a legal agreement.">
    umbrellaAgreementParty UmbrellaAgreementParty (2..*) <"Specifies the underlying parties to the agreement that either act as legal parties to the agreement or represent legal constructs that are managed by a party to the agreement but do not represent a Legal Entity as of themselves.">
    additionalLanguage string (0..1) <"Represents any additional language that may need to be captured regarding the structure or application of terms that may exist for the agreement.">

    condition ExcludeGMSLA: <"Umbrella agreement fn=unctionality is not applicable to the GMSLA legal agreement type">
        agreement -> masterAgreementElections -> islaGmsla is absent

type ContactInformationElection: <"Specifies the party reference and the associated contact information.">
    partyReference CounterpartyRoleEnum (1..1) <"The party to which the election and details apply.">
    primaryContactInformation ContactInformation (0..1) <"The main notice/address information of the party.">

type NoticeInformationElection extends ContactInformationElection: <"Specifies the party reference and the associated notice and contact information.">
    override primaryContactInformation NoticeContactInformation (1..1) <"The main notice/address information of the party.">
    additionalContactInformation NoticeContactInformation (0..*) <"Additional notice/address information of the party.">

type NoticeContactInformation extends ContactInformation: <"Specifies notice information including address and, optionally, an associated person. This type also supports the ISDA CSA representation as a single string, through the address attribute in Contact Information.">
    naturalPerson NaturalPerson (0..*) <"Optional information about people involved in a transaction or business process.">
    additionalInformation string (0..1) <"Additional information on the recipient.">

type TransferInformationElection extends ContactInformationElection: <"Specifies the party reference and the associated transfer & contact information.">
    override primaryContactInformation TransferContactInformation (1..1) <"The main notice/address and account information of the party.">

type TransferContactInformation extends ContactInformation: <"Specifies transfer information including address and, optionally, an associated account. This type also supports the ISDA CSA representation as a single string, through the address attribute in Contact Information.">
    account Account (0..*) <"Specifies the account infomation for transfers to a party.">
    additionalInformation string (0..1) <"Additional transfer information.">

type OtherAgreementTerms: <"A class to specify a related legal agreement. For example, ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (s): Other CSA and Japanese Law CSA (VM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (o): Other CSA.">

    isSpecified boolean (1..1) <"The qualification of whether some other related agreement is specified (True) or not (False).">
    legalDocument string (0..1) <"The specification of this other agreement, when the qualification is True.">

    condition LegalDocumentNotSpecified: <"A data rule to enforce that the related legal agreement should not be referenced if it is deemed as not specified as part of the boolean attribute.">
        if isSpecified = False then legalDocument is absent

    condition LegalDocumentSpecified: <"A data rule to enforce that the related legal agreement should be referenced if it is deemed as specified as part of the boolean attribute.">
        if isSpecified = True then legalDocument exists

type Agreement: <"Specification of the standard set of terms that define a legal agreement.">
    creditSupportAgreementElections CreditSupportAgreementElections (0..1) <"Elections to specify a Credit Support Annex or Credit Support Deed for Intial or Variation Margin.">
    collateralTransferAgreementElections CollateralTransferAgreementElections (0..1) <"Elections to specify a Collateral Transfer Agreement.">
    securityAgreementElections SecurityAgreementElections (0..1) <"Elections to specify a Security agreement.">
    masterAgreementSchedule MasterAgreementSchedule (0..1) <"Elections to specify a Master Agreement Schedule.">
    transactionAdditionalTerms TransactionAdditionalTerms (0..1) <"Any additional terms which mainly intend to specify the extraordinary events that may affect a trade and the related contractual rights and obligation of the parties when this happens">
    masterAgreementElections MasterAgreementElections (0..1) <"A legal representation of the different possible master agreements">
    condition:
        one-of
