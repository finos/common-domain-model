namespace cdm.legaldocumentation.csa : <"Credit support concepts: CSA, collateral, elections, initial margin, threshold, minimum transfer amount.">
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.math.*
import cdm.base.staticdata.asset.common.*
import cdm.base.staticdata.identifier.*
import cdm.base.staticdata.party.*
import cdm.legaldocumentation.common.*
import cdm.observable.asset.*
import cdm.product.collateral.*

corpus Deed "ISDA 2016 English Law Credit Support Deed for Initial Margin" CSD_IM_English_2016
corpus Deed "ISDA 2018 English Law Credit Support Deed for Initial Margin" CSD_IM_English_2018
corpus Deed "1995 English Law Credit Support Deed" CSD_English_1995
corpus Annex "ISDA 2016 Japanese Law Credit Support Annex for Initial Margin" CSA_IM_Japanese_2016
corpus Annex "ISDA 2016 New York Law Credit Support Annex for Initial Margin" CSA_IM_NewYork_2016
corpus Annex "ISDA 2018 New York Law Credit Support Annex for Initial Margin" CSA_IM_NewYork_2018
corpus Annex "ISDA 2018 Credit Support Annex For Initial Margin" CSA_IM_2018
corpus Annex "ISDA 2016 Credit Support Annex for Initial Margin" CSA_IM_2016
corpus Annex "ISDA 2016 Credit Support Annex for Variation Margin" CSA_VM_2016
corpus Annex "1995 English Law Credit Support Annex" CSA_English_1995
corpus Annex "2016 English Law Variation Margin Credit Support Annex" CSA_VM_English_2016
corpus Annex "2016 New York Law Variation Margin Credit Support Annex" CSA_VM_NewYork_2016
corpus Annex "1994 New York Law Credit Support Annex" CSA_NewYork_1994
corpus Annex "Initial Margin Credit Support Annex" CSA_IM
corpus Annex "Variation Margin Credit Support Annex" CSA_VM
corpus Annex "ISDA Japanese Law Credit Support Annex" CSA_Japanese
corpus Annex "ISDA Japanese Law Credit Support Annex for Variation Margin" CSA_VM_Japanese
corpus Definition "2003 ISDA Collateral Asset Definitions" CollateralAssetDefinitions_2003

segment paragraph
segment clause
segment name

type CreditSupportAgreementElections: <"The set of elections which specify a Credit Support Annex or Deed.">
    csaInitialMargin CSAInitialMarginElections (0..1) <"The set of elections which specify an Initial Margin Credit Support Annex or Deed.">
    csaVariationMargin CSAVariationMarginElections (0..1) <"The set of elections which specify a Variation Margin Credit Support Annex or Deed.">
    csaLegacy CSALegacyElections (0..1) <"The set of elections which specify a Legacy (1994 or 1995) Credit Support Annex or Deed.">

    condition InitialMarginOrVariationMarginOrLegacy: <"Agreement elections must be specifed as one-of csaInitialMargin, csaVariationMargin or csaLegacy.">
        one-of

type CSAInitialMarginElections extends CSABase: <"The set of elections which specify an Initial Margin Credit Support Annex or Deed.">
    regime Regime (1..1) <"The Regime Table provision , which determines the regulatory regime(s) applicable to each of the parties to the agreement.">
    oneWayProvisions OneWayProvisions (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false).">
    generalSimmElections GeneralSimmElections (0..1) <"The specification of the ISDA SIMM Method for all Covered Transactions with respect to all Regimes.">
    identifiedCrossCurrencySwap boolean (0..1) <"The qualification of whether cross-currency swaps need to be identified in the Confirmation so that the obligations to exchange principal be disregarded for the purpose of determining the Delivery Amount or Return Amount.">
    sensitivityMethodologies SensitivityMethodologies (1..1) <"The specification of methodologies to compute sensitivities specific to the agreement.">
    fxHaircutCurrency FxHaircutCurrency (0..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">
    postingObligations PostingObligations (1..1) <"The security providers posting obligations.">
    substitutedRegime SubstitutedRegime (0..*) <"The specification of Additional regimes for purposes of determining whether a Regulatory Event has occurred.">
    coveredTransactions CoveredTransactions (0..1) <"The specification of transactions covered by the terms of the agreement.">
    rightsEvents RightsEvents (1..1) <"The bespoke provisions that might be specified by the parties to the agreement to specify the rights of Security Taker and/or Security Provider when an Early Termination or Access Condition event has occurred.">
    custodyArrangements CustodyArrangements (0..1) <"The Custodian and Segregated Account details in respect of each party to the agreement.">
    otherAgreements OtherAgreements (0..1) <"The bespoke definition of other agreement terms as specified by the parties to the agreement.">
    terminationCurrencyAmendment TerminationCurrencyAmendment (1..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Termination Currency.  Unless specified the definition of Termination Currency has the meaning specified in the Schedule to the ISDA Master Agreement.">
    minimumTransferAmountAmendment MinimumTransferAmountAmendment (1..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Minimum Transfer Amount.  Unless specified the definition of Minimum Transfer Amount in any Other Regulatory CSA has the meaning specified in such Other Regulatory CSA.">
    interpretationTerms string (0..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Interpretations.">
    processAgent ProcessAgent (1..1) <"The Process Agent that might be appointed by the parties to the agreement.">
    appropriatedCollateralValuation AppropriatedCollateralValuation (0..1) <"The election for the Valuation of Appropriate Collateral.">
    jurisdictionRelatedTerms JurisdictionRelatedTerms (0..1) <"The jurisdiction specific terms relevant to the agreement.">
    additionalAmendments string (0..1) <"Any additional amendments that might be specified by the parties to the agreement.">
    additionalBespokeTerms string (0..1) <"Any additional terms that might be specified applicable.">
    trustSchemeAddendum boolean (1..1) <"The qualification of whether Trust Scheme Addendum is applicable (True) or not applicable (False).">
    cass string (0..1) <"Details of language relating to Client Asset and Money Rules.">
    creditSupportObligations CreditSupportObligationsInitialMargin (1..1) <"The Credit Support Obligations applicable to the agreement.">
    calculationAndTiming CalculationAndTimingInitialMargin (1..1) <"The set of elections for determining Valuation and Timing terms specific to the agreement.">

type CSAVariationMarginElections extends CSABase: <"The set of elections which specify a Variation Margin Credit Support Annex or Deed.">
    fxHaircutCurrency FxHaircutCurrency (0..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">
    exchangeDate string (0..1) <"The bespoke exchange date terms that might be specified by the parties to the agreement.">
    coveredTransactions CoveredTransactions (1..1) <"The specification of transactions covered by the terms of the agreement.">
    creditSupportOffsets boolean (1..1) <"The specification of whether the standard Credit Support Offset provisions are applicable (true) or not applicable (false).">
    custodyArrangements CustodyArrangements (0..1) <"The Custodian and Segregated Account details in respect of each party to the agreement.">
    addressesForTransfer ContactElection (0..1) <"The optional specification of address for transfer as specified by the respective parties to the agreement.">
    otherAgreements OtherAgreements (0..1) <"The bespoke definition of other agreement terms as specified by the parties to the agreement.">
    securityInterestForObligations SecurityInterestForObligations (1..1) <"The party to whom obligations under the Master Agreement are owed and secured by the credit support arrangements. Applicable to 1994 NY CSA, 1995 English Law CSD and 2016 New York Law VM CSA.">
    creditSupportObligations CreditSupportObligationsVariationMargin (1..1) <"The Credit Support Obligations applicable to the agreement.">
    calculationAndTiming CalculationAndTimingVariationMargin (1..1) <"The set of elections for determining Valuation and Timing terms specific to the agreement.">

type CSALegacyElections extends CSABase: <"The set of elections which specify a Legacy (1994 or 1995) Credit Support Annex or Deed.">
    exchangeDate string (0..1) <"The bespoke exchange date terms that might be specified by the parties to the agreement.">
    addressesForTransfer ContactElection (0..1) <"The optional specification of address for transfer as specified by the respective parties to the agreement.">
    securityInterestForObligations SecurityInterestForObligations (0..1) <"The party to whom obligations under the Master Agreement are owed and secured by the credit support arrangements. Applicable to 1994 NY CSA, 1995 English Law CSD and 2016 New York Law VM CSA.">
    singlePostingParty SinglePostingParty (0..1) <"Where only a single  party is explicitly designated to post collateral, the party which is specified as the sole poster of collateral.">
    creditSupportObligations CreditSupportObligationsLegacy (1..1) <"The Credit Support Obligations applicable to the agreement.">
    calculationAndTiming CalculationAndTimingLegacy (1..1) <"The set of elections for determining Valuation and Timing terms specific to the agreement.">

type CSABase: <"A logical container designed to hold a set of related data. In this case, designed to hold a set of clauses that are common amongst Legacy (1994/95), Variation Margin and Initial Margin Credit Support Documents.">
    baseAndEligibleCurrency BaseAndEligibleCurrency (1..1) <"The base and eligible currency(ies) for the document as specified by the parties to the agreement.">
    conditionsPrecedent ConditionsPrecedent (0..1) <"The set of elections that may overwrite the default Condition Precedent provision, and the set of provisions that are deemed Access Condition.">
    substitution Substitution (0..1) <"The conditions under which the Security Provider can substitute posted collateral.">
    disputeResolution DisputeResolution (1..1) <"The election terms under which a party disputes (i) the Calculation Agents calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support.">
    holdingAndUsingPostedCollateral HoldingAndUsingPostedCollateral (0..1) <"The elections for the holding and using of posted collateral by the respective parties to the Credit Support Annex for Variation Margin.">
    distributionAndInterestPayment DistributionAndInterestPayment (0..1) <"The Distributions and Interest Payment terms specified as part of the agreement.">
    otherEligibleAndPostedSupport OtherEligibleAndPostedSupport (0..1) <"The Other Eligible Support elections associated with margin agreements.">
    demandsAndNotices DemandsAndNotices (0..1) <"The optional specification of address where the demands, specifications and notices will be communicated to for each of the parties to the agreement.">
    additionalRepresentations AdditionalRepresentations (0..1) <"The specification Additional Representations that may be applicable to the agreement.">
    datedAsOfDate DatedAsOfDate (0..1) <"Defines the dates agreed by the parties as the date of the Credit Support Annex or Credit Support Deed.">
    finalReturns FinalReturns (1..1) <"A clause providing that final return collateral is to be provided without consideration of any Minimum Transfer Amount and collateral haircut.">

type CollateralTransferAgreementElections: <"The set of elections which specify a Collateral Transfer Agreement.">
    regime Regime (1..1) <"The Regime Table provision , which determines the regulatory regime(s) applicable to each of the parties to the agreement.">
    oneWayProvisions OneWayProvisions (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false).">
    generalSimmElections GeneralSimmElections (0..1) <"The specification of the ISDA SIMM Method for all Covered Transactions with respect to all Regimes.">
    identifiedCrossCurrencySwap boolean (0..1) <"The qualification of whether cross-currency swaps need to be identified in the Confirmation so that the obligations to exchange principal be disregarded for the purpose of determining the Delivery Amount or Return Amount.">
    sensitivityMethodologies SensitivityMethodologies (1..1) <"The specification of methodologies to compute sensitivities specific to the agreement.">
    fxHaircutCurrency FxHaircutCurrency (0..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">
    postingObligations PostingObligations (1..1) <"The security providers posting obligations.">
    substitutedRegime SubstitutedRegime (0..*) <"The specification of Additional regimes for purposes of determining whether a Regulatory Event has occurred.">
    baseAndEligibleCurrency BaseAndEligibleCurrency (1..1) <"The base and eligible currency(ies) for the document as specified by the parties to the agreement.">
    creditSupportObligations CreditSupportObligationsCollateralTransferAgreement (1..1) <"The Credit Support Obligations applicable to the agreement.">
    calculationAndTiming CalculationAndTimingCollateralTransferAgreement (1..1) <"The set of elections for determining Valuation and Timing terms specific to the agreement.">
    conditionsPrecedent ConditionsPrecedent (0..1) <"The set of elections that may overwrite the default Condition Precedent provision, and the set of provisions that are deemed Access Condition.">
    substitution Substitution (0..1) <"The conditions under which the Security Provider can substitute posted collateral.">
    disputeResolution DisputeResolution (1..1) <"The election terms under which a party disputes (i) the Calculation Agents calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support.">
    rightsEvents RightsEvents (0..1) <"The bespoke provisions that might be specified by the parties to the agreement to specify the rights of Security Taker and/or Security Provider when an Early Termination or Access Condition event has occurred.">
    custodyArrangements CustodyArrangements (0..1) <"The Custodian and Segregated Account details in respect of each party to the agreement.">
    additionalRepresentations AdditionalRepresentations (0..1) <"The specification Additional Representations that may be applicable to the agreement.">
    demandsAndNotices DemandsAndNotices (0..1) <"The optional specification of address where the demands, specifications and notices will be communicated to for each of the parties to the agreement.">
    addressesForTransfer ContactElection (0..1) <"The optional specification of address for transfer as specified by the respective parties to the agreement.">
    otherAgreements OtherAgreements (0..1) <"The bespoke definition of other agreement terms as specified by the parties to the agreement.">
    terminationCurrencyAmendment TerminationCurrencyAmendment (1..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Termination Currency.  Unless specified the definition of Termination Currency has the meaning specified in the Schedule to the ISDA Master Agreement.">
    minimumTransferAmountAmendment MinimumTransferAmountAmendment (1..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Minimum Transfer Amount.  Unless specified the definition of Minimum Transfer Amount in any Other Regulatory CSA has the meaning specified in such Other Regulatory CSA.">
    interpretationTerms string (0..1) <"The bespoke provision that might be specified by the parties to the agreement applicable to Interpretations.">
    processAgent ProcessAgent (0..1) <"The Process Agent that might be appointed by the parties to the agreement.">
    jurisdictionRelatedTerms JurisdictionRelatedTerms (0..1) <"The jurisdiction specific terms relevant to the agreement.">
    additionalAmendments string (0..1) <"Any additional amendments that might be specified by the parties to the agreement.">
    additionalBespokeTerms string (0..1) <"Any additional terms that might be specified applicable.">
    pledgeeRepresentativeRider PledgeeRepresentativeRider (0..1) <"The terms of the Rider for the ISDA Euroclear 2019 Collateral Transfer Agreement with respect to the use of a Pledgee Representative attached to this Agreement.">
    finalReturns FinalReturns (0..1) <"A clause providing that final return collateral is to be provided without consideration of any Minimum Transfer Amount and collateral haircut.">

type SecurityAgreementElections: <"The set of elections which specify a Security Agremeent.">
    pledgedAccount Account (0..1) <"The pledged account associated with the agreement.">
    enforcementEvent EnforcementEvent (0..1) <"Enforcement Events specific to the agreement.">
    deliveryInLieuRight boolean (0..1) <"Delivery In Lieu rights.">
    fullDischarge boolean (0..1) <"Full Discharge condition.">
    appropriatedCollateralValuation AppropriatedCollateralValuation (0..1) <"The election for the Valuation of Appropriate Collateral.">
    processAgent ProcessAgent (0..1) <"The Process Agent that might be appointed by the parties to the agreement.">
    jurisdictionRelatedTerms JurisdictionRelatedTerms (0..1) <"The jurisdiction specific terms.">
    additionalAmendments string (0..1) <"Any additional amendments that might be specified by the parties to the agreement.">
    additionalBespokeTerms string (0..1) <"Any additional terms that might be specified applicable.">
    executionTerms ExecutionTerms (0..1) <"The location and language of execution to determine duty to be paid.">

type PostedCreditSupportItem: <"Posted Credit Support item with corresponding Valuation Percentage, FX Haircut Percentage and any related disputed Posted Credit Support valuation.">
    cashOrSecurityValue Money (1..1) <"The Base Currency Equivalent of Cash or Security.">
    haircutPercentage number (1..1) <"Valuation Percentage means, for any item of Eligible Collateral (IM), the percentage specified in accordance with Paragraph 13.">
    fxHaircutPercentage number (0..1) <"FX Haircut Percentage means, for any item of Eligible Collateral (IM), the percentage specified in accordance with Paragraph 13.">
    additionalHaircutPercentage number (0..1) <"Percentage value of any additional haircut to be applied to a collateral asset,the percentage value is expressed as the discount haircut to the value of the collateral- as an example a 5% haircut would be expressed as 0.05.">
    disputedCashOrSecurityValue Money (1..1) <"Paragraph 5. If a party (a 'Disputing Party') disputes the Value of any Posted Credit Support (IM).">

type AccessConditions: <"A class to specify each party's election with respect to the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA).">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(e) (ii)"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(f) (ii)"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(e) (ii)"]
    [docReference ISDA CSA_VM_2016 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
    partyElection AccessConditionsElections (2..2) <"The parties' Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA) election.">
    additionalTerminationEvent AdditionalTerminationEvent (0..*) <"Additional Termination Events applicable to the agreement.">

type AccessConditionsElections: <"A class to specify the Termination Events that will be deemed an Access Condition or a Specified Condition.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(e) (ii)"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(f) (ii)"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(e) (ii)"]
    [docReference ISDA CSA_VM_2016 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    illegality boolean (1..1) <"Represents an illegality Termination Event.">
    forceMajeure boolean (1..1) <"Represents a force Majeure Termination Event.">
    taxEvent boolean (1..1) <"Represents a Tax Termination Event.">
    taxEventUponMerger boolean (1..1) <"Represents a Tax Event Upon Merger Termination Event.">
    creditEventUponMerger boolean (1..1) <"Represents a Credit Event Upon Merger Termination Event.">

type AdditionalRepresentation: <"A class to specify the Additional Representation.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(o)" name "Additional Representations(s)"]
    partyElection AdditionalRepresentationElection (2..2) <"A qualification as to whether the Additional Representation is applicable.">
    customElection string (0..1) <"A supplemental custom election that might be specified by the parties for the purpose of specifying the Additional Representation.">

type AdditionalRepresentationElection: <"A class to specify the parties' Additional Representation(s) election.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    isApplicable boolean (1..1) <"The Additional Representation is applicable when True, and not applicable when False.">

type AdditionalRepresentations: <"A class to specify Additional Representations that may be applicable to an agreement.">
    additionalRepresentation AdditionalRepresentation (0..1) <"The specification of the Additional Representation that may be applicable to the agreement.">
    regulatoryComplianceRepresentation boolean (0..1) <"The qualification of whether Additional Information related to Regulatory Compliance and Concentration Limits is applicable or not.">

    condition:
        one-of

type AdditionalRightsEvent: <"A class to specify the Pledgor/Obligor/Chargor Additional Rights Event election.">
    [docReference ISDA CSD_IM_English_2016 paragraph  "13 General Principles" clause "(k)" name "Chargor Additional Rights Event"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph  "13 General Principles" clause "(k)" name "Obligor Additional Rights Event"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph  "13 General Principles" clause "(k)" name "Pledgor Additional Rights Event"]
    isApplicable boolean (1..1) <"The Pledgor Additional Rights Event election is applicable when True, and not applicable when False.">
    qualification string (0..1) <"The qualification of the Pledgor Additional Rights Event election, when specified.">

    condition Qualification: <"The Pledgor/Obligor/Chargor Additional Rights should be qualified only when the Pledgor Additional Rights Event election is specified as applicable.">
        if qualification exists then isApplicable = True

type AdditionalTerminationEvent: <"A class to specify an optional termination event, additional to the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA).">
    name string (1..1) <"The name of the additional termination event.">
    applicableParty CounterpartyRoleEnum (1..2) <"Whether the additional termination event is applicable for the relevant party.">

type AdditionalType: <"The specification of the Additional Type of transaction that can require the collection or delivery of initial margin under a given regulatory regime for the purposes of Covered Transactions.">
    [docReference ISDA CSA_IM_2016 paragraph  "13 General Principles" clause "(b)(B)"]
    standardValue AdditionalTypeEnum (1..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a standard value.">
    customValue string (0..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a custom value by the parties to the legal agreement.">

    condition CustomValue: <"The specification of a custom value by the parties to the legal agreement takes place alongside the qualification of the `Other` value as part of the AdditionalTypeEnum.">
        if standardValue = AdditionalTypeEnum -> Other
        then customValue exists

    condition StandardValue: <"The specification of a standard value by the parties to the legal agreement is done through the qualification of a value distinct than `Other` as part of the AdditionalTypeEnum, and implies that the customerValue is not being qualified.">
        if standardValue <> AdditionalTypeEnum -> Other
        then customValue is absent

type AmendmentEffectiveDate: <"A class to specify the effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision.">

    date date (0..1) <"The effective date of the Amendment to Termination Currency when specified as an actual date.">
    specificDate AmendmentEffectiveDateEnum (0..1) <"The effective date of the Amendment to Termination Currency when specified as relative to another date (e.g. the annex date).">
    customProvision string (0..1) <"The effective date of the Amendment to Termination Currency when specified as a non normalized custom provision.">

    condition:
        one-of

type ApplicableRegime: <"A class to specify the applicable regulatory regime(s) that parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin, might be subject to.">

    regime RegulatoryRegimeEnum (0..1) <"The applicable regulatory regime, as specified through an enumeration.">
    additionalRegime string (0..1) <"The additional regulatory regime as specified by the parties.">
    regimeTerms RegimeTerms (2..2) <"A class that is used by the ApplicableRegime and the AdditionalRegime classes to specify the terms that are specific to each party and regime which are referred to in the Regime Table as part of certain legal agreements, such as the ISDA 2016 and 2018 CSA for Initial Margin.">
    additionalType AdditionalTypeEnum (0..1)
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(b)(B)"]
    additionalTerms string (0..1) <"The bespoke Additional Type for the purposes of Covered Transactions (IM).">

    condition ApplicableRegimeChoice: <"The applicable regime should be specified either as an enumeration or as an additional regime specified by the parties.">
        required choice regime, additionalRegime

type AppropriatedCollateralValuation: <"A class to specify the Valuation of Appropriated Collateral.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(u)" name "Valuation of Appropriated Collateral"]

    isSpecified boolean (1..1) <"The qualification of whether the Valuation of Appropriate Collateral provision is applicable (True) or not applicable (False).">
    election string (0..1) <"The parties' election that qualify the Valuation of Appropriate Collateral in the case where it is deemed applicable.">

    condition Specified: <"The Valuation of Appropriate Collateral election must be specified when it is applicable.">
        if isSpecified = True then election exists

    condition NotSpecified: <"The Valuation of Appropriate Collateral election cannot be specified when it is not applicable.">
        if isSpecified = False then election is absent

type BespokeCalculationDate: <"A class to specify bespoke Calculation Date terms for the purposes of Initial Margin.">

    isApplicable boolean (1..1) <"Additional Calculation Date terms are applicable when True and not applicable when False.">
    calculationDateImTerms string (0..1) <"The Additional Calculation Date terms for the purposes of Initial Margin.">

    condition CalculationDateImTerms: <"A data rule to enforce that Additional Calculation Date Terms should be absent when not applicable.">
        if isApplicable = False then calculationDateImTerms is absent

type BespokeCalculationTime: <"A class to specify additional Calculation Time terms for the purposes of Initial Margin.">

    asCalculationAgent boolean (1..1) <"If set to True, the Calculation Time for Initial Margin is the time as of which the Calculation Agent (IM) computes its end of day valuations of derivatives transactions.">
    bespokeCalculationTimeTerms string (0..1) <"Additional Terms applicable to Calculation Time for Initial Margin.">

    condition AsCalculationAgentIm: <"A data rule to enforce that the terms applicable to Calculation Time for Initial Margin should be specified when the computation time is not as per Calculation Agent.">
        if asCalculationAgent = True
        then bespokeCalculationTimeTerms is absent

    condition BespokeCalculationTimeTerms: <"A data rule to enforce that the Calculation Time for Initial Margin shouldn't be specified when the Control Agreement isn't deemed a Credit Support Document with respect to the party(ies).">
        if asCalculationAgent = False
        then bespokeCalculationTimeTerms exists

type BespokeTransferTiming: <"A class to specify any bespoke Transfer Timing language by each party to the agreement.">

    isApplicable boolean (1..1) <"A boolean flag to specify whether bespoke transfer terms are applicable or not.">
    bespokeTransferTimingTerms string (0..1) <"The bespoke transfer timing terms applicable to the agreement.">

type CalculationAgentTerms: <"A class to specify Calculation Agent for purposes of Initial or Variation Margin agreements.">
    party CalculationValuationAgentPartyEnum (0..1) <"The party which is specified as Calculation Agent for Initial Margin.">
    bespokeCalculationAgentTerms string (0..1) <"The Calculation Agent (IM) terms when specified.">

    condition:
        one-of

type CalculationAndTimingLegacy extends CalculationAndTimingBase: <"Specification of the Calculation, Valuation and Timing terms specific to the Legacy (1994/95) Credit Support Annex.">
    valuationDate CSAValuationDate (1..1) <"Details of the days on which calculations are to be made to determine the Delivery Amount and/or Return Amount.">
    valuationTime ValuationTime (1..1) <"Specifies the time on which the value of collateral or exposure are calculated in accordance with the agreement.">
    valuationAgent ValuationAgent (1..1) <"Details of the party calculating the value of collateral to be delivered or returned.">

type CalculationAndTimingVariationMargin extends CalculationAndTimingBase: <"Specification of the Calculation, Valuation and Timing terms specific to the Variation Margin Credit Support Annex.">
    valuationDate CSAValuationDate (1..1) <"Details of the days on which calculations are to be made to determine the Delivery Amount and/or Return Amount.">
    valuationTime ValuationTime (1..1) <"Specifies the time on which the value of collateral or exposure are calculated in accordance with the agreement.">
    valuationAgent ValuationAgent (1..1) <"Details of the party valuating the collateral to be delivered or returned.">
    valuationDateLocation ValuationCalculationDateLocation (1..1) <"The specified location where the credit exposure will be calculated by the respective parties.">

type CalculationAndTimingInitialMargin extends CalculationAndTimingBase: <"Specification of the Calculation, Valuation and Timing terms specific to the Initial Margin Credit Support Annex.">
    calculationAgentTerms CalculationAgentTerms (1..1) <"Details of the party calculating the value of collateral to be delivered or returned.">
    bespokeCalculationDate BespokeCalculationDate (0..1) <"The specification of bespoke Calculation Date terms for the purposes of Initial or Variation Margin by the parties to the agreement.">
    bespokeCalculationTime BespokeCalculationTime (0..1) <"Bespoke terms to describe the time as of which such party (or the Calculation Agent (IM) (if applicale)) computes its end of day valuations of derivatives transactions.">
    calculationDateLocation ValuationCalculationDateLocation (1..1) <"The specified location where the credit exposure will be calculated by the respective parties.">
    cashSettlementDay string (0..1)
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "4" clause "(b)(i)"]
    securitiesSettlementDay string (0..1)
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "12"]

type CalculationAndTimingCollateralTransferAgreement extends CalculationAndTimingBase: <"Specification of the Calculation, Valuation and Timing terms specific to the Initial Margin Credit Support Annex.">
    calculationAgentTerms CalculationAgentTerms (0..1) <"Details of the party calculating the value of collateral to be delivered or returned.">
    bespokeCalculationDate BespokeCalculationDate (0..1) <"The specification of bespoke Calculation Date terms for the purposes of Initial or Variation Margin by the parties to the agreement.">
    bespokeCalculationTime BespokeCalculationTime (0..1) <"Bespoke terms to describe the time as of which such party (or the Calculation Agent (IM) (if applicale)) computes its end of day valuations of derivatives transactions.">
    calculationDateLocation ValuationCalculationDateLocation (0..1) <"The specified location where the credit exposure will be calculated by the respective parties.">
    cashSettlementDay string (0..1)
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "4" clause "(b)(i)"]
    securitiesSettlementDay string (0..1)
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "12"]

type CalculationAndTimingBase: <"A logical container designed to hold a set of related data. In this case, designed to hold a set of clauses that are common amongst Legacy (1994/95), Variation Margin and Initial Margin Credit Support Documents in regards to Calculation, Valuation and Timing terms.">
    notificationTime NotificationTime (1..1) <"The time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions.">

type CalculationCurrencyElection: <"A class to specify the ISDA SIMM Calculation Currency.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ee)(3"]
    party CounterpartyRoleEnum (1..1) <"The party which the SIMM Calculation Currency qualification applies to.">
    isBaseCurrency boolean (1..1) <"The SIMM Calculation Currency (also known as SIMM Reporting Currency) means the Base Currency when True. It means a different currency when False. In that latter case, the SIMM Calculation Currency is specified as part of the currency attribute.">
    currency string (0..1) <"The currency in which the ISDA SIMM Calculation is denominated, when different from the Base Currency. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">
        [metadata scheme]

    condition BaseCurrency: <"A data rule to enforce that, when the SIMM calculation currency is specified as the Base Currency and vice versa.">
        if isBaseCurrency = True
        then currency is absent
        else currency exists

type ValuationCalculationDateLocation: <"Specification of the Calculation Date Location for the respective parties to the legal agreement.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(d)(i)" name "Calculation Date Location"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(e)(i)" name "Calculation Date Location"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(d)(i)" name "Calculation Date Location"]
    partyElection ValuationCalculationDateLocationElection (2..2) <"The parties' calculation date location election.">

type ValuationCalculationDateLocationElection: <"A class to specify each of the party elections with respect to the Calculation Date Location.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(d)(i)" name "Calculation Date Location"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(e)(i)" name "Calculation Date Location"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(d)(i)" name "Calculation Date Location"]
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    businessCenter BusinessCenterEnum (0..1) <"The Calculation Date Location when specified as a business center which corresponds to the FpML list of business centers or can be mapped to it.">
        [metadata scheme]
    customLocation string (0..1) <"The Calculation Date Location when specified a location which doesn't correspond to the FpML list of business centers or cannot be mapped to it.">

    condition Choice: <"The Calculation Date Location is specified either as a standard business center or as a custom location.">
        required choice businessCenter, customLocation

type CollateralAccessBreach: <"A class to specify Collateral Access Breach language.">
    isApplicable boolean (1..1) <"Collateral Access Breach terms are applicable when True and not applicable when False.">
    cabEndDateElection boolean (0..1) <"Determination of whether the Collateral Access Breach end date is a number of days (True) or specified (False).">
    cabEndDate number (0..1) <"The business days following the related Collateral Access Breach when the additional terms end.">
    cabEndDateTerms string (0..1) <"Specific terms for when Collateral Access Breach terms end.">

    condition CabEndDateTerms: <"A condition to require Collateral Access Breach End Date Terms when a specification is required.">
        if cabEndDateElection = True then cabEndDateTerms is absent

type CollateralManagementAgreement: <"A class to specify the Collateral Management Agreement election.">
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(b)(i)" name "Collateral Management Agreement"]
    partyElection CollateralManagementAgreementElection (2..2) <"The parties' Collateral Management Agreement election.">

type CollateralManagementAgreementElection: <"A class to specify the Collateral Management Agreement election.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(b(i)" name "Collateral Management Agreement"]
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    collateralManagementAgreement string (1..1) <"The designated Collateral Management Agreement with respect to the elective party as the Obligee.">

type CollateralRounding: <"A class to specify the rounding methodology applicable to the Delivery Amount and the Return Amount.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(c)(vi)( C )" name "Rounding"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(d)(vi)( C )" name "Rounding"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(c)(vi)( C )" name "Rounding"]
    deliveryAmount number (1..1) <"The nearest integral multiple of Base Currency units to which the Delivery Amount will be rounded in accordance with the specified rounding direction.">
    deliveryDirection RoundingModeEnum (1..1) <"The rounding rule applicable to the Delivery Amount (which can be (i) up to nearest; (ii) down to nearest).">
    returnAmount number (1..1) <"The nearest integral multiple of Base Currency units to which the Return Amount will be rounded in accordance with the specified rounding direction.">
    returnDirection RoundingModeEnum (1..1) <"The rounding rule applicable to the Return Amount (which can be (i) up to nearest; (ii) down to nearest).">
    currency ISOCurrencyCodeEnum (1..1) <"The currency in which the Delivery Amount and Return Amount rounding amounts are specified.">
    other string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">

type ConditionsPrecedent: <"A class to specify the two set of elections that may overwrite the default Condition Precedent provision.">
    [docReference ISDA CSA_IM_2016 paragraph "4" clause "(a)"]
    [docReference ISDA CSA_VM_2016 paragraph "4" clause "(a)"]
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(e)" name " Conditions Precedent"]
    [docReference ISDA CSD_IM_English_2018 paragraph "13 General Principles" clause "(e)" name " Conditions Precedent"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(f)" name " Conditions Precedent"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(e)" name " Conditions Precedent"]
    [docReference ISDA CSA_VM_2016 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies"]
    [docReference ISDA CSD_English_1995 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(d)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
    conditionsPrecedentElection ExceptionEnum (0..1) <"The election to specify whether the standard Conditions Precedent apply.">
    customProvision string (0..1) <"The custom provisions that might be specified by the parties to the agreement for the purpose of overwriting the default Condition Precedent provision specified in the CSA.">
        [docReference ISDA CSA_IM_2016 paragraph "4" clause "(a)"]
        [docReference ISDA CSA_VM_2016 paragraph "4" clause "(a)"]
        [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(e)(i)"]
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(f)(i)"]
        [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(e)(i)"]
        [docReference ISDA CSA_VM_2016 paragraph "13" name "Conditions Precedent and Secured Partys Rights and Remedies"]
    accessConditions AccessConditions (1..1) <"The parties' election with respect to the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA).">
        [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(e)(ii)"]
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(f)(ii)"]
        [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(e)(ii)"]
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
    specifiedCondition SpecifiedCondition (0..1) <"Specifies events elected by the parties, that if triggered, can temporarily or permanently suspend rights to rehypothecate, transfer or substitute collateral.">
        [docReference ISDA CSD_English_1995 paragraph "13" clause "(e)" name "Conditions Precedent and Secured Partys Rights and Remedies."]
        [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(d)" name "Conditions Precedent and Secured Partys Rights and Remedies."]

    condition CustomProvision: <"When a Custom Provision is specified then the Conditions Precedent Election should be absent.">
        if customProvision exists
        then conditionsPrecedentElection = ExceptionEnum -> Other

type ControlAgreement: <"A class to specify the relationship between the Control Agreement and the Credit Support Agreement.">
    partyElection ControlAgreementElections (2..2) <"The party specific elections.">

type ControlAgreementElections: <"A class to specify the Control Agreement election sby each party to the agreement.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    controlAgreementAsCsd boolean (1..1) <"The identification of whether the Control Agreement is a Credit Support Document with respect to each party.">
    consistencyWithControlAgreement boolean (0..1) <"Unless specified as inapplicable in the event of any inconsistency between this Deed and the Control Agreement, this Deed will prevail over the Control Agreement.">
    relationshipWithControlAgreement boolean (0..1) <"Unless specified as inapplicable the parties recognise that the Control Agreement is a means by which the parties can perform their obligations.">

type ControlAgreementNecEvent: <"A class to specify Control Agreement language related to delivery of a Notice of Exclusive Control.">
    controlAgreementNecEventElection ControlAgreementNecEventElection (2..2) <"Indicates party specific Control Agreement language related to delivery of a Notice of Exclusive Control.">

type ControlAgreementNecEventElection: <"A class to specify party specific Control Agreement language related to delivery of a Notice of Exclusive Control.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    necEvent boolean (1..1) <"Indicates Control Agreement language related to delivery of a Notice of Exclusive Control.">

type Custodian: <"A class to specify the custodian and custody account details for each party to the agreement.">
    partyElection CustodianElection (2..2) <"The party specific elections.">

type CustodianElection: <"A class to specify the custodian and custody account details for each party to the agreement.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    custodian LegalEntity (0..1) <"The custody agent. While CreateiQ only specifies the custodian's name, specifying the legal entity as part of the CDM is deemed more appropriate, while this will still provide the ability to accommodate situations where only the entity name is available, as the entityId attribute is optional as part of the LegalEntity class.">
    segregatedCashAccount Account (0..1) <"The identification of the segregated cash account for the purpose of holding cash collateral.">
    segregatedSecurityAccount Account (0..1) <"The identification of the segregated security account for the purpose of holding security collateral.">
    legacyVMCustodian LegacyVMCustodianEnum (0..1) <"Details how the Custodian is determined.">
    legacyNamedEntity LegalEntity (0..1) <"The identity of the relevant custodian where Named Entity is elected.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition NamedEntity: <"The entity should be named if this is specified in the determination.">
        if legacyVMCustodian = LegacyVMCustodianEnum -> NamedEntity
        then legacyNamedEntity exists

    condition AdditionalLanguage: <"If no Custodian determination methodology is defined, additional language must be specified.">
        if legacyVMCustodian is absent then additionalLanguage exists

type CustodianEvent: <"A class to specify the Custodian Event.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(n)(iii)" name " Custodian Event"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(m)(ii)" name "Collateral Manager Event"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(n)(iii)" name "Custodian Event"]

    isApplicable boolean (1..1) <"The qualification as to whether the Custodian Event (English Law & New York Law ISDA CSA) or the Collateral Manager Event (Japanese Law ISDA CSA) is applicable.">
    endDate CustodianEventEndDate (0..1) <"The qualification of the Custodian Event (English Law & New York Law ISDA CSA) or Collateral Manager Event (Japanese Law ISDA CSA) End Date.">

type CustodianEventEndDate: <"A class to specify the Custodian Event.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(n)(iii)" name "CE End Date"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(m)(ii)" name "CME End Date"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(n)(iii)" name "CE End Date"]

    daysAfterCustodianEvent CustomisableOffset (1..1) <"Election to specify the number of days after the occurrence of the Custodian Event/Collateral Management Event for the purpose of qualifying the CE/CME End Date.">
        [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(n)(iii)" name " Custodian Event"]
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(m)(ii)" name "Collateral Manager Event"]
        [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(n)(iii)" name " Custodian Event"]
    releaseDate CustomisableOffset (1..1) <"Election to specify the number of days prior to the termination of the Control Agreement /Collateral Management Event for the purpose of qualifying the CE/CME End Date, in the case where advance notice is given.">
        [docReference ISDA CSD_IM_English_2016]
        [docReference ISDA CSA_IM_Japanese_2016]
        [docReference ISDA CSA_IM_NewYork_2016]
    safekeepingPeriodExpiry CustomisableOffset (0..1) <"The parties' election to specify the number of days prior to the end of the safekeeping period (Clearstream CTA) purpose of qualifying the CE/CME End Date, in the case where advance notice is given.">
    dateOfTimelyStatement CustomisableOffset (1..1) <"The parties' election to specify the number of days one party has effectively provided the Timely Statement to the other party.">

type CustodianRisk: <"A class to specify the Custodian Risk elections specific to a Credit Support Agreement.">

    partyElection CustodianRiskElection (1..2) <"The party specific elections.">

type CustodianRiskElection: <"A class to specify the Custodian Risk.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]
    [docReference ISDA CSD_IM_English_2018 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(m)(ii)" name "Collateral Manager Risk"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]

    party CounterpartyRoleEnum (0..1) <"The elective party.">
    isSpecified boolean (1..1) <"The qualification as to whether the risk is deemed as Specified.">
    qualification string (0..1) <"The Custodian Risk or Collateral Manager Risk qualification.">
        [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]
        [docReference ISDA CSD_IM_English_2018 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(m)(ii)" name "Collateral Manager Risk"]
        [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]

    condition Specified: <"The Custodian Risk (English Law and New York Law ISDA CSA) or Collateral Manager Risk (Japanese Law ISDA CSA) should only be qualified if that risk is deemed 'specified'.">
        if isSpecified = False then qualification is absent

type CustodianTerms: <"A class to specify the requirements applicable to the custodian with respect to the holding of posted collateral.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause " (h)(i)" name "Eligibility to Hold Posted Collateral (VM) Custodians (VM)"]
    minimumAssets Money (0..1) <"The minimal level of assets requirement with respect to the custody agent.">
    minimumRating CreditNotation (0..1) <"The minimal rating requirement with respect to the custody agent.">
    initialDesignation LegalEntity (0..1) <"Election to specify the initial custodian.">
        [docReference ISDA CSA_VM_2016]

type CustodyArrangements: <"A class to specify the Custody Arrangements for the agreement.">

    isCreditSupportDocument boolean (0..1) <"Unless specified as True, the Control Agreement is not a Credit Support Document under the agreement with respect to a party.">
        [docReference ISDA CSA_VM_2016 paragraph "Paragraph 6" clause " (e) " name "The Control Agreement as a Credit Support Document"]
    hasControlAgreementLanguage boolean (0..1) <"Control Agreement language is specified when True.">
    otherProvisions string (0..1) <"Other Custom Provisions of Custody Arrangements.">
        [docReference ISDA CSA_IM_2016 paragraph "3 General Principles" clause "(n)(vii)" name "Other Provisions"]
    collateralAccessBreach CollateralAccessBreach (0..1) <"The elections specific to Collateral Access Breach language.">
    custodianRisk CustodianRisk (0..1) <"The qualification of the Custodian Risk.">
        [docReference ISDA CSA_IM_2016 paragraph "3 General Principles" clause "(n)(ii)" name "Custodian (IM) Risk"]
    controlAgreement ControlAgreement (0..1) <"The party-specific election with respect to the control agreement.">
    custodianEvent CustodianEvent (1..1) <"Specifies if and until when a Custodian Event clause applies.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(m)(iii)" name "Custodian Event"]
    custodian Custodian (0..1) <"The custodian and segregated account details for each party to the agreement.">
    collateralManagementAgreement CollateralManagementAgreement (0..1)
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(b)(i)" name "Collateral Management Agreement"]

type DisputeResolution: <"A class to specify the election terms under which a party disputes the Value of any Transfer of Eligible Credit Support or Posted Credit Support.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(g)" name "Dispute Resolution"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(h)" name "Dispute Resolution"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(g)" name "Dispute Resolution"]
    resolutionTime BusinessCenterTime (0..1) <"The time by which the dispute needs to be resolved, failure of which would trigger a recalculation alongside a process that is specified as part of the agreement.">
        [docReference ISDA CSA_IM_2016 paragraph "3 General Principles" clause "(g)(i)" name "Resolution Time"]
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(g)(i)" name "Resolution Time"]
    otherTerms string (0..1) <"The custom Resolution Time election that might be specified by the parties.">
    alternativeTerms string (0..1) <"The alternative dispute resolution procedure if specified.">
    recalculationOfValue RecalculationOfValue (0..1) <"The elections to specify terms for recalculation of the market value of posted collateral.">
    legacyAlternative LegacyResolutionAlternative (0..1) <"Alternative procedures to those in the pre-print for resolving collateral related disputes.">
    value ResolutionValue (0..1) <"Methodology for resolving disputes in respect of the determination of the value of collateral.">

type ElectiveAmountElection: <"A class to specify the party elective amounts which can be used for the purpose of specifying elections such as the ISDA CSA Threshold and Minimum Transfer Amount.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    electiveAmount ElectiveAmountEnum (0..1) <"Specifies an enumerated election to express the elective amount.">
    amount Money (0..1) <"The elective amount when expressed as a currency amount. The associated PartyElectiveAmount_amount data rule enforces that the currency amount is actually greater than 0.">
    customElection string (0..1) <"The elective amount when expressed as a custom election by the party.">

    condition NonZeroAmount: <"When the elective amount is not zero either a currency amount or a custom election must exist.">
        if electiveAmount is absent
        then amount exists or customElection exists

type EnforcementEvent: <"A class to specify Enforcement Events specific to Security Agreements.">

    earlyTerminationDate boolean (1..1) <"The early termination election.">
    failureToPay boolean (0..1) <"The failure to pay election.">

type ExecutionLanguage: <"A class to specify execution language terms of a Security Agreement.">

    standardLanguage boolean (1..1) <"A boolean attribute to determine if standard language is applicable or not.">
    numberOfOriginals string (0..1) <"The number of original documents.">
    otherLanguage string (0..1) <"Bespoke execution language to be included when specified.">

    condition NumberOfOriginals: <"A data rule to enforce that the number of original documents should only be specified when standard execution language is used.">
        if standardLanguage = False then numberOfOriginals is absent

    condition OtherLanguage: <"A data rule to enforce that bespoke execution language must be included if non-standard execution language is specified.">
        if standardLanguage = False then otherLanguage exists

type ExecutionLocation: <"A class to specify execution location terms of a Security Agreement.">

    executionLocation ExecutionLocationEnum (1..1) <"The execution location of the agreement.">
    otherLanguage string (0..1) <"Bespoke execution location language to be included when specified.">
    dutyPayer string (0..1) <"The payer of documentary duty.">
    dutyPayerLanguage string (0..1) <"Bespoke terms specific to the payment of documentary duty.">
    dutyPaymentDate date (0..1) <"The date that documentary duty will be paid.">
    dutyPaymentLanguage string (0..1) <"Bespoke terms specific to the date that documentary duty will be paid.">

    condition DutyPayerLanguage: <"A data rule to enforce that Duty Payer Language should only be specified when required.">
        if dutyPayer = "Specify" then dutyPayerLanguage exists

    condition DutyPaymentLanguage: <"A data rule to enforce that Duty Payment Language should be absent when a Duty Payment date is provided.">
        if dutyPaymentDate exists then dutyPaymentLanguage is absent

    condition OtherLanguage: <"A data rule to enforce that bespoke execution language must be included if non-standard execution language is specified.">
        if executionLocation = ExecutionLocationEnum -> OtherLocation
        then otherLanguage exists

type ExecutionTerms: <"A class to specify execution location and language of execution to determine duty to be paid.">

    executionLanguage ExecutionLanguage (1..1) <"The bespoke execution language election.">
    executionLocation ExecutionLocation (1..1) <"The bespoke execution location election.">

type FrenchLawAddendum: <"A class to specify party specific elections when a Collateral Transfer Agreement is governed by French Law.">

    isApplicable boolean (1..1) <"The qualification of whether the French Law Addendum is deemed applicable by the parties (True) or not (False).">
    partyElection FrenchLawAddendumElection (0..2) <"The parties French Law Addendum Elections.">

    condition Applicable: <"A data rule to enforce that the French Law Addendum party elections must be specified when applicable.">
        if isApplicable = True then partyElection count = 2

type FrenchLawAddendumElection: <"A class to specify party specific French Law Addendum language.">

    party CounterpartyRoleEnum (1..1) <"The elective party.">
    isApplicable boolean (1..1) <"The qualification of whether the party elects specific language.">
    addendumLanguage string (0..1) <"The party specific language to be included in the agreement.">

    condition AddendumLanguage: <"A data rule to enforce that the French Law Addendum party language must be specified when applicable.">
        if isApplicable = True
        then addendumLanguage exists
        else if isApplicable = False
        then addendumLanguage is absent

type FxHaircutCurrency: <"A class to specify the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">

    isTerminationCurrency boolean (1..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is the Termination Currency when the Boolean value is set to True.">
    fxDesignatedCurrency string (0..1) <"When specified, the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">
        [metadata scheme]

    condition TerminationCurrency: <"The FX Designated Currency shouldn't be specified when the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is specified as the Termination Currency.">
        if isTerminationCurrency = True
        then fxDesignatedCurrency is absent

    condition FxDesignatedCurrency: <"The FX Designated Currency should be specified when the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is specified as not being the Termination Currency.">
        if isTerminationCurrency = False
        then fxDesignatedCurrency exists

type GeneralSimmElections: <"A class to specify the ISDA SIMM as the Method for all Covered Transactions with respect to all Regimes.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ee)"]
    simmVersion SimmVersion (0..1) <"The qualification of the ISDA SIMM version that is specified for all Covered Transactions.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ee)(1)"]
    simmCalculationCurrency SimmCalculationCurrency (1..1) <"The SIMM Calculation Currency, as specified for each of the parties to the CSA Initial Margin.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ee)(3)"]

type HoldingAndUsingPostedCollateral: <"Specifies the conditions that need to be satisfied before the Secured Party or Custodian acting on its behalf can hold and use posted collateral.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause " (h)" name "Holding and Using Posted Collateral (VM)"]
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(g)"]
    [docReference ISDA CSD_English_1995 paragraph "13" clause "(h)"]
    partyElection HoldingAndUsingPostedCollateralElection (2..2) <"The parties' elections for the holding and using of posted collateral.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type HoldingAndUsingPostedCollateralElection: <"Specifies the parties' elections related to the holding and using of posted collateral.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause " (h)" name "Holding and Using Posted Collateral (VM)"]
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(g)"]
    [docReference ISDA CSD_English_1995 paragraph "13" clause "(h)"]
    party CounterpartyRoleEnum (1..1) <"The party to which the elections apply.">
    eligibilityToHoldCollateral EligibilityToHoldCollateral (1..1) <"The specification of the conditions under which a party and its custodian(s) are entitled to hold posted collateral.">
    useOfPostedCollateral boolean (1..1) <"Indicates whether the party has the right to reuse, rehypothecate, or otherwise make use of the posted collateral. 'True' if the party has the right,'False' if the party does not have the right.">
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(h)(ii)" name "Use of Posted Collateral (VM)"]
        [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(g)(ii)"]

type IneligibleCreditSupport: <"Identifies collateral that has been designated as not meeting the eligibility criteria set out in the agreement.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause "(c)(iii)" name "Legally Ineligible Credit Support (VM)"]
    totalIneligibilityDate string (0..1) <"Specifies the date from which all credit support provided by a party is treated as ineligible under the agreement.">
    transferIneligibilityDate string (0..1) <"Specifies the date from which the transfer of credit support by a party is treated as ineligible under the agreement.">
    specifiedParty CounterpartyRoleEnum (0..2) <"Identifies a party designated in the agreement for the purpose of applying particular rights, obligations, or elections.">
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(c)(iii)" name "Legally Ineligible Credit Support (VM)"]

type InterestAdjustment: <"A class to specify whether the Interest Adjustment is applicable and what its periodicity is.">
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(n)(ii)"]

    isApplicable boolean (1..1) <"The Interest Adjustment is applicable when True and not applicable when False.">
    periodicity InterestAdjustmentPeriodicity (1..1) <"The qualification of the Interest Adjustment periodicity.">

type InterestAdjustmentPeriodicity: <"A class to specify the Interest Adjustment periodicity.">
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(n)(ii)"]

    standardElection InterestAdjustmentPeriodicityEnum (0..1) <"The Interest Adjustment periodicity when specified through a standardized election.">
    customElection string (0..1) <"The Interest Adjustment periodicity when specified through a custom election.">

    condition:
        one-of

type InterestAmount: <"A class to specify the application of Interest Amount with respect to the Delivery Amount and the Return Amount.">
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(n)(ii)"]

    returnAmount ReturnAmount (1..1) <"The application of Interest Amount with respect the Return Amount.">
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(n)(ii)"]
    deliveryAmount DeliveryAmount (1..1) <"The application of Interest Amount with respect the Delivery Amount.">
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(n)(ii)"]

type JapaneseSecuritiesProvisions: <"A class to specify Japanese Securities Provision elections.">
    isApplicable boolean (1..1) <"Japanese Securities Provisions are applicable when True and Not Applicable when False.">
    relevantProvisionsElection boolean (0..1) <"Recommended Japanese Securities Provisions are applicable when True, additional Provisions are specified when False.">
    relevantProvisionsTerms string (0..1) <"Specific terms applicable to Recommended Japanese Securities Provisions.">
    amendmentsToJapaneseProvisions boolean (0..1) <"Additional Amendments to Japanese Securities Provisions are specified when True, and not specified when False.">
    amendmentsToJapaneseProvisionsTerms string (0..1) <"Specific terms applicable to Additional Amendments to Japanese Securities Provisions.">
    clearstreamAmendmentToJapaneseProvisions boolean (0..1) <"Specification of whether Clearstream Event amendment language is included (true) or excluded (false).">

    condition RelevantProvisionsElection: <"A data rule to enforce that the specific terms applicable to Recommended Japanese Securities Provisions should be specified when required.">
        if relevantProvisionsElection = True
        then relevantProvisionsTerms is absent

    condition AmendmentsToJapaneseProvisions: <"A data rule to enforce that the specific terms applicable to Additional Amendments to Japanese Securities Provisions should be specified when required.">
        if amendmentsToJapaneseProvisions = False
        then amendmentsToJapaneseProvisionsTerms is absent

type JurisdictionRelatedTerms: <"A class to specify terms jurisdiction related terms.">
    japaneseSecuritiesProvisions JapaneseSecuritiesProvisions (0..1) <"The Japanese Securities Provisions election.">
    exclusiveJurisdiction boolean (0..1) <"Classification of optional exclusive jurisdiction terms.">
    juryWaiver boolean (0..1) <"The Jury Waiver conditions specific to the agreement.">
    frenchLawAddendum FrenchLawAddendum (0..1) <"The French Law Addendum Provisions specific to the agreement.">
    belgianLawSecurityAgreement boolean (0..1) <"The qualification of whether the Belgian Law Security Agreement Addendum is deemed applicable by the parties (True) or not (False).">

type MarginApproach: <"A class for selection of Margin Approach.">
    marginApproach MarginApproachEnum (1..1) <"Specifies the margin approach specific to Initial Margin agreements.">

type MinimumTransferAmountAmendment: <" A class to specify whether Amendment to Minimum Transfer Amount language is applicable or not.">
    isApplicable boolean (1..1) <"The definition of Minimum Transfer Amount in any Other Regulatory CSA will be amended when applicable.">
    effectiveDate AmendmentEffectiveDate (0..1) <"The effective date of the Amendment to Termination Currency.">
    partyElections ElectiveAmountElection (0..2) <"The party elective amounts.">

    condition AmendmentNotApplicable: <"A data rule to enforce that the Effective Date and Party Elections should be absent when the Minimum Transfer Amount Amendment is stated as not specified for the agreement.">
        if isApplicable = False
        then effectiveDate is absent and partyElections is absent

type NotificationTime: <"A class to specify the time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(d)(iii)" name "Notification Time"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(e)(iii)" name "Notification Time"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(d)(iii)" name "Notification Time"]

    partyElections NotificationTimeElection (2..2) <"The parties' Notification Time election.">
    disputeNotificationReference boolean (0..1) <"The determination of whether reference is made to dispute resolution notification timing in the agreement.">
    transferTimingProviso boolean (0..1) <"The determination of whether transfer timing language is applicable or not.">

type NotificationTimeElection: <"A class to specify the notification time election by the respective parties to the agreement.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(d)(iii)" name "Notification Time"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(e)(iii)" name "Notification Time"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(d)(iii)" name "Notification Time"]

    party CounterpartyRoleEnum (1..1) <"The elective party.">
    notificationTime BusinessCenterTime (0..1) <"The Notification Time as a time that is qualified as a standard business center.">
    customNotification string (0..1) <"The Notification Time as a bespoke election.">
    localBusinessDay boolean (0..1) <"Whether the Notification Time is precluded on the day being a local business day.">

    condition NotificationTimeElectionChoice: <"The Notification Time is specified either as a time that is qualified by a standard business center or as a custom election.">
        required choice notificationTime, customNotification

type OneWayProvisions: <"A class to specify whether One Way Provisions apply.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(aa)" name "One Way Provisions"]

    isApplicable boolean (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false).">
    postingParty CounterpartyRoleEnum (0..1) <"The Posting Party for the purposes of One Way Provisions. It is specified in the case where the One Way Provision is deemed applicable.">

    condition PostingPartyExists: <"A data rule to enforce that the Posting Party must be specified in the case where the One Way Provision is deemed applicable.">
        if isApplicable = True then postingParty exists

    condition PostingPartyAbsent: <"A data rule to enforce that the Posting Party must not be specified in the case where the One Way Provision is not deemed applicable.">
        if isApplicable = False then postingParty is absent

type OtherAgreements: <"The bespoke definition of other agreement terms as specified by the parties to the agreement.">
    otherCsa OtherAgreementTerms (0..1) <"The bespoke definition of Other CSA as specified by the parties to the agreement.">
    japaneseLawCsa OtherAgreementTerms (0..1) <"The bespoke definition of whether Japanese Law CSA (VM) are specified by the parties to the agreement.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(s)(ii)" name "Japanese Law CSA (VM)"]

type OtherEligibleAndPostedSupport: <"A class to specify the Other Eligible Support elections associated Initial and Variation margin agreements.">
    applicableValue boolean (1..1) <"The definition of 'Value' with respect to Other Eligible Support (IM) and Other Posted Support (IM).">
    applicableTransfer boolean (0..1) <"The definition of 'Transfer' with respect to Other Eligible Support (IM) and Other Posted Support (IM).">

type PartyAgreementIdentifier: <"A class defining a legal agreement identifier issued by the indicated party.">
    [metadata key]
    partyReference Party (1..1) <"Party that issued the document identifier.">
        [metadata reference]
    documentIdentifier Identifier (1..*) <"While FpML specifies the document identifier with a value and an associated scheme, the CDM makes use of the Identifier, which has an explicit issuer. The issuer of this identifier is not necessarily the same as the party reference.">
        [metadata scheme]

type PostingObligations: <"A class to specify the collateral posting obligations of the security provider or security providers as specified in the corresponding agreement.">
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(ii)"]
    securityProvider string (1..1) <"The security provider party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">
    partyElection PostingObligationsElection (1..2) <"The specification of the collateral posting obligations for the security provider party(ies).">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ii)"]

type PostingObligationsElection: <"Specifies the collateral posting obligations for the security provider party(ies).">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ii)"]
    party CounterpartyRoleEnum (1..1) <"Specifies the party providing the security.">
    asPermitted boolean (1..1) <"If set to True, the Control Agreement is a Credit Support Document with respect to the party(ies).">
        [docReference ISDA CSA_IM_2016 paragraph "6" clause "(e)"]
    eligibleCollateral EligibleCollateralCriteria (0..*) <"The eligible collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation.">
        [docReference ISDA CSA_IM_2016 name "Eligible Credit Support (IM) Schedule"]
    excludedCollateral string (0..1) <"The excluded collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(ii)(B)(i)"]
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AsPermitted: <"A data rule to enforce that the eligible collateral should be specified when the Control Agreement is a Credit Support Document with respect to the party(ies).">
        if asPermitted = True then eligibleCollateral is absent

    condition EligibleCollateral: <"A data rule to enforce that the eligible collateral shouldn't be specified when the Control Agreement isn't deemed a Credit Support Document with respect to the party(ies).">
        if asPermitted = False then eligibleCollateral exists

    condition ConsistentParty: <"A data rule to ensure that the elective party is only set on the agreement and not in the eligible collateral criteria.">
        if eligibleCollateral exists
        then eligibleCollateral -> appliesTo count = 0

type ProcessAgent: <"A class to specify the Process Agent that might be appointed by the parties as part of a Credit Support Annex/Deed or Collateral Transfer Agreement.">
    partyElection ProcessAgentElection (2..2) <"The parties' Process Agent election.">

type ProcessAgentElection: <"A class to specify the parties' respective elections with respect to the Process Agent.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    isApplicable boolean (1..1) <"The qualification of whether the Process Agent is applicable (True) or not applicable (False).">
    processAgent PartyContactInformation (0..1) <"The Process Agent specification, when applicable.">

    condition Applicable: <"A data rule to enforce that the Process Agent must be specified when it is applicable.">
        if isApplicable = True then processAgent exists

    condition NotApplicable: <"A data rule to enforce that the Process Agent cannot be specified if deemed not applicable.">
        if isApplicable = False then processAgent is absent

type RecalculationOfValue: <"A class to specify terms for Recalculation of the Market Value of Posted Collateral when a dispute has been failed to be resolved by Resolution Time.">

    partyElection RecalculationOfValueElection (2..2) <"The parties' Recalculation of Value terms.">

type RecalculationOfValueElection: <"A class to specify Recalculation of Value terms that will be applicable.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    recalculationOfValueElection RecalculationOfValueElectionEnum (1..1) <"The procedure for Recalculation of Value.">
    recalculationOfValueTerms string (0..1) <"Additional Recalculation of Value terms when specified.">

type Regime: <"A class to specify one or more regimes that may be specified as relevant to a legal agreement.">
    [docReference ISDA CSA_IM_2016 name "Regime"]
    [docReference ISDA CSA_IM_2018 name "Regime"]

    applicableRegime ApplicableRegime (1..*) <"A class to specify the regime(s) that parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin, might agree to apply to one or both parties when acting as collateral taker, and specific terms associated with that application.">
    fallbackToMandatoryMethodDays number (0..1) <"The specification of the number of days after effective delivery of notice that Mandatory method fallback applies. Specification is only required when one or more Regimes have Fall Back to Mandatory Method elected as a SIMM exception.">

type RegimeTerms: <"A class that is used by the ApplicableRegime and the AdditionalRegime classes to specify the regulatory regime terms.">
    [docReference ISDA CSA_IM_2016 name "Regime"]
    [docReference ISDA CSA_IM_2018 name "Regime"]

    party CounterpartyRoleEnum (1..1) <"The party for which the regime terms are being specified when acting as collateral taker.">
    isApplicable ExceptionEnum (0..1) <"The specification of whether the regime is elected as applicable to the party when acting as collateral taker.">
    asSpecified string (0..1) <"The bespoke party specific Regime term elections applicable when specified.">
    simmException SimmException (0..1) <"The election for SIMM exception to the regulatory regime clause.">
        [docReference ISDA CSA_IM_2016 paragraph "13"]
    retrospectiveEffect RetrospectiveEffect (0..1) <"Specifies the retrospective effect exception to the regulatory regime clause of Initial Margin documents as either a normalized value specified as part of an enumeration or a customized value specified of type string.">
        [docReference ISDA CSA_IM_2016 paragraph "13" clause "(b)(i)"]

type PledgeeRepresentativeRider: <"The terms of the Rider for the ISDA Euroclear 2019 Collateral Transfer Agreement with respect to the use of a Pledgee Representative attached to this Agreement.">
    isApplicable boolean (1..1) <"Identification of whether the representative CTA provisions are applicable (True) or not applicable (False).">
    party CounterpartyRoleEnum (0..1) <"Identification of the represented party.">
    representativeTerms string (0..1) <"The specific representative terms applicable when specified.">
    representativeEvent ExceptionEnum (0..1) <"The specification of whether the representative event terms are applicable.">
    representativeEventTerms string (0..1) <"The specific representative event terms applicable when specified.">
    representativeEndDate CustomisableOffset (0..1) <"The definition of representative end date in relation to a representative event.">

    condition RepresentativeParty: <"A data rule to enforce that representative terms and a represented party are specified when representative provisions are applicable.">
        if isApplicable = True
        then party exists
                and representativeTerms exists
                and representativeEvent exists
                and representativeEndDate exists

    condition RepresentativeEventTerms: <"A data rule to enforce that representative event terms are specified when applicable.">
        if representativeEvent = ExceptionEnum -> Other
        then representativeEventTerms exists

type RetrospectiveEffect: <"A class to specify the retrospective effect exception to the regulatory regime clause of Initial Margin documents as either a normalized value specified as part of an enumeration or a customized value specified of type string.">

    standardisedException ExceptionEnum (0..1) <"The Standard Initial Margin Model exception when specified by the party according to one of the enumerated values.">
    asSpecified string (0..1) <"The Standard Initial Margin Model exception when specified as a customized approach by the party.">

type RightsEvents: <"A class to specify the rights of Security Taker and/or Security Provider when an Early Termination or Access Condition event has occurred.">

    securityTakerRightsEvent SecuredPartyRightsEvent (1..1) <"The bespoke provisions that might be specified by the parties to the agreement applicable to a Security Taker Rights Event.">
    controlAgreementNecEvent ControlAgreementNecEvent (0..1) <"The bespoke provisions that might be specified by the parties to the agreement applicable to a Notice of Exclusive Control Event.">
    securityProviderRightsEvent SecurityProviderRightsEvent (1..1) <"The bespoke provisions that might be specified by the parties to the agreement applicable to a Security Provider Rights Event.">
    deliveryInLieuRight boolean (0..1) <"The specification of whether Delivery In Lieu language is applicable to the agreement (true) or not (false).">
    additionalRightsEvent AdditionalRightsEvent (0..1) <"The Additional Rights Event election.">

type SecuredPartyRightsEvent: <"A class to specify Secured Party Rights Event language.">

    securedPartyRightsEventElection SecuredPartyRightsEventElection (0..2)
    earlyTerminationDateOptionalLanguage boolean (1..1) <"A boolean attribute to specify whether Failure to Pay Early Termination language is included (True) or excluded (False) from the agreement.">
    failureToPayEarlyTermination boolean (0..1) <"A boolean attribute to specify whether Failure to Pay Early Termination language in the agreement is deemed applicable or not.">

    condition FailureToPayLanguage: <"A data rule to enforce that the applicability of Failure to Pay language is only applied when Early Termination language is included.">
        if earlyTerminationDateOptionalLanguage = False
        then failureToPayEarlyTermination is absent

type SecuredPartyRightsEventElection: <"A class to specify party specific Secured Party Rights Event language.">

    party CounterpartyRoleEnum (1..1) <"The elective party.">
    rightsEvent boolean (1..1) <"A boolean attribute to specify whether a Secured Party Rights Event will only occur upon the occurrence of one or more of the event specified in a Control Agreement.">

type SecurityProviderRightsEvent: <"A class to specify the Pledgor/Obligor/Chargor Rights Event election.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(j)" name "Chargor Rights Event"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(j)" name "Obligor Rights Event"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(j)" name "Pledgor Rights Event."]

    partyElection SecurityProviderRightsEventElection (0..2)
    includeCoolingOffLanguage boolean (1..1) <"The Pledgor/Obligor/Chargor Rights Event election includes cooling off language when the attribute is set of True.">
    fullDischarge boolean (0..1) <"If specified as applicable here, a Pledgor/Obligor/Chargor Rights Event will not occur unless the Pledgor/Obligor/Chargor (A) has provided a statement to the Secured Party in respect of such Early Termination Date.">
    automaticSetOff boolean (0..1) <"The Automatic Set-Off provision applies when the value is set to True.">
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(l)" name "Modification to Obligors Rights and Remedies"]
    customElection string (0..1) <"A custom Pledgor/Obligor/Chargor Rights Event election might be specified by the parties.">

    condition RightsEvent_includeCoolingOffLanguage: <"A data rule to enforce that, when the Rights Event election includes cooling off language, no custom election should be specified.">
        if includeCoolingOffLanguage = True
        then customElection is absent

    condition RightsEvent_customElection: <"A data rule to enforce that, when the Rights Event is specified through a custom election, no standard cooling off language should be specified.">
        if customElection exists
        then includeCoolingOffLanguage = False

type SecurityProviderRightsEventElection: <"A class to specify party specific Secured Party Rights Event language.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    rightsEvent boolean (1..1) <"If True, the Rights Event language applies for the specified Party.">

type SensitivityMethodologies: <"A class to specificy methodologies to compute sensitivities specific to the agreement.">
    partyElection SensitivityMethodologiesPartyElection (2..2) <"Party specific elections for Sensitivity Methodology for Equity and Commodities.">

type SensitivityMethodologiesPartyElection: <"A class to specificy methodologies to compute sensitivities specific to the agreement.">
    party CounterpartyRoleEnum (1..1) <"The party to which the election applies.">
    sensitivityToEquity SensitivityToEquity (1..1) <"The methodology to compute sensitivities to equity indices, funds and ETFs for the purpose of Initial Margin agreements.">
    sensitivityToCommodity SensitivityMethodology (1..1) <"The methodology to compute sensitivities to commodity indices for the purpose of Initial Margin agreements.">

type SensitivityToEquity: <"Represents risk sensitivity calculated with respect to the value of an individual equity instrument.">
    sensitivityToIndices SensitivityMethodology (1..1) <"Represents risk sensitivity calculated with respect to the level of an equity or market index.">
    sensitivityToFunds SensitivityMethodology (1..1) <"Represents risk sensitivity calculated with respect to the value of a fund, such as a mutual fund or collective investment vehicle.">
    sensitivityToETFs SensitivityMethodology (1..1) <"Represents risk sensitivity calculated with respect to the value of an exchange-traded fund (ETF).">

type SensitivityMethodology: <"A class to specify the methodology according to which sensitivities are computed.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(gg)(2)"]

    specifiedMethodology SensitivitiesEnum (0..1) <"The methodology according to which sensitivities will be computed, when specified through a normalized election.">
    customMethodology string (0..1) <"The methodology according to which sensitivities will be computed, when specified through a custom election.">
    condition:
        one-of

type SimmCalculationCurrency: <"A class to specify the SIMM Calculation Currency elections by each party to the agreement.">

    partyElection CalculationCurrencyElection (2..2) <"The parties' SIMM Calculation Currency election.">

type SimmException: <"A class to specify the SIMM exception to the regulatory regime clause.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" name "Regime: SIMM Exception."]

    standardisedException ExceptionEnum (0..1) <"The Standard Initial Margin Model exception when specified by the party according to one of the enumerated values.">
    simmExceptionApplicable SimmExceptionApplicableEnum (0..1) <"The Standard Initial Margin model exception approach applicable when specified by the party according to one of the enumerated values.">
    asSpecified string (0..1) <"The Standard Initial Margin Model exception when specified as a customized approach by the party.">

type SimmVersion: <"A class to specify the ISDA SIMM version that applies to the ISDA 2018 CSA for Initial Margin.">
    [docReference ISDA CSA_IM_2018 paragraph "13 General Principles" clause "(ee)(1)"]

    isSpecified boolean (0..1) <"A boolean attribute to determine whether the SIMM version is specified for the purpose of the legal agreement.">
    partyVersion CounterpartyRoleEnum (0..1) <"The party which the specified SIMM version applies to.">
    asSpecified string (0..1) <"The SIMM version exception when specified as a customized approach by the party.">

    condition VersionNotSpecified: <"A data rule to enforce that the version attribute should be absent when the SIMM version is stated as not specified for the CSA.">
        if isSpecified = False then partyVersion is absent

    condition VersionSpecified: <"A data rule to enforce that the version attribute should be specified when the SIMM version is stated as specified for the CSA.">
        if isSpecified = True then partyVersion exists

type SubstitutedRegime: <"A class to specify each party's election with respect to the Substituted Regimes that will be applicable...">

    regime RegulatoryRegimeEnum (0..1) <"The applicable regulatory regime, as specified through an enumeration.">
    additionalRegime string (0..1) <"The additional regulatory regime as specified by the parties.">
    regimeTerms SubstitutedRegimeTerms (2..2) <"Specifies the applicability of the Substituted Regime as denoted in the Substituted Regime Table as part of certain legal agreements, such as the ISDA 2016 and 2018 CSA for Initial Margin.">

    condition SubstitutedRegimeChoice: <"The applicable regime should be specified either as an enumeration or as an additional regime specified by the parties.">
        required choice regime, additionalRegime

type SubstitutedRegimeTerms: <"Specifies the applicability of the Substituted Regime as denoted in the Substituted Regime Table as part of certain legal agreements, such as such as the ISDA 2016 and 2018 CSA for Initial Margin.">

    party CounterpartyRoleEnum (1..1) <"The party for which the regime terms are being specified when acting as collateral taker.">
    isApplicable boolean (1..1) <"The specification of whether the regime is elected as applicable to the party when acting as collateral taker.">

type Substitution: <"A class to specify the conditions under which the Security Provider can substitute posted collateral. The details required for 'Legacy' agreements are substantially different to those required for newer agreements; where the agreement is a 1994 or 1995 CSA, or 1995 CSD, the legacyConsent attribute needs to be used which contains additional values beyond a simple true/false value.">
    partyElection SubstitutionPartyElection (0..2) <"Party specific elections for substitution of posted collateral.">

type SubstitutionPartyElection: <"Party specific elections for substitution of posted collateral.">
    party CounterpartyRoleEnum (1..1) <"The party that is acting as the Pledgor/Obligor/Chargor/Security Provider.">
    needsConsent boolean (1..1) <"The election as to whether the Pledgor/Obligor/Chargor/Security Provider must obtain the Secured Partys consent for any collateral substitution.">
        [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(f)(ii)" name "Consent"]
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(f)(ii)" name "Consent"]
    substitutionDateLanguage string (0..1) <"Substitution Date has the meaning specified in Paragraph4(d)(ii), unless otherwise specified.">
    specificConsentLanguage string (0..1) <"Specific consent language might be specified by the parties.">
    legacyConsent LegacyConsentEnum (0..1) <"The process for substituting/exchanging one form of collateral held by a party for a different form of collateral, used with legacy agreements.">

type TerminationCurrencyAmendment: <"A class to specify the Amendment to Termination Currency elections by the parties to the agreement.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(t)" name "Amendment to Termination Currency."]

    isApplicable boolean (1..1) <"The qualification of whether the Amendment to Termination Currency is deemed applicable by the parties (True) or not (False).">
    effectiveDate AmendmentEffectiveDate (0..1) <"The effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision.">
    partyElection TerminationCurrencyElection (0..3) <"The parties' Amendment Currency election.">

    condition Applicability: <"The Amendment to Termination Currency elections only apply when the Amendment to Termination Currency is deemed applicable by the parties.">
        if isApplicable = False
        then effectiveDate is absent and partyElection is absent

type TerminationCurrencyElection: <"A class to specify the Amendment to Termination Currency election by the parties to the agreement.">
    [docReference ISDA CSA_IM_2016 paragraph "13 General Principles" clause "(t)(A) & (B)"]

    party CounterpartyRoleEnum (1..2) <"The elective party.">
    isSpecified boolean (1..1) <"The qualification of whether the Termination Currency is specified in this document (True) or in an Eligible Support Credit Support (IM) Schedule (False).">
    currency string (0..1) <"The Termination Currency associated with the party that referenced as part of this class. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">
        [metadata scheme]

    condition CurrencyElection: <"A Termination Currency election should only exist when required by the affected parties election.">
        if isSpecified = False then currency is absent

type AdditionalObligations: <"The election of party specific additional obligations applicable to the agreement.">
    party CounterpartyRoleEnum (1..1) <"The party that the additional obligations apply to.">
    additionalObligations string (1..1) <"The party specific additional obligations applicable to the agreement.">

type BaseAndEligibleCurrency: <"The base and eligible currency(ies) for the document as specified by the parties to the agreement.">
    baseCurrency ISOCurrencyCodeEnum (1..1) <"The common agreed currency into which relevant amounts of all collateral arrangements between the parties are converted, or if not an actual currency, the process through which this is determined. Where hardcoded (e.g. under the 1994 ISDA Credit Support Annex (Security Interest  NY Law)), the currency that effectively performs this function.">
    baseCurrencyTerminationCurrency boolean (1..1) <"A flag detailing whether the Base Currency is set to  the Termination Currency as defined in the related  Master Agreement.">
    baseCurrencyOther string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">
        [metadata scheme]
    eligibleCurrency ISOCurrencyCodeEnum (0..*) <"A definition of a currency agreed by the parties, typically to indicate the currencies of eligible cash collateral.">
    eligibleCurrencyInclBaseCurrency boolean (1..1) <"A flag detailing whether the Base Currency is included as an Eligible Currency.">
    eligibleCurrencyOther string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">
        [metadata scheme]

type CoveredTransactions: <"Specification of Transactions covered by the legal agreement.">
    inclusionDate date (1..1) <"Includes any Transaction specified below that is entered into on or after the specified date.">
    coveredTransactions ProductTaxonomy (1..*) <"Covered Transactions when expressed using the ISDA taxonomy.">
    bespokeCoveredTransactions string (1..*) <"Covered Transactions when not expressed using the ISDA taxonomy.">
    additionalObligations AdditionalObligations (0..2) <"The party specific additional obligations applicable to the document.">
    exposure ExposureScope (0..1) <"Specification of amendments to the calculation of Exposure in terms of the Transactions covered.">

type CreditSupportObligationsBase: <"A logical container designed to hold a set of related data. In this case, designed to hold a set of clauses that are common amongst Legacy (1994/95), Variation Margin and Initial Margin Credit Support Documents in regards to Credit Support Obligations.">
    deliveryAmount DeliveryAmount (1..1) <"In respect of a Valuation Date, the amount of collateral which a party is entitled to demand to be transferred to it as a Delivery Amount.">
    returnAmount ReturnAmount (1..1) <"In respect of a Valuation Date, the amount of collateral which a party is entitled to demand to be recalled to it as a Return Amount.">
    minimumTransferAmount MinimumTransferAmount (1..1) <"Represents the threshold specified in the agreement below which collateral need not be delivered, and represents the smallest incremental value of exposure that must be reached before the parties are obliged to transfer collateral.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]
        [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(c)(vi)(C)" name "Rounding"]
        [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(d)(vi)(C)" name "Rounding"]
        [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(c)(vi)(C)" name "Rounding"]
    collateralTransferTiming CollateralTransferTiming (0..1) <"Specification of transfer / settlement timing for cash and securities collateral assuming that the notice is provided before the Notification Time (and with a 1LBD lag introduced if the notice is delivered after the Notification Time), and that securities allow for a further Local Business Day for transfer / settlement.">
    bespokeTransferTiming BespokeTransferTiming (0..1) <"The time by which the transfer of collateral must take place when different from the Regular Settlement Day as a result of parties' election.">

type CreditSupportObligationsLegacy extends CreditSupportObligationsBase: <"Specification of the Credit Support Obligations applicable to the Legacy (1994/95) Credit Support Annex.">
    threshold Threshold (1..1) <"Represents the amount of unsecured risk that a party is willing to tolerate without holding any posted collateral provided by the other party.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]
    rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units.">
    independentAmount LegacyIndependentAmount (1..1) <"Specifies the additional amount of collateral that is required to be delivered by a party irrespective of the mark-to-market exposure under the related Transactions. It may take the form of a fixed amount or may be determined using ratings.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]
        [docReference ISDA CSD_IM_English_2016]
        [docReference ISDA CSA_IM_NewYork_2016]
    creditSupportAmount CreditSupportAmount (1..1) <"The total amount one counterparty must deliver to the other at any time: the combination of the Exposure to that party and the net Independent Amounts it must post, minus any agreed Threshold.">    
    eligibleCreditSupport EligibleCreditSupport (1..1) <"Specification of the Eligible Collateral or Credit Support applicable to the Agreement.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]    

type CreditSupportObligationsInitialMargin extends CreditSupportObligationsBase: <"Specification of the Credit Support Obligations applicable to the Initial Margin Credit Support Annex.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(c)" name "Credit Support Obligations"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(d)" name "Credit Support Obligations"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(c) " name "Credit Support Obligations"]
    marginApproach MarginApproach (1..1) <"The selection of Margin Approach applicable to the agreement.">
    otherEligibleSupportIM string (0..1) <"The Other Eligible Support election. If not specified, this election is deemed as not applicable.">
        [docReference ISDA CSA_IM_NewYork_2016]
        [docReference ISDA CSA_IM_NewYork_2018]
    rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units.">
    threshold Threshold (1..1) <"Represents the amount of unsecured risk that a party is willing to tolerate without holding any posted collateral provided by the other party.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]
    creditSupportAmount CreditSupportAmount (1..1) <"The total amount one counterparty must deliver to the other at any time: the combination of the Exposure to that party and the net Independent Amounts it must post, minus any agreed Threshold.">

type CreditSupportObligationsCollateralTransferAgreement extends CreditSupportObligationsBase: <"Specification of the Credit Support Obligations applicable to the Initial Margin Credit Support Annex.">
    [docReference ISDA CSD_IM_English_2016 paragraph "13 General Principles" clause "(c)" name "Credit Support Obligations"]
    [docReference ISDA CSA_IM_Japanese_2016 paragraph "13 General Principles" clause "(d)" name "Credit Support Obligations"]
    [docReference ISDA CSA_IM_NewYork_2016 paragraph "13 General Principles" clause "(c) " name "Credit Support Obligations"]
    marginApproach MarginApproach (1..1) <"The selection of Margin Approach applicable to the agreement.">
    otherEligibleSupportIM string (0..1) <"The Other Eligible Support election. If not specified, this election is deemed as not applicable.">
        [docReference ISDA CSA_IM_NewYork_2016]
        [docReference ISDA CSA_IM_NewYork_2018]
    threshold Threshold (1..1) <"Represents the amount of unsecured risk that a party is willing to tolerate without holding any posted collateral provided by the other party.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]
    creditSupportAmount CreditSupportAmount (1..1) <"The total amount one counterparty must deliver to the other at any time: the combination of the Exposure to that party and the net Independent Amounts it must post, minus any agreed Threshold.">
    rounding CollateralRounding (0..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units.">

type CreditSupportObligationsVariationMargin extends CreditSupportObligationsBase: <"Specification of the Credit Support Obligations applicable to the Variation Margin Credit Support Annex.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause "(c) " name "Credit Support Obligations"]
    ineligibleCreditSupport IneligibleCreditSupport (0..1) <"The parties to which the provisions of the Credit Support Annex for Variation Margin will apply to.">
        [docReference ISDA CSA_VM_2016 paragraph "13 " clause "(c)(iii)" name "Legally Ineligible Credit Support (VM"]
    majorCurrency string (0..*) <"The additional currencies that are specified as Major Currency for the purpose of applying the FX Haircut Percentage.">
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(c)(v)(B)" name "FX Haircut Percentage"]
        [metadata scheme]
    fxHaircut string (0..1) <"The alternative definition for FX haircut percentage that applies to each party and item of Eligible Collateral unless this item is denominated in a Major Currency or in the Base Currency.">
        [docReference ISDA CSA_VM_2016 paragraph "13" clause "(c)(v)(B)" name "FX Haircut Percentage"]
    rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units.">
    eligibleCreditSupport EligibleCreditSupport (1..1) <"Specification of the Eligible Collateral or Credit Support applicable to the Agreement.">
        [docReference ISDA CSA_NewYork_1994]
        [docReference ISDA CSA_English_1995]
        [docReference ISDA CSD_English_1995]
        [docReference ISDA CSA_VM_English_2016]
        [docReference ISDA CSA_VM_NewYork_2016]

type EligibleCreditSupport: <"Specifies the collateral posting obligations of the security provider or security providers as specified in the corresponding agreement">
    partyElection EligibleCollateralElection (1..2) <"The specification of the collateral posting obligations for the security provider party(ies)">

type EligibleCollateralElection extends PostingObligationsElection: <"Specifies the collateral posting obligations for the security provider party(ies).">
    otherEligibleSupport string (1..1) <"Specifies the Other Eligible Support conditions. If not specified, this election is deemed as not applicable.">
        [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(b)(iii)"]
        [docReference ISDA CSD_English_1995 paragraph "13" clause "(c)(iii)"]
        [docReference ISDA CSA_VM_NewYork_2016]
        [docReference ISDA CSA_IM_NewYork_2018]
        [docReference ISDA CSA_IM_NewYork_2016]

type SecurityInterestForObligations: <"Security Interest for Obligations clause applicable to 1994 NY CSA, 1995 English Law CSA, 1995 English Law Credit Support Deed, 2016 English Law VM CSA and 2016 New York Law VM CSA.">
    [docReference ISDA CSA_NewYork_1994 name "Security Interest for Obligations"]
        [docReference ISDA CSA_English_1995 name "Security Interest for Obligations"]
        [docReference ISDA CSD_English_1995 name "Security Interest for Obligations"]
        [docReference ISDA CSA_VM_English_2016 name "Security Interest for Obligations"]
        [docReference ISDA CSA_VM_NewYork_2016 name "Security Interest for Obligations"]
    party CounterpartyRoleEnum (2..2) <"The elective party to which the Security Interest for Obligations is applicable.">
    obligations SecurityInterestObligationsEnum (1..1) <"A description of any additional obligations secured by the credit support arrangements.">
    obligee SecurityInterestObligeeEnum (1..1) <"The party to whom obligations under the Master Agreement are owed and secured by the credit support arrangements.">
    other string (0..1) <"Exception value.">

type SinglePostingParty: <"Where only a single  party is explicitly designated to post collateral, the party which is specified as the sole poster of collateral.">
    party CounterpartyRoleEnum (0..1) <"The party acting as the Single Posting Party.">
    other string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">

type Threshold: <"Represents the amount of unsecured risk that a party is willing to tolerate without holding any posted collateral provided by the other party.">
    [docReference ISDA CSA_NewYork_1994 name "Threshold"]
        [docReference ISDA CSA_English_1995 name "Threshold"]
        [docReference ISDA CSD_English_1995 name "Threshold"]
        [docReference ISDA CSA_VM_English_2016 name "Threshold"]
        [docReference ISDA CSA_VM_NewYork_2016 name "Threshold"]
    partyElection ThresholdElection (0..2) <"The party specific election for the threshold clause.">

type ThresholdMinmumTransferAmountBase: <"A logical container designed to hold a set of related data. In this case, designed to hold a set of information required to specify conditions under which Threshold or Minimum Transfer Amount can fall to zero.">
    zeroEvent boolean (1..1) <"Indicates whether a trigger applies for the Threshold or Minimum Transfer Amount to fall to zero. If 'True' then such a trigger exists.">
    event ZeroEventEnum (0..*) <"Specifies the relevant trigger for the Threshold or Minimum Transfer Amount to fall to zero.">

    condition ZeroEvent: <"If zero event exists then boolean must be set to 'True' and event must be specified.">
        if zeroEvent = True then event exists

type ThresholdElection: <"Specifies whether the threshold is rating based, a fixed amount, or infinity.">
    party CounterpartyRoleEnum (1..1) <"The party to which the threshold applies.">
    fixedAmount ThresholdMinimumTransferAmountFixedAmount (0..1) <"Defines that the Threshold is a Fixed Amount.">
    ratingsBased ThresholdRatingsBased (0..1) <"Defines that the Threshold is based on a Ratings condition(s).">
    infinity boolean (0..1) <"Specifies that the Threshold is Infinity. If 'True' then the threshold for the elective party is Infinity.">
    other string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">

    condition ThresholdOneOf: <"Ensures that threshold is specified as one-of fixedAmount, ratingsBased, infinity, or other.">
        required choice fixedAmount, ratingsBased, infinity, other

type ThresholdRatingsBased extends ThresholdMinmumTransferAmountBase: <"Defines that the Threshold is based on Ratings condition(s).">
    currency ISOCurrencyCodeEnum (1..1) <"The threshold currency code.">
    ratingType RatingTypeEnum (1..1) <"The relevant rating type.">
    variableSet CSAThresholdVariableSet (0..*) <"Defines a combination of Rating Agency, Rating Value, Threshold amount and Currency code.">
    ratedParty RatedPartyEnum (0..1) <"The party to which a rating applies.">
    namedEntity string (0..1) <"Details the Named Entity where the Rated Party is Named Entity.">
    namedAffiliate string (0..1) <"Details the Named Affiliate where the Rated Party is Named Affiliate.">
    compare CreditNotationMismatchResolutionEnum (0..1) <"Where two ratings are specified whether the higher or lower rating prevails.">
    noRating boolean (1..1) <"What conditions apply where a party has no rating.">
    notRatedBy NotRatedByEnum (0..1) <"Defines where conditions apply if no Rating where ratings may not exist.">
    numberOfRatingAgencies NumberOfRatingAgenciesEnum (0..1) <"Defines the number of Rating Agencies that the Party must be rated by.">

    condition NamedEntity: <"If the rated party is NamedEntity then the entity must be specified.">
        if ratedParty = RatedPartyEnum -> NamedEntity
        then namedEntity exists

    condition NamedAffiliate: <"If rated party is NamedAffilite then the affiliate must be specified.">
        if ratedParty = RatedPartyEnum -> NamedAffiliate
        then namedAffiliate exists

type CSAThresholdVariableSet: <"Defines a combination of Rating Agency, Rating Value, Threshold amount and Currency code.">
    name CreditRatingAgencyEnum (0..1) <"The relevant rating agency.">
    value string (0..1) <"Rating applicable to the party from a given rating agency.">
    amount int (0..1) <"The amount value applicable to the Threshold.">

    condition NameMustExist: <"If we have a name then we must also have a value.">
        if name exists then value exists

    condition ValueMustExist: <"If we have a value then we must also have a name.">
        if value exists then name exists

type MinimumTransferAmount: <"Represents the threshold specified in the agreement below which collateral need not be delivered, and represents the smallest incremental value of exposure that must be reached before the parties are obliged to transfer collateral.">
    [docReference ISDA CSA_NewYork_1994 name "Minimum Transfer Amount"]
    [docReference ISDA CSA_English_1995 name "Minimum Transfer Amount"]
    [docReference ISDA CSD_English_1995 name "Minimum Transfer Amount"]
    [docReference ISDA CSA_VM_English_2016 name "Minimum Transfer Amount"]
    [docReference ISDA CSA_VM_NewYork_2016 name "Minimum Transfer Amount"]
    partyElection MinimumTransferAmountElection (0..2) <"The party specific elections for the Minimum Transfer Amount clause.">

type MinimumTransferAmountElection: <"Specifies the method by which the minimum transfer amount is defined, such as a fixed amount, ratings based, or an alternative basis set out in the agreement.">
    party CounterpartyRoleEnum (1..1) <"The party to which the minimum transfer amount applies.">
    fixedAmount ThresholdMinimumTransferAmountFixedAmount (0..1) <"Defines that the Minimum Transfer Amount (MTA) is a Fixed Amount.">
    ratingsBased MinimumTransferAmountRatingsBased (0..1) <"Defines that the Minimum Transfer Amount (MTA) is based on a Ratings condition(s).">
    other string (0..1) <"Utilised where the clause data structure is not able to capture a material aspect of the clause.">

    condition MinimumTransferAmountOneOf: <"Ensures that Minimum Transfer Amount is specified as one-of fixedAmount, ratingsBased, or other.">
        required choice fixedAmount, ratingsBased, other

type ThresholdMinimumTransferAmountFixedAmount extends ThresholdMinmumTransferAmountBase: <"Defines the Threshold or the Minimum Transfer Amount (MTA) as a fixed amount.">
    amount Money (1..1) <"Specifies the monetary value of the Threshold or the Minimum Transfer Amount (MTA) applicable to collateral transfer obligations.">

type MinimumTransferAmountRatingsBased extends ThresholdMinmumTransferAmountBase: <"Defines that the Minimum Transfer Amount (MTA) is based on a Ratings condition(s).">
    currency ISOCurrencyCodeEnum (1..1) <"The minimum transfer amount currency code.">
    ratingType RatingTypeEnum (1..1) <"The relevant rating type.">
    variableSet CSAMinimumTransferAmountVariableSet (0..*) <"Defines a combination of Rating Agency, Rating Value, amount and Currency code.">
    ratedParty RatedPartyEnum (0..1) <"The party to which a rating applies.">
    namedEntity string (0..1) <"Details the Named Entity where the Rated Party is Named Entity.">
    namedAffiliate string (0..1) <"Details the Named Affiliate where the Rated Party is Named Affiliate.">
    compare CreditNotationMismatchResolutionEnum (0..1) <"Where two ratings are specified whether the higher or lower rating prevails.">
    noRating boolean (1..1) <"What conditions apply where a party has no rating.">
    notRatedBy NotRatedByEnum (0..1) <"Defines where conditions apply if no Rating where ratings may not exist.">
    numberOfRatingAgencies NumberOfRatingAgenciesEnum (0..1) <"Defines the number of Rating Agencies that the Party must be rated by.">

    condition NamedEntity: <"If the rated party is NamedEntity then the entity must be specified.">
        if ratedParty = RatedPartyEnum -> NamedEntity
        then namedEntity exists

    condition NamedAffiliate: <"If rated party is NamedAffilite then the affiliate must be specified.">
        if ratedParty = RatedPartyEnum -> NamedAffiliate
        then namedAffiliate exists

type CSAMinimumTransferAmountVariableSet: <"Defines a combination of Rating Agency, Rating Value, amount and Currency code.">
    name CreditRatingAgencyEnum (0..1) <"The name of the Rating Agency.">
    value string (0..1) <"The value assigned to the rating.">
    amount int (0..1) <"The Minimum Transfer Amount (MTA) applicable to the Rating.">
    amountIsInfinity boolean (0..1) <"A flag that defines the Minimum Transfer Amount (MTA) as Infinity.">

    condition NameMustExist: <"If we have a name then we must also have a value.">
        if name exists then value exists

    condition ValueMustExist: <"If we have a value then we must also have a name.">
        if value exists then name exists

type DatedAsOfDate: <"Defines the dates agreed by the parties as the date of the Credit Support Annex or Credit Support Deed.">
    csaDatedAsOfDate date (0..1) <"The date agreed by the parties as the Dated as of Date for the Credit Support Annex.">
    masterAgreementDatedAsOfDate date (0..1) <"The date agreed by the parties as the Dated as of Date for the Master Agreement.">
    csaMadeOn date (0..1) <"The date agreed by the parties as the Made On date of the Credit Support Deed.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type LegacyDeliveryAmount: <"In respect of a Valuation Date, the amount of collateral which a party is entitled to demand to be transferred to it as a Delivery Amount.">
    legacyDeliveryAmount LegacyDeliveryReturnAmountEnum (1..1) <"Specifies whether the meaning of Delivery Amount follows the pre-print definition or deviates from this (either based on a rating or some other methodology).">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type LegacyReturnAmount: <"In respect of a Valuation Date, the amount of collateral which a party is entitled to demand to be recalled to it as a Return Amount.">
    legacyReturnAmount LegacyDeliveryReturnAmountEnum (1..1) <"Specifies whether the meaning of Return Amount follows the pre-print definition or deviates from this (either based on a rating or some other methodology).">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type ValuationTime: <"Specifies the time by which the value of the collateral and obligations to transfer or return collateral are to be calculated relative to the Valuation Date.">
    day ValuationTimeDayEnum (0..1) <"The day in respect of which the Valuation Time is being specified. If not specified, addional language must be used to define the methodology.">
    location BusinessCenterEnum (0..*) <"The location relevant to the timezone for the Valuation Time.">
    time ValuationTimeEnum (0..1) <"The type of time specified for the Valuation Time.">
    hourminutetime time (0..1) <"A time specified in a 24-hour notation, e.g. 3pm would be represented as 15:00.">
    timezone BusinessCenterEnum (0..1) <"Any relevant time zone where specified.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AdditionalLanguage: <"If no Day valuation methodology is defined, additional language must be specified.">
        if day is absent then additionalLanguage exists

    condition DayAndTime: <"Day and Time must be used together and neither can be used alone.">
        if day exists
        then time exists
        else time is absent
            and item -> location is absent
            and hourminutetime is absent
            and timezone is absent

type FinalReturns: <"A clause providing that final return collateral is to be provided without consideration of any Minimum Transfer Amount and collateral haircut.">
    applicable boolean (0..1) <"Defines if the clause is applicable to this agreement or any other exceptions.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AdditionalLanguage: <"The additional language should be qualified only if the clause is specified as applicable.">
        if applicable then additionalLanguage exists

type CSAValuationDate: <"Specifies the date on which the value of collateral or exposure are calculated in accordance with the agreement.">
    date ValuationDateDateEnum (0..1) <"Defines whether a date is defined by a certain day of the week or a particular date in the month. If not specified here, then additional language must be used.">
    day ValuationDateDayEnum (0..1) <"Specific day(s) or day of the week, if any, specified for Valuation Time.">
    calendarDay number (0..1) <"Defines the day of the calendar if this is specified for in the ValuationDateDateEnum.">
    frequency ValuationDateFrequencyEnum (0..1) <"Defines the regularity of the date specified for the Valuation Date.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition CalendarDay: <"If a specific calendar day is specified, it must be defined.">
        if date = ValuationDateDateEnum -> CalendarDay
        then calendarDay exists
        else calendarDay is absent

    condition AdditionalLanguage: <"If no methodology is defined, additional language must be specified.">
        if date is absent or frequency is absent
        then additionalLanguage exists

type ValuationAgent: <"Details of the party calculating the value of collateral to be delivered or returned.">
    party CalculationValuationAgentPartyEnum (0..1) <"Specification of which party calculating the value of collateral to be delivered or returned.">
    valuationAgent ValuationAgentDeterminationEnum (0..1) <"Specifies how the Valuation Agent is determined.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AdditionalLanguage: <"If no party is defined, additional language must be used to specify the elective party.">
        if party is absent then additionalLanguage exists

type ExposureScope: <"Optionally, for each party, specifies the amendments to the included coverage in the calculation of Exposure.">
    partyElection LegacyExposureScopeElection (0..2) <"Defines the electice scope for a specific party.">

type LegacyExposureScopeElection: <"Specification of amendments to the calculation of Exposure in terms of the Transactions and Products covered.">
    party CounterpartyRoleEnum (1..1) <"The elective party.">
    excludedTransactions boolean (1..1) <"A boolean flag to represent whether any transactions are excluded (ie True) or not (ie False).">
    excludedProducts ExcludedProducts (0..*) <"Description of the relevant derivative transactions excluded from the calculation of exposure.">
    additionalLanguage string (0..1) <"The bespoke/additional language that might be specified by the parties to the legal agreement.">

type ExcludedProducts: <"Description of the relevant derivative transactions excluded from the calculation of exposure.">
    product ExposureScopeProductEnum (0..*) <"Description of the relevant derivative transactions excluded from the calculation of exposure.">
    branch BusinessCenterEnum (0..*) <"Description of the specific branch entered into by the relevant party.">
    excluded boolean (1..1) <"A flag to indicate whether transactions entered into by a specific branch with respect to the relevant party are excluded; set to True if excluded, false if included.">

type CreditSupportAmount: <"The total amount one counterparty must deliver to the other at any time: the combination of the Exposure to that party and the net Independent Amounts it must post, minus any agreed Threshold.">
    creditSupportAmount CreditSupportAmountEnum (1..1) <"Specifies whether the meaning of credit support amount follows the pre-print definition or deviates from the standard (either based on a rating or some other methodology).">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AdditionalLanguage: <"If a Non Standard Other definition is defined, additional language must be specified.">
        if creditSupportAmount = CreditSupportAmountEnum -> NonStandardOther
        then additionalLanguage exists

type LegacyResolutionAlternative: <"Alternative procedures to those in the pre-print for resolving collateral related disputes.">
    resolutionAlternative LegacyResolutionAlternativeEnum (0..1) <"Details of the alternative dispute resolution procedure (if any).">
    amount Money (0..1) <"The maximum amount to which the difference between the calculations by the valuation agent and disputing party can be split, where Split the Difference is elected.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition AdditionalLanguage: <"If no resolution alternative procedure is defined, additional language must be specified.">
        if resolutionAlternative is absent
        then additionalLanguage exists

    condition SplitDifferenceAmount: <"If the resolution is to split the difference, then the amount concerned should be specified.">
        if resolutionAlternative = LegacyResolutionAlternativeEnum -> SplitDifference
        then amount exists

type ResolutionValue: <"Methodology for resolving disputes in respect of the determination of the value of collateral.">
    cash ValueCashEnum (0..1) <"Details of how cash collateral is valued when resolving disputes.">
    securities ValueSecuritiesEnum (0..1) <"Details of how securities collateral is valued when resolving disputes.">
    fallback boolean (0..1) <"Represents whether a fallback calculation method is applicable. 'True' if fallback calculation method is applicable.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition OtherValues: <"Additional language is required when 'Other' values are used.">
        if cash = ValueCashEnum -> Other or securities = ValueSecuritiesEnum -> Other
        then additionalLanguage exists

type CollateralTransferTiming: <"Specification of transfer / settlement timing for cash and securities collateral assuming that the notice is provided before the Notification Time (and with a 1LBD lag introduced if the notice is delivered after the Notification Time), and that securities allow for a further Local Business Day for transfer / settlement.">
    transferSettlementTiming LegacyTransferSettlementTiming (0..1) <"Specification of transfer / settlement timing for cash and securities collateral assuming that the notice is provided before the Notification Time (and with a 1LBD lag introduced if the notice is delivered after the Notification Time), and that securities allow for a further Local Business Day for transfer / settlement.">
    legacyDefinition LegacyDefinition (0..1) <"Bespoke language removing the pre-print requirement to transfer legal title of securities collateral through written instructions to the relevant depository institution or other securities intermediaries solely for the purposes of the 1994 New York Law Credit Support Annex.">

type LegacyTransferSettlementTiming: <"Specification of transfer / settlement timing for cash and securities collateral assuming that the notice is provided before the Notification Time (and with a 1LBD lag introduced if the notice is delivered after the Notification Time), and that securities allow for a further Local Business Day for transfer / settlement.">
    cashCollateralTransferSettlementTime CashCTSTimeEnum (0..1) <"Details the day on which collateral is required to be transferred relative to the Notification Time.">
    securititesCollateralTransferSettlementTime SecuritiesCTSTimeEnum (0..1) <"Details the day on which collateral is required to be transferred relative to the Notification Time.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition OtherValues: <"Additional language is required when 'Other' values are used.">
        if cashCollateralTransferSettlementTime = CashCTSTimeEnum -> Other
                or securititesCollateralTransferSettlementTime = SecuritiesCTSTimeEnum -> Other
        then additionalLanguage exists

type LegacyDefinition: <"Bespoke language removing the pre-print requirement to transfer legal title of securities collateral through written instructions to the relevant depository institution or other securities intermediaries solely for the purposes of the 1994 New York Law Credit Support Annex.">
    isApplicable boolean (0..1) <"A boolean flag to represent bespoke amendment to the pre-print definition of Transfer.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition ApplicableLanguage: <"Additional language is required when a bespoke amendment is used.">
        if isApplicable = False then additionalLanguage exists

type CollateralValueMethod: <"Details how particular types of collateral are valued (other than in respect of a default).">
    cashCollateral ValueCashEnum (0..1) <"Details of how cash collateral is valued.">
    securitiesCollateral ValueSecuritiesEnum (0..1) <"Details of how securities collateral is valued.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

    condition OtherValues: <"Additional language is required when 'Other' values are used.">
        if cashCollateral = ValueCashEnum -> Other
                or securitiesCollateral = ValueSecuritiesEnum -> Other
        then additionalLanguage exists

type CreditSupportDocument: <"Identification of party specific Credit Support Documents applicable to the document.">

    creditSupportDocumentElection CreditSupportDocumentElection (2..2) <"The party election of Credit Support Document(s), if any.">

type CreditSupportDocumentElection: <"The party election of Credit Support Provider(s), if any.">
    party Party (1..1) <"The elective party.">
    creditSupportDocumentTerms CreditSupportDocumentTermsEnum (1..1) <"Specification of the Credit Support Document terms.">
    creditSupportDocument LegalAgreement (0..*) <"The specified Credit Support Document(s), if any.">
    bespokeCreditSuppportDocument string (0..1) <"Specification of a document when not captured under RelatedAgreement.">

    condition CreditSupportDocument: <"A validation rule to ensure that a Credit Support Document is specified when required.">
        if creditSupportDocumentTerms = CreditSupportDocumentTermsEnum -> Specified
        then creditSupportDocument exists

type CreditSupportProvider: <"Identification of party specific Credit Support Providers applicable to the document.">

    creditSupportProviderElection CreditSupportProviderElection (2..2) <"The party election of Credit Support Provider(s), if any.">

type CreditSupportProviderElection: <"The party election of Credit Support Provider(s), if any.">
    party Party (1..1) <"The elective party.">
    creditSupportProviderTerms CreditSupportProviderTermsEnum (1..1) <"Specification of the Credit Support Provider terms.">
    creditSupportProvider LegalEntity (0..*) <"The specified Credit Support Provider(s), if any.">
    bespokeCreditSuppportProvider string (0..1) <"...">

    condition CreditSupportProvider: <"A validation rule to ensure that a Credit Support Provider is specified when required.">
        if creditSupportProviderTerms = CreditSupportProviderTermsEnum -> Specified
        then creditSupportProvider exists

type SpecifiedCondition: <"Specifies events elected by the parties, that if triggered, can temporarily or permanently suspend rights to rehypothecate, transfer or substitute collateral.">
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(d)"]
        [docReference ISDA CSD_English_1995 paragraph "13" clause "(e)"]
    isApplicable boolean (1..1) <"Indicates whether events that could temporarily or permanently suspend rights to rehypothecate, transfer or substitute collateral are applicable. If applicable,'True'. If not applicable,'False'.">
    partyElection SpecifiedConditionPartyElection (0..2) <"The party specific elections.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type SpecifiedConditionPartyElection: <"The party specific elections in respect to the Specified Condition clause.">
    party CounterpartyRoleEnum (1..1) <"Identifies the party making the election.">
    specifiedCondition CSASpecifiedConditionEnum (0..*) <"Specifies events that may temporarily or permanently suspend a partys rights to rehypothecate, transfer, or substitute collateral.">
    additionalTerminationEvent AdditionalTerminationEventEnum (0..*) <"Specifies events that may lead to the early termination of the Master Agreement, including suspension of the affected party's rights to rehypothecate, transfer, or substitute collateral.">
    specifiedAdditionalTerminationEvent string (0..1) <"Specifies any bespoke Additional Termination Event(s) that are set out in the related Specified Condition clause.">

    condition AdditionalTerminationEvent: <"If Specified Condition is Additional Termination Event, then Additional Termination Event must be specified.">
        if specifiedCondition any = CSASpecifiedConditionEnum -> AdditionalTerminationEvents
        then additionalTerminationEvent exists
        else additionalTerminationEvent is absent

    condition SpecifiedAdditionalTerminationEvent: <"If Additional Termination Event is As Specified, then Additional Termination event must be specified.">
        if additionalTerminationEvent any = AdditionalTerminationEventEnum -> AsSpecified
        then specifiedAdditionalTerminationEvent exists
        else specifiedAdditionalTerminationEvent is absent

type LegacyIndependentAmount: <"Sets out the additional amount of collateral that is required to be delivered by that party irrespective of the mark-to-market exposure under the related Transactions. It may take the form of a fixed amount, or be determined using credit ratings.">
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(b)(iv)(A)"]
        [docReference ISDA CSA_English_1995 paragraph "11" clause "(b)(iii)(A)"]
        [docReference ISDA CSD_English_1995 paragraph "11" clause "(c)(iii)(A)"]
    partyElection LegacyIndependentAmountParty (2..2) <"The party specific elections for the Independent Amount clause.">
    additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">

type LegacyIndependentAmountParty: <"The party specific elections for the Independent Amount clause.">
    party CounterpartyRoleEnum (1..1) <"Identifies the Party making the election.">
    isApplicable boolean (1..1) <"Specifies whether Independent Amount is applicable to the elected party. If applicable, 'True', and if not applicable then 'False'.">
    fixedAmount Money (0..1) <"Indicates the Independent Amount is specified as a fixed amount.">
    ratingsBased LegacyIndependentAmountRatingsBased (0..1) <"Indicates the Independent Amount is determined using a credit rating.">
    ratingsXExposure LegacyIndependentAmountRatingsXExposure (0..1) <"Indicates the Independent Amount is determined using both credit rating and exposure metrics.">

    condition LegacyIAMethod: <"Ensures that the Independent Amount is specified as only one of Fixed Amount, Ratings Based or RatingsXExposure.">
        required choice fixedAmount, ratingsBased, ratingsXExposure

type IndependentAmountRatings: <"A logical container designed to hold a set of related data. In this case, designed to hold a set of information required when Independent Amount is determined using credit ratings.">
    compare IndependentAmountCompareEnum (1..1) <"Specifies how multiple credit ratings are compared when determining the applicable Independent Amount, and whether that measure is highest, lowest, or a comparison of either.">
    ratedParty RatedPartyEnum (1..1) <"Specifies the party to whom the rating agency rating applies to for the purposes of determining the Independent Amount.">
    namedAffiliate LegalEntity (0..1) <"Details the Named Affiliate where the Rated Party is Named Affiliate.">
    namedEntity LegalEntity (0..1) <"Details the Named Entity where the Rated Party is Named Entity.">
    ratingType RatingTypeEnum (1..1) <"The relevant rating type.">

    condition RestrictCompare: <"Restricts allowable values pertaining to how multiple credit ratings are compared when determining the applicable Independent Amount.">
        if compare exists
        then compare = IndependentAmountCompareEnum -> Compare
                or compare = IndependentAmountCompareEnum -> Lowest
                or compare = IndependentAmountCompareEnum -> Highest

    condition NamedAffiliate: <"Ensures Named Affiliate is provided where the Rated Party is Named Affiliate.">
        if ratedParty = RatedPartyEnum -> NamedAffiliate
        then namedAffiliate exists
        else namedAffiliate is absent

    condition NamedEntity: <"Ensures Named Entity is provided where the Rated Party is Named Entity.">
        if ratedParty = RatedPartyEnum -> NamedEntity
        then namedEntity exists
        else namedEntity is absent

type LegacyIndependentAmountRatingsBased extends IndependentAmountRatings: <"Specifies that the Independent Amount is determined using a credit rating.">
    variableSet RatingAgencyAmount (1..*) <"A logical container designed to hold a defined set of related data. In this case the collation of Rating Information and an associated Independent Amount.">
    currency string (0..1) <"Specifies the currency applicable to the relevant Independent Amount.">
        [metadata scheme]

    condition RestrictCompare: <"Restricts allowable values pertaining to how multiple credit ratings are compared when determining the applicable Independent Amount.">
        if compare exists
        then compare = IndependentAmountCompareEnum -> Lowest
                or compare = IndependentAmountCompareEnum -> Highest

type RatingAgencyAmount: <"A logical container to hold a defined set of related data. In this case the collation of Rating Information and an associated Independent Amount.">
    ratingVariableSet CreditNotation (1..*) <"Allows a grouping of Rating agencies and Rating values.">
    amount number (0..1) <"Specifies the notional or collateral Independent Amount.">

    condition RestrictingCreditNotation: <"Disables attributes that are not required from CreditNotation.">
        ratingVariableSet -> scale is absent
            and ratingVariableSet -> outlook is absent
            and ratingVariableSet -> debt is absent
            and ratingVariableSet -> creditWatch is absent
            and ratingVariableSet -> agency exists
            and ratingVariableSet -> notation exists

type LegacyIndependentAmountRatingsXExposure extends IndependentAmountRatings: <"Specifies that the Independent Amount is determined using both credit rating and exposure metrics.">
    highestLowestVariableSet RatingMultiplier (0..*) <"A logical container to hold a defined set of related data. In this case the collation of Ratings Information and associated exposure multiplier where highest or lowest of the associated credit ratings are used when determining the applicable Independent Amount.">
    compareVariableSet DirectionRatingMultiplier (0..*) <"A logical container to hold a defined set of related data. In this case the collation of Ratings Information and associated exposure multiplier where multiple credit ratings are compared when determining the applicable Independent Amount.">

    condition CompareIA: <"Ensures the appropriate variable set is populated based on how the Ratings are compared.">
        if compare = IndependentAmountCompareEnum -> Compare
        then compareVariableSet exists and highestLowestVariableSet is absent

    condition LowestIA: <"Ensures the appropriate variable set is populated based on how the Ratings are compared.">
        if compare = IndependentAmountCompareEnum -> Lowest
        then highestLowestVariableSet exists and compareVariableSet is absent

    condition HighestIA: <"Ensures the appropriate variable set is populated based on how the Ratings are compared.">
        if compare = IndependentAmountCompareEnum -> Highest
        then highestLowestVariableSet exists and compareVariableSet is absent

type RatingMultiplier: <"A logical container to hold a defined set of related data. In this case the collation of Ratings Information and associated exposure multiplier.">
    ratingVariableSet CreditNotation (1..*) <"Allows a grouping of Rating agencies and Rating values.">
    exposureMultiplier number (1..1) <"Specifies the multiplier applied to exposure based on the credit rating of the rated party.">

    condition RestrictingCreditNotation: <"Disables attributes that are not required from CreditNotation.">
        ratingVariableSet -> scale is absent
            and ratingVariableSet -> outlook is absent
            and ratingVariableSet -> debt is absent
            and ratingVariableSet -> creditWatch is absent
            and ratingVariableSet -> agency exists
            and ratingVariableSet -> notation exists

type DirectionRatingMultiplier: <"A logical container to hold a defined set of related data. In this case the collation of Ratings Information and associated exposure multiplier.">
    variableSet DirectionRatingSet (1..*) <"A logical container to hold a defined set of related data. In this case the collation of Ratings Agency, Rating value and an associated direction.">
    exposureMultiplier number (1..1) <"Specifies the multiplier applied to exposure based on the credit rating of the rated party.">

choice DirectionRatingSet: <"A logical container to hold a defined set of related data. In this case the collation of Ratings Agency, Rating value and an associated direction that are grouped by either AND or OR logic.">
    AllDirectionRating <"Enables two or more Direction and Ratings to be combined using AND logic.">
    AnyDirectionRating <"Enables two or more Direction and Ratings to be combined using OR logic.">
    DirectionRating <"A logical container to hold a defined set of related data. In this case the collation of Ratings Agency, Rating value and an associated direction.">

type AllDirectionRating: <"Used to combine two or more Ratings and associated direction using AND logic.">
    allDirectionRating DirectionRatingSet (2..*)

type AnyDirectionRating: <"Used to combine two or more Ratings and associated direction using OR logic.">
    anyDirectionRating DirectionRatingSet (2..*)

type DirectionRating extends CreditNotation: <"A logical container to hold a defined set of related data. In this case the collation of Ratings Agency, Rating value and an associated direction.">
    direction CompareOp (1..1) <"Specifies the direction of measurement for the rated party.">

    condition RestrictingCreditNotation: <"Disables attributes that are not required from CreditNotation.">
        (scale is absent and debt is absent and outlook is absent and creditWatch is absent)
            and (agency exists and notation exists)

type EligibilityToHoldCollateral: <"Specifies the conditions under which a party and its custodian(s) are entitled to hold collateral.">
    [docReference ISDA CSA_VM_2016 paragraph "13" clause " (h)(i)" name "Eligibility to Hold Posted Collateral (VM) Custodians (VM)"]
    custodianEligibility boolean (1..1) <"Specifies whether the party's custodian is eligible to hold the other party's posted collateral. If 'True' then the custodian is eligible to hold the Posted Collateral given that the specified criteria are met.">
    initialCustodian LegalEntity (0..1) <"Election to specify the initial custodian.">
        [docReference ISDA CSA_VM_2016]
    criteria EligibilityToHoldCollateralCriteria (0..1) <"A logical container to hold a defined set of related data. In this case the collation of criteria required by the party and its custodian to Hold the other party's Posted Collateral.">
    eligibleCountry string (0..*) <"The restrictions that might be required by a party and its custodian in terms of country(ies) where the other party's collateral can be held.">
        [metadata scheme]
    custodianCondition string (0..1) <"Specifies any additional conditions relevant to the Custodian of the respective parties as set out in the Holding and Using Posted Collateral clause.">

    condition Custodian: <"A condition to ensure that if a Custodian is eligible to hold collateral, the Initial Custodian must be specified.">
        if custodianEligibility = True
        then initialCustodian exists
        else initialCustodian is absent and custodianCondition is absent

choice EligibilityToHoldCollateralCriteria: <"Defines a set of Criteria required by the party and its custodian to hold the other party's Posted Collateral.">
    AllEligibilityToHoldCollateralCriteria <"Enables two or more Criteria to be combined using AND logic.">
    AnyEligibilityToHoldCollateralCriteria <"Enables two or more Criteria to be combined using OR logic.">
    RatingAgencyValue <"A logical container designed to define a combination of Ratings based attributes. In this case, the minimum Rating Value, associated Rating Agency and Rating Type for the Party/Custodian to be entitled to hold Posted Collateral.">
    AmountOfNetCapital <"The minimum amount of net capital to be entitled to hold Posted Collateral.">

type AllEligibilityToHoldCollateralCriteria: <"Used to combine two or more Criteria using AND logic.">
    allCriteria EligibilityToHoldCollateralCriteria (2..*)

type AnyEligibilityToHoldCollateralCriteria: <"Used to combine two or more Criteria using OR logic.">
    anyCriteria EligibilityToHoldCollateralCriteria (2..*)

type AmountOfNetCapital: <"The amount of net capital.">
    amountOfNetCapital Money (0..1) <"The amount of net capital.">

type RatingAgencyValue extends CreditNotation: <"A logical container designed to hold a set of related data. In this case the Rating Agency, Rating Value and Rating Type.">
    ratingType RatingTypeEnum (1..1) <"The relevant rating type.">

    condition AgencyValueType: <"A condition to ensure that either all or no rating based information is provided.">
        ((agency exists and notation exists and ratingType exists)
                or (agency is absent and notation is absent and ratingType is absent))
            and (scale is absent and debt is absent and outlook is absent and creditWatch is absent)

type DemandsAndNotices: <"The optional specification of address where the demands, specifications and notices will be communicated to for each of the parties to the agreement.">
    [docReference ISDA CSA_NewYork_1994 paragraph "13" clause "(k)"]
    partyElection PartyContactInformation (2..2) <"The parties' contact information election.">
    deemedEffectiveNextLBD boolean (1..1) <"Specifies if notice shall be deemed to be effective at the time such notice is actually received unless such notice is received on a day which is not a Local Business Day or after the Notification Time on any Local Business Day in which event such notice shall be deemed to be effective on the next succeeding Local Business Day. If deemed effective,'True'. If not deemed effective, or not stated,'False'.">
