namespace cdm.base.staticdata.identifier : <"Basic identifier and assigned identifier concepts that are applicable across the model.">
version "${project.version}"

import cdm.base.*
import cdm.base.staticdata.party.*

type AssignedIdentifier: <"A class to specify the identifier value and its associated version.">
	identifier string (1..1) <"The identifier value.">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value "tradeId" meta "tradeIdScheme"]
		[synonym FpML_5_10 value "tradeId" path "versionedTradeId" meta "tradeIdScheme"]
		[synonym Workflow_Event value "identifier"]
		[synonym CME_ClearedConfirm_1_17 value "tradeId" meta "tradeIdScheme"]
		[synonym CME_ClearedConfirm_1_17 value "usi" meta "usiScheme"]
		[synonym CME_SubmissionIRS_1_0 value "RptID" path "TrdCaptRpt"]
		[synonym FpML_5_10 value "businessUnitId" meta "unitScheme"]
		[synonym ISDA_Create_1_0 value "id"]
	version int (0..1) <"The identifier version, which is specified as an integer and is meant to be incremented each time the transaction terms (whether contract or event) change. This version is made option to support the use case where the identifier is referenced without the version. The constraint that a contract and a lifecycle event need to have an associated version is enforced through data rules.">
		[synonym FpML_5_10 value "version" path "versionedTradeId"]
		[synonym FpML_5_10 value "version"]


type Identifier: <"A class to specify a generic identifier, applicable to CDM artefacts such as executions, contracts, lifecycle events and legal documents. An issuer can be associated with the actual identifier value as a way to properly qualify it.">
	[metadata key]
	[synonym FpML_5_10 meta "id"]
	[synonym FpML_5_10, CME_SubmissionIRS_1_0 meta "id" path "versionedTradeId->tradeId"]
	issuerReference Party (0..1) <"The identifier issuer, when specified by reference to a party specified as part of the transaction.">
		[metadata reference]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value "issuer" meta "href"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value "partyReference" meta "href"]
		
		[synonym CME_ClearedConfirm_1_17 value "partyReference" meta "href"]
	issuer string (0..1) <"The identifier issuer, when specified explicitly alongside the identifier value (instead of being specified by reference to a party).">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value "issuer" meta "issuerIdScheme"]
		
		
		[synonym CME_ClearedConfirm_1_17 value "issuer" meta "issuerIdScheme"]
	assignedIdentifier AssignedIdentifier (1..*) <"The identifier value. This level of indirection between the issuer and the identifier and its version provides the ability to associate multiple identifiers to one issuer, consistently with the FpML PartyTradeIdentifier.">
	condition IssuerChoice: <"The identifier issuer is specified either explicitly or by reference to one of the parties.">
		required choice issuerReference, issuer

		
		
 