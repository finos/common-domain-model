namespace "org.isda.cdm"
version "%project.version%"

class AccessConditions <"A class to specify each party's election with respect to the Termination Events that will be deemed an Access Condition. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii).">
{
	partyElections TerminationEvents (2..2) <"The parties' Access Condition elections.">;
}

class AdditionalRegime extends RegimeTerms <"A class to specify the additional regulatory regime(s) that might be specified by the parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin.">
{
	regime string (1..1) <"The additional regulatory regime as specified by the parties.">;
		[synonym ISDA_Create_1_0 value regime_name]
}

class AdditionalRepresentation <"A class to specify the Additional Representation. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (o): Additional Representation(s).">
{
	/*
	 *  TODO - The ISDA Create template for 2016 CSA #44 provide for each part to specify an applicable boolean election alongside with a common specification in the form of a string field.  Having compared two documents which  have distinct elections with respect to the former attribute, I don’t see such election reflected in the pdf resulting document.
	 */
	partyElection PartyAdditionalRepresentation (2..2);
	specifiedProvision string (0..1) <"">;
	// TODO - Need for a relevant example to specify the mapping
}

class AdditionalType <"The specification of the Additional Type of transaction that can require the collection or delivery of initial margin under a given regulatory regime for the purposes of Covered Transactions, as specified in ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(B).">
{
	standardValue AdditionalTypeEnum (1..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a standard value.">;
		[synonym ISDA_Create_1_0 value additional_type]
	customValue string (0..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a custom value by the parties to the legal agreement.">;
		[synonym ISDA_Create_1_0 value additional_type_specify]
}

data rule AdditionalType_customValue <"The specification of a custom value by the parties to the legal agreement takes place alongside the qualification of the `Other` value as part of the AdditionalTypeEnum.">
	when AdditionalType -> standardValue = AdditionalTypeEnum.Other
	then AdditionalType -> customValue exists

data rule AdditionalType_standardValue <"The specification of a standard value by the parties to the legal agreement is done through the qualification of a value distinct than `Other` as part of the AdditionalTypeEnum, and implies that the customerValue is not being qualified.">
	when AdditionalType -> standardValue <> AdditionalTypeEnum.Other
	then AdditionalType -> customValue is absent	

class ApplicableRegime extends RegimeTerms <"A class to specify the applicable regulatory regime(s) that parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin, might be subject to.">
{
	regime RegulatoryRegimeEnum (1..1) <"The applicable regulatory regime, as specified through an enumeration.">;
		[synonym AcadiaSoft_AM_1_0 value regime]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.AustraliaMarginRules when "australia" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.CanadaMarginRules when "canada" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.CFTC_MarginRules when "cftc" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.EMIR_MarginRules when "emir" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.HongKongMarginRules when "hongkong" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.JapanMarginRules when "japan" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SEC_MarginRules when "sec" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SingaporeMarginRules when "singapore" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SwitzerlandMarginRules when "switzerland" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.US_PrudentialMarginRules when "prudential" exists]
}

class CalculationDateLocation <"A class to specify the Calculation Date Location election for the respective parties to the legal agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(i).">
{
	partyElections CalculationDateLocationElection (2..2) <"The parties' calculation date location elections.">;
}

class CalculationDateLocationElection <"A class to specify each of the party elections with respect to the Calculation Date Location.">
{
	party Party (1..1) reference <"The party which the Calculation Date Location election pertains to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	businessCenter BusinessCenterEnum (0..1) scheme <"The Calculation Date Location when specified as a business center which corresponds to the FpML list of business centers or can be mapped to it.">;
		[synonym ISDA_Create_1_0 value partyA_location, partyB_location]
	customLocation string (0..1) <"The Calculation Date Location when specified a location which doesn't correspond to the FpML list of business centers or cannot be mapped to it.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

choice rule CalculationDateLocationElection_choice <"The Calculation Date Location can be specified either as a standard business center, or as a custom location.">
	for CalculationDateLocationElection required choice between
	businessCenter and customLocation

class ChargorPostingObligations extends PostingObligations <"A class to specify the chargor(s) collateral posting obligations as specified under the terms of the English Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	chargor Party (1..2) reference <"The chargor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
		[synonym ISDA_Create_1_0 value chargor]
	// See mapping comment in PledgorPostingObligations class	
}

class CollateralRounding <"A class to specify the rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding.">
{
	deliveryAmount number (1..1) <"The rounding methodology applicable to the Delivery Amount in terms of nearest integral multiple of Base Currency units.">;
		[synonym ISDA_Create_1_0 value delivery_amount]
	returnAmount number (1..1) <"The rounding methodology applicable to the Return Amount in terms of nearest integral multiple of Base Currency units.">;
		[synonym ISDA_Create_1_0 value return_amount]
} 

class CreditSupportObligation <"A class to specify the Credit Support Obligations related to margin agreements. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations.">
{
	otherEligibleSupport string (0..1) <"The items that qualify as Other Eligible Support in accordance with the ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(iv). ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (vi)(C): Rounding.">;
		[synonym ISDA_Create_1_0 set to "other_eligible_support.eligible_support_specify" when "other_eligible_support.other_eligible_support" = "specify"]
	threshold Threshold (1..1) <"The amount of net exposure that a party is willing to bear in relation to the other party before it requires asking for collateral.">;
		[synonym ISDA_Create_1_0 value threshold]
	minimumTransferAmount MinimumTransferAmount (1..1) <"The net amount of exposure reached before collateral has to be posted or returned.">;
		[synonym ISDA_Create_1_0 value minimum_transfer_amount]
	rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding">;
		[synonym ISDA_Create_1_0 value rounding]
	transferTiming string (0..1) <"The time by which the transfer of collateral must take place when different from the Regular Settlement Day as a result of parties' election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vii): Transfer Timing.">;
		[synonym ISDA_Create_1_0 value settlement_day_specify path "transfer_timing"]
}

abstract class CsaInitialMargin2016 <"An abstract class to specify the provisions for the 2016 ISDA Credit Support Annex for Initial Margin that are common among the corresponding governing laws.">
{
	regime Regime (1..1) <"The Regime Table provision as specified by the ISDA 2016 CSA for Initial Margin, Paragraph 13 General Principles, which determines the regulatory regime(s) applicable to each of the parties to the CSA in their capacity as Secured Party (English Law & New York Law) or Obligee (Japanese Law), with one set of values per counterparty. As specified in the CSA, the applicability of a regime shall not be construed as a representation, admission or acknowledgement that either party is actually regulated under such regime.">;
	oneWayProvisions OneWayProvisions (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false) as specified by ISDA 2016 CSA for Initial Margin, Paragraph 13, (aa).">;
	method Method (1..1) <"The specification of the ISDA SIMM Method for all Covered Transactions with respect to all Regimes. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee).">;
	identifiedCrossCurrencySwap boolean (1..1) <"The qualification of whether cross-currency swaps need to be identified in the Confirmation so that the obligations to exchange principal be disregarded for the purpose of determining the Delivery Amount or Return Amount. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (gg)(1).">;
		[synonym ISDA_Create_1_0 set to True when path = "cross_currency_swap.yey_ney.true",
			set to False when path = "cross_currency_swap.yey_ney.false"]
	sensitivityToEquity SensitivityMethodology (1..1) <"The methodology to compute sensitivities to equity indices, funds and ETFs for the purpose of the ISDA 2016 and 2018 CSA for Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (gg)(2)(A).">;
		[synonym ISDA_Create_1_0 value sensitivities_to_equity_indices_funds_etfs]
	sensitivityToCommodity SensitivityMethodology (1..1) <"The methodology to compute sensitivities to commodity indices for the purpose of the ISDA 2016 and 2018 CSA for Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (gg)(2)(B).">;
		[synonym ISDA_Create_1_0 value sensitivities_to_commodity_indices]
	baseCurrency string (1..1) scheme <"The base currency as specified by the ISDA 2016 CSA for Initial Margin, Paragraph 13 (a). The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value base_currency]
	additionalObligations string (0..1) <"The additional obligations as specified in the ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(ii).">;
		[synonym ISDA_Create_1_0 set to "additional_obligations.additional_obligations_specify" when "additional_obligations.additional_obligations" = "specify"]
	creditSupportObligation CreditSupportObligation (1..1) <"The Credit Support Obligations related to margin agreements. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations.">;
	calculationDateLocation CalculationDateLocation (1..1) <"The specified location where the credit exposure will be calculated by the respective parties.">;
		[synonym ISDA_Create_1_0 value calculation_date_location]
	notificationTime NotificationTime (1..1) <"The time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iii).">;
		[synonym ISDA_Create_1_0 value notification_time]
	conditionPrecedent string (0..1) <"When specified, this attribute overwrites the default Condition Precedent provision as specified in ISDA 2016 Credit Support Annex for Initial Margin, paragraph 4, (a).">;
		[synonym ISDA_Create_1_0 value conditions_precedent]
	accessConditions AccessConditions (1..1) <"The Termination Event(s) that will be deemed Access Conditions by the respective parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii).">;
		[synonym ISDA_Create_1_0 value access_conditions]
	substitution Substitution (1..1) <"The conditions under which the Pledgor can substitute posted collateral. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Substitution.">;
		[synonym ISDA_Create_1_0 value consent]
	disputeResolution DisputeResolution (1..1) <"The election terms under which a party disputes (i) the Calculation Agent’s calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution.">;
	pledgorRightsEvent PledgorRightsEvent (1..1) <"The Pledgor Rights Event election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (j): Pledgor Rights Event.">; // TODO - Look to enhance this definition, as it's not clear what it means in layman terms
		[synonym ISDA_Create_1_0 value pledgor_rights_event]
	pledgorAdditionalRightsEvent PledgorAdditionalRightsEvent (0..1) <"The Pledgor Additional Rights Event election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Pledgor Additional Rights Event.">; // TODO - Look to enhance this definition, as it's not clear what it means in layman terms;
		[synonym ISDA_Create_1_0 value pledgor_additional_rights_event]
	deliveryInLieuRight boolean (1..1) <"The Delivery in Lieu Right election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m): Modification to Pledgor’s Rights and Remedies.">; // TODO - Look to enhance this definition, as it's not clear what it means in layman terms;
		[synonym ISDA_Create_1_0 set to True when path = "delivery_in_lieu_right.right.applicable",
			set to False when path = "delivery_in_lieu_right.right.not_applicable"]
	custodyArrangements CustodyArrangements (1..1) <"The Custodian and Segregated Account details in respect of each party as the Pledgor. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Custody Arrangements.">;
    additionalRepresentation AdditionalRepresentation (1..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (o): Additional Representation(s).">;
	otherEligibleSupport OtherEligibleSupport (1..1) <"The Other Eligible Support elections associated with margin agreements. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM).">;
	demandsAndNotices PartyContactInformation (1..1) <"The address where the demands, specifications and notices will be communicated to within each of the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (q): Demands and Notices.">;
	addressesForTransfer PartyContactInformation (1..1) <"The addresses for transfer as specified by the respective parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (r): Addresses for Transfers.">;
	terminationCurrency TerminationCurrencyAmendment (1..1) <"The Amendment to Termination Currency elections by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t): Amendment to Termination Currency.">;
		[synonym ISDA_Create_1_0 value amendment_to_termination_currency]
	bespokeProvision string (0..1) <"The bespoke provision that might be specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (u)(iii): Other Provisions.">;
		[synonym ISDA_Create_1_0 value specify path "bespoke_provisions"]
	japaneseSecuritiesProvisions boolean (1..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (v): Japanese Securities Provisions (Shichiken).">;
		[synonym ISDA_Create_1_0 set to True when path = "japanese_securities_provisions.is_applicable.applicable",
			set to False when path = "japanese_securities_provisions.is_applicable.not_applicable"]
}

class CsaInitialMargin2016EnglishLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the English Law version of the ISDA 2016 Credit Support Annex for Initial Margin.">
{
	chargorPostingObligations ChargorPostingObligations (1..1) <"The chargor's posting obligations as specified in the English Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value chargor_posting_obligations]
}

class CsaInitialMargin2016JapaneseLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the Japanese Law version of the ISDA 2016 Credit Support Annex for Initial Margin.">
{
	obligorPostingObligations ObligorPostingObligations (1..1) <"The obligor's posting obligations as specified in the Japanese Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value obligor_posting_obligations]
}

class CsaInitialMargin2016NewYorkLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the New York Law version of the ISDA 2016 Credit Support Annex for Initial Margin.">
{
	pledgorPostingObligations PledgorPostingObligations (1..1) <"The pledgor's posting obligations as specified in the New York Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value pledgor_posting_obligations]
}

data rule CreditSupportAnnexInitialMargin2018EnglishLaw_baseCurrency <"The ISDA 2016 CSA for Initial Margin under English and New York Law specify that the base currency should either be USD or EUR. ISDA 2016 Credit Support Annex for Initial Margin, New York Law, paragraph 13, General Principles, (a)(i): Base Currency means United States Dollar/Euro.">
	when LegalDocument -> csaInitialMargin2016NewYorkLaw exists
	then LegalDocument -> csaInitialMargin2016NewYorkLaw -> baseCurrency = "USD"
		or LegalDocument -> csaInitialMargin2016NewYorkLaw -> baseCurrency = "EUR"

class CreditNotation <"A class to specify the credit notation as the combination of agency, notation, scale and debt type qualifications.">
	[synonym FpML_5_10 value CreditNotation]
{
	agency CreditRatingAgencyEnum (1..1) scheme <"The credit agency to which the other variables (notation, scale, debt type) refer to.">;
		[synonym FpML_5_10 value agency meta creditRatingAgencyScheme]
	notation string (1..1) scheme <"The credit rating notation. As it varies among credit rating agencies, FpML doesn't specify a default scheme.">;
		[synonym FpML_5_10 value notation meta creditRatingNotationScheme]
	scale string (0..1) scheme <"The credit rating scale, with a typical distinction between short term, long term. FpML doesn't specify a default scheme, which is hence not specified as an enumeration as part of the CDM.">;
		[synonym FpML_5_10 value scale meta creditRatingScaleScheme]
	debt CreditRatingDebt (0..1) <"The credit rating debt type (e.g. long term, high yield, deposits, ...) associated with the credit rating notation and scale.">;
		[synonym FpML_5_10 value debt]
}

class CreditNotations one of <"The credit rating notation higher level construct, which provides the ability to specify multiple rating notations.">
	[synonym FpML_5_10 value CreditNotations]
{
	creditNotation CreditNotation (1..1) <"This attribute is specified when only one credit notation is determined.">;
		[synonym FpML_5_10 value creditNotation]
	creditNotations MultipleCreditNotations (2..*) <"This attribute provides the ability to specify several credit notations, alongside an 'any' or 'all' or all condition.">;
}

class CreditRatingDebt one of <"The credit rating debt type(s) associated with the credit rating notation and scale. When several debt types are specified, they must be qualified through an 'any' or 'all'.">
	[synonym FpML_5_10 value CreditRatingDebt]
{
	debtType string (0..1) scheme <"This attribute is to be specified when only one debt type is specified. FpML doesn't specify values in relation to the associated scheme, which is hence not specified as an enumeration as part of the CDM.">;
		[synonym FpML_5_10 value debtType meta debtTypeScheme]
	debtTypes MultipleDebtTypes (0..1) <"This attribute provides the ability to specify several debt types, alongside an 'any' or 'all' or all condition. As an example, Baa1 rating is required for any long term debt and deposit.">;
}

class CustodianEvent <"A class to specify the Custodian Event in terms of applicability and end-date. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): Custodian Event.">
{
	isApplicable boolean (1..1) <"The qualification as to whether the Custodian Event is applicable. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): Custodian Event.">;
	// TODO - Need to confirm through sample document that the CE End date needs to be specified if the Custodian Event is not applicable
	endDate CustodianEventEndDate (1..1);
		[synonym ISDA_Create_1_0 value ce_end_date]
}

class CustodianEventEndDate <"A class to specify the Custodian Event End Date, which qualification is function of three elective periods: either (i) a specified number of days after the occurrence of the Custodian Event (the daysAfterCustodianEvent attribute), (ii) or the number of days prior to the date on which the Control Agreement will terminate, with in this latter case the further qualification of the number of days prior to the Release Date if only one party has effectively provided the Timely Statement to the other party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): CE End Date.">
{
	daysAfterCustodianEvent CustomisableOffset (1..1) <"The parties' election to specify the number of days after the occurrence of the Custodian Event for the purpose of qualifying the Custodian Event End Date. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii)(1)(A).">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `after`
	releaseDate CustomisableOffset (1..1) <"The parties' election to specify the number of days prior to the Control Agreement termination for the purpose of qualifying the Custodian Event End Date, in the case where advance notice is given in accordance with the Control Agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii)(1)(B).">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `release`
	dateOfTimelyStatement CustomisableOffset (1..1) <"The parties' election to specify the number of days one party has effectively provided the Timely Statement to the other party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii)(1)(B)(ii).">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `timely` or `date_of_timely`
}

class CustomisableOffset <"A class to specify an offset either as a normalized [multiplier, period, dayType] or as a custom provision of type string.">
{
	offset Offset (0..1);
	customProvision string (0..1);	
}

class CustodianRisk <"A class to specify the Custodian Risk. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Custodian (IM) Risk.">
{
	isSpecified boolean (1..1) <"The qualification as to whether the risk is deemed as Specified.">;
		[synonym ISDA_Create_1_0 set to True when "partyA_custodian_risk" = "other",
			set to False when "partyA_custodian_risk" = "not_specified",
			set to True when "partyB_custodian_risk" = "other",
			set to False when "partyB_custodian_risk" = "not_specified"]
	qualification string (0..1) <"The Custodian Risk qualification. This attribute is optional because a Custodian Risk can be deemed as 'specified', but not be qualified.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

data rule CustodianRisk_specified <"The Custodian Risk should only be qualified if that risk is deemed 'specified'.">
	when CustodianRisk -> isSpecified = False
	then CustodianRisk -> qualification is absent

class CustodyArrangements <"A class to specify the Custodian and Segregated Account details with respect to each party as the Pledgor, and to specify the Custodian Event provision and the terms with respect to the Control Agreement that will apply to both parties. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Custody Arrangements. | ISDA 2016 Credit Support Annex for Initial Margin, paragraph 6: Custody Arrangements and the Control Agreement.">
{
	partyCustodian PartyCustodianElection (2..2) <"The parties' custody agent elections.">;
	custodianEvent CustodianEvent (1..1) <"When specified as True, the Custodian Event means that the following Custodian Events specified in the agreement will constitute an Additional Termination Event. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(iii): Custodian Event.">;
		[synonym ISDA_Create_1_0 set to True when path = "custodian_event.definition.applicable",
			set to True when path = "custodian_event.definition.not_applicable"]
	isCreditSupportDocument boolean (1..1) <"Unless specified as True, the Control Agreement is not a Credit Support Document under the agreement with respect to a party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 6 (e): The Control Agreement as a Credit Support Document.">;
		[synonym ISDA_Create_1_0 set to True when path = "control_agreement_as_a_credit_support_document.definition.creditSupportDocument",
			set to False when path = "control_agreement_as_a_credit_support_document.definition.not_specified"]
	hasControlAgreementLanguage boolean (1..1) <"">;
		[synonym ISDA_Create_1_0 set to True when path = "relationship_with_the_control_agreement.include_language.include",
			set to False when path = "relationship_with_the_control_agreement.include_language.exclude"] // TODO - mapping to be confirmed through relevant example 
	otherProvisions string (0..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(vii): Other Provisions.">;
}

class DisputeResolution one of <"A class to specify the election terms under which a party disputes (i) the Calculation Agent’s calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support. Parties can specify such election either through a business center time or through a custom election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution.">
{
	resolutionTime BusinessCenterTime (0..1) <"The time by which the dispute needs to be resolved, failure of which would trigger a recalculation alongside a process that is specified as part of the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g)(i): Resolution Time.">;
		[synonym ISDA_Create_1_0 value resolution_time]
	otherTerms string (0..1) <"The custom Resolution Time election that might be specified by the parties.">;
		// TODO - ISDA Create synonym to be confirmed through sample document
}

class EligibleCollateral <"The collateral eligibility as a function of the types of asset, the maturity and rating terms.">
	[synonym FpML_5_10 value EligibleCollateral]
{
	/*
	 * ISDA Create specifies the eligible collateral as well as the valuation % as a string. Let's discuss if there is room a for better approach.
	 * The below approach which combines the asset, the tenor and the credit rating qualification is extremely comprehensive and was vetted extensively as part of the
	 * FpML standard CSA initiative.
	 */
	eligibleAsset CollateralAssetDefinitionsEnum (1..1) <"The eligible asset is specified by reference to the ISDA 2003 Collateral Assets definitions.">;
		[synonym FpML_5_10 value eligibleAsset]
		[synonym ISDA_Create_1_0 value items] // This mapping will not actually work as such because ISDA Create is specified as a string
	lowerMaturity Period (0..1) <"The (optional) lower maturity band of the eligible collateral assets.">;
		[synonym FpML_5_10 value lowerMaturity]
	higherMaturity Period (0..1) <"The (optional) higher maturity band of the eligible collateral assets.">;
		[synonym FpML_5_10 value higherMaturity]
	minimumRating CreditNotations (0..1) <"The (optional) minimal rating terms that can be specified as part of the eligible collateral.">;
		[synonym FpML_5_10 value minimumRating]
	valuationPercentage number (0..1) <"Specifies the haircut associated to the collateral asset, expressed as a percentage. A value of 90% means that 90% of the value of the applicable asset is considered as collateral. Absence of this element implies that the valuation percentage is 100%.">;
		[synonym FpML_5_10 value valuationPercentage]
		[synonym ISDA_Create_1_0 value valuation_percentage] // This mapping will not work in a number of cases, as the ISDA Credit field is specified as a string
}

data rule EligibleCollateral_valuationPercentage <"The valuation percentage should be specified as a value between 0 and 1.">
	when EligibleCollateral -> valuationPercentage exists
	then EligibleCollateral -> valuationPercentage >= 0
	and EligibleCollateral -> valuationPercentage <= 1

class IndependentAmountEligibleCollateral <"A class to specify the eligible collateral for initial margin purposes. Those features go beyond what is required for meeting the terms specified by the ISDA 2016 CSA for Initial Margin.">
	[synonym FpML_5_10 value IndependentAmountEligibleCollateral]
{
	party Party (1..1) reference <"The party which the eligible collateral election applies to.">;
		[synonym FpML_5_10 value partyReference]
	eligibleCollateral EligibleCollateral (0..*) <"The eligible collateral assets.">;
		[synonym FpML_5_10 value eligibleCollateral]
	independentAmountEligibility IndependentAmountEligibilityEnum (0..1) scheme <"The terms when no collateral assets are specified.">;
		[synonym FpML_5_10 value independentAmountEligibility meta independentAmountEligibilityScheme]
	fxHaircut number (0..1) <"The ISDA 2018 CSA specifies the FX haircut with respect to each party's posting obligation to be equal to 8% if the eligible collateral is denominated in a currency that is distinct from the Termination Currency specified with respect to the Secured Party, and 0% if the same.">;
	terminationCurrency PartyTerminationCurrency (2..2) <"The Termination Currency election by the parties to the agreement. ISDA 2018 CSA for Initial Margin, Paragraph 13 Eligible Credit Support (IM) Schedule.">;
}

choice rule IndependentAmountEligibleCollateral_choice <"The independentAmountEligibility attribute provides alternative terms to specifying eligible collateral.">
	for IndependentAmountEligibleCollateral required choice between
	eligibleCollateral and independentAmountEligibility

//data rule IndependentAmountEligibleCollateral_fxHaircut <"The ISDA 2018 CSA specifies the FX haircut with respect to each party's posting obligation to be equal to 8% if the eligible collateral is denominated in a currency that is distinct from the Termination Currency specified with respect to the Secured Party, and 0% if the same.">
//	when CsaInitialMargin2018 -> eligibleCollateral -> fxHaircut exists
//	then CsaInitialMargin2018 -> eligibleCollateral -> fxHaircut = 0.08
//	or CsaInitialMargin2018 -> eligibleCollateral -> fxHaircut = 0.0

class LegalDocument extends LegalDocumentBase rosettaKey one of <"A class to specify the elections and variables that characterize a legal document.">
{
	csaInitialMargin2016EnglishLaw CsaInitialMargin2016EnglishLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Initial Margin, English Law.">;
	csaInitialMargin2016JapaneseLaw CsaInitialMargin2016JapaneseLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Initial Margin, Japanese Law.">;
	csaInitialMargin2016NewYorkLaw CsaInitialMargin2016NewYorkLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Initial Margin, New York Law.">;
}
/*
 * TODO ISDA Create information other than elections should be accessed from the API  /api/v1/subAccounts/{subAccountId}/negotiations/{negotiationId}
 * Need to work with Victor Parashiv @ Linklater to confirm how to access that API, which requires an AccountId and NegociationId
 * See email sent to Pierre on 28 Feb 2019
 */
abstract class LegalDocumentBase <"A class describing the legal document. The purpose of this abstract class is to provide the ability to associate a 'one of' feature to the LegalDocument class as a way to enforce a systematic choice between its attributes.">
{
	agreementDate date (1..1) <"The date on which the legal document has been agreed between the parties. This corresponds to the Date of Deed in an English Law document.">;
		[synonym FpML_5_10 value agreementDate path "legalDocumentHeader"]
		[synonym ISDA_Create_1_0 value execution_date]
	effectiveDate date (0..1) <"The date on which the agreement is effective, if different from the agreement date. It is expected that it will most often correspond to the agreement date, although there could be situations where the parties will explicitly agree on a distinct effective date.">;
		[synonym FpML_5_10 value effectiveDate path "legalDocumentHeader"]
	identifier Identifier (0..*) <"The legal document identifier. Several identifiers can be specified.">;
		[synonym FpML_5_10 value partyDocumentIdentifier path "legalDocumentHeader"]
		[synonym ISDA_Create_1_0 value id]
	lineage Lineage (0..1) <"The lineage into prior versions of this legal document or into other legal documents that might be referenced by it.">;
	documentType LegalDocumentType (1..1) <"The type of legal document, identified via a set of distinct attributes: name, publisher, governing law and version, e.g. ISDA 2013 Standard Credit Support Annex English Law.">;
		[synonym FpML_5_10 value documentType]
	contractualparty Party (2..*) reference <"The contractual parties to the legal document, which reference information is positioned as part of the partInformation attribute.">;
		[synonym FpML_5_10 value party]
	otherParty PartyRole (0..*) <"The role(s) that other party(ies) may have in relation to the legal agreement, further to the contractual parties.">;
	partyInformation Party (2..*) <"The information about the parties involved with the legal documents, which consists of both the contractual parties and the parties that might be involved in relation to some other roles (e.g. custody agent).">;
}

class LegalDocumentAmendment <"A legal document amendment, which can be specified by either referencing a legal document that is described elsewhere in the document, or by specifying the identity of that amended document.">
{
	agreementDate date (1..1) <"The date on which the legal document has been agreed between the parties.">;
		[synonym FpML_5_10 value agreementDate]
	documentIdentifier Identifier (0..*) <"The legal document identifier. Several identifiers can be specified.">;
		[synonym FpML_5_10 value partyDocumentIdentifier]
}

class LegalDocumentType <"A class to specify the type of legal document, which is extended by each legal document instance, such as the ISDA 2016 CSA for Initial Margin.">
	[synonym FpML_5_10 value LegalDocumentType]
{
	name LegalDocumentNameEnum (1..1) <"The legal document name.">;
		[synonym FpML_5_10 value name]
		[synonym AcadiaSoft_AM_1_0 value documentName]
		[synonym ISDA_Create_1_0 value documentType]
	publisher LegalDocumentPublisherEnum (1..1) <"The legal document publisher.">;
		[synonym FpML_5_10, AcadiaSoft_AM_1_0, ISDA_Create_1_0 value publisher]
	governingLaw LegalDocumentStyleEnum (0..1) <"The legal document reference style, e.g. English Law, New York Law or Japanese Law.">;
		[synonym FpML_5_10 value style]
		[synonym AcadiaSoft_AM_1_0 value governingLaw]
		[synonym ISDA_Create_1_0 value governingLaw]
	vintage string (0..1) <"In the case where successive definitions of the legal document have been developed, the vintage identification. This is typically (but not necessarily) done by referencing the year, e.g. 2013 in the case of the ISDA 2013 Standard Credit Support Annex.">;
		[synonym FpML_5_10, FpML_5_10 value version]
		[synonym AcadiaSoft_AM_1_0 value csaVersion]
}

class Method <"A class to specify the ISDA SIMM as the Method for all Covered Transactions with respect to all Regimes. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee).">
{
	version SimmVersion (1..1) <"The qualification of the ISDA SIMM version that is specified for all Covered Transactions as specified by ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles, (ee)(1).">;
		// TODO - Need to clarify how the specification of the version of ISDA SIMMTM applicable to the relevant Secured Party is specified as part of ISDA Create
		// ISDA Create for the 2016 IM NY Law only has the election for the SIMM calculation currency 
	calculationCurrency SimmCalculationCurrency (1..2) <"The SIMM Calculation Currency, as specified for each of the parties to the CSA Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee)(3).">;
		[synonym ISDA_Create_1_0 value simm_calculation_currency]
}

class MinimumTransferAmount <"A class to specify amount of exposure reached before collateral has to be posted or returned.">
{
	partyElections PartyElectiveAmount (2..2) <"The parties' minimum transfer amount elections.">;
}

class MultipleDebtTypes <"A class to specify the ability to specify multiple credit debt types alongside a conditional 'any' or 'all' qualifier.">
{
	condition QuantifierEnum (1..1) <"An enumerated attribute, to qualify whether All or Any debt type applies.">;
		[synonym FpML_5_10 value condition]
	debtType string (2..*) scheme <"The type of debt, e.g. long term debt, deposit, ... FpML doesn't specific a scheme value, hence no enumeration is specified as part of the CDM. At least two debt types much be specified.">;
		[synonym FpML_5_10 value debtType meta debtTypeScheme]
}

class MultipleCreditNotations <"A class to specify the specify multiple credit notations alongside a conditional 'any' or 'all' qualifier.">
{
	condition QuantifierEnum (1..1) <"An enumerated element, to qualify whether All or Any credit notation applies.">;
		[synonym FpML_5_10 value condition]
	creditNotation string (2..*) scheme <"At least two credit notations much be specified.">;
		[synonym FpML_5_10 value debtType meta creditNotation]
}

class NotificationTime <"A class to specify the time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iii).">
{
	partyElections PartyNotificationTime (2..2) <"The parties' Notification Time elections.">;
}

class ObligorPostingObligations extends PostingObligations <"A class to specify the obligor(s) collateral posting obligations as specified under the terms of the Japanese Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	obligor Party (1..2) reference <"The obligor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
		[synonym ISDA_Create_1_0 value obligor]
	// See mapping comment in PledgorPostingObligations class
}

class OneWayProvisions <"A class to specify whether One Way Provisions apply in relation to the ISDA CSA for Initial Margin and, if yes, to specify the Posting Party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (aa): One Way Provisions.">
{
	isApplicable boolean (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false).">;
		[synonym ISDA_Create_1_0 set to True when "one_way_provisions" = "applicable",
			set to False when "one_way_provisions" = "not_applicable"]
	postingParty Party (0..1) reference <"The Posting Party for the purposes of One Way Provisions. This party is specified in the case where the One Way Provision is deemed applicable.">;
		[synonym ISDA_Create_1_0 value posting_party]
}

data rule OneWayProvisions_postingPartyReference_exists <"This Posting Party must be specified in the case where the One Way Provision is deemed applicable.">
	when OneWayProvisions -> isApplicable = True
	then OneWayProvisions -> postingParty exists

data rule OneWayProvisions_postingPartyReference_absent <"This Posting Party must not be specified in the case where the One Way Provision is not deemed applicable.">
	when OneWayProvisions -> isApplicable = False
	then OneWayProvisions -> postingParty is absent

// TODO - Need to associate a more meaningful definition to the below class and attribute.  I couldn't find anything as part of the CSA terms.
class OtherEligibleSupport <"A class to specify the Other Eligible Support elections associated with margin agreements. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM).">
{
	valuee boolean (1..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(i): Value.">;
		[synonym ISDA_Create_1_0 set to True when path = "support_value.is_applicable.applicable",
			set to False when path = "support_value.is_applicable.not_applicable"]
	transfer boolean (1..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(ii): Transfer.">;
		[synonym ISDA_Create_1_0 set to True when path = "support_transfer.is_applicable.applicable",
			set to False when path = "support_transfer.is_applicable.not_applicable"]
}

class PartyAdditionalRepresentation <"A class to specify the parties' Additional Representation(s) election.">
{
	party Party (1..1) reference <"The party which the Additional Representation() election applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	isApplicable boolean (1..1) <"The party's election to qualify whether .">;
		[synonym ISDA_Create_1_0 set to True when path = "partyA_additional_representations.applicable",
			set to False when path = "partyB_additional_representations.applicable",
			set to True when path = "partyA_additional_representations.not_applicable",
			set to False when path = "partyB_additional_representations.not_applicable"]
}

class PartyCalculationCurrency <"A class to specify the SIMM Calculation Currency elections by each party to the agreement.">
{
	partyElection SimmCalculationCurrency (2..2 ) <"The parties'SIMM Calculation Currency elections.">;
}

class PartyCustodianElection <"A class to specify the Custody Arrangements elections by each party to the agreement: custody agent and account(s) identification, as well as custodian risk qualification.">
{
	party Party (1..1) reference <"The pledgor party to which the custody agent election applies.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	custodian LegalEntity (1..1) <"The custody agent. While ISDA Create only specifies the custodian's name, specifying the legal entity as part of the CDM is deemed more appropriate, while this will still provide the ability to accommodate situations where only the entity name is available, as the entityId attribute is optional as part of the LegalEntity class.">;
	segregatedCashAccount Account (1..1) <"The identification of the segregated cash account for the purpose of holding cash collateral.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create suffix to identify cash vs. security with custodian_and_segregated_account_details as a path
	segregatedSecurityAccount Account (1..1) <"The identification of the segregated security account for the purpose of holding security collateral.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create suffix to identify cash vs. security with custodian_and_segregated_account_details as a path
	custodianRisk CustodianRisk (1..1) <"The qualification of the Custodian Risk. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Custodian (IM) Risk.">;
		[synonym ISDA_Create_1_0 value custodian_risk]
}

class PartyDocumentIdentifier <"A class defining a legal document identifier issued by the indicated party.">
	[synonym FpML_5_10 value PartyDocumentIdentifier]
{
	id string (0..1) id;
		[synonym FpML_5_10 value id]
	partyReference string (1..1) <"Reference to the party that issued the document identifier">;
		[synonym FpML_5_10 value partyReference]
	documentIdentifier Identifier (1..*) scheme <"While FpML specifies the document identifier with a value and an associated scheme, the CDM makes use of the Identifier, which has an explicit issuer. The issuer of this identifier is not necessarily the same as the party reference">;
		[synonym FpML_5_10 value documentId meta legalDocumentIdScheme]
}

class PartyElectiveAmount <"A class to specify the party elective amounts which can be used for the purpose of specifying elections such as the ISDA CSA Threshold and Minimum Transfer Amount.">
{
	party Party (1..1) reference <"The party which the elective amount applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	amount Money (0..1) <"The elective amount when expressed as a currency amount. The associated PartyElectiveAmount_amount enforces that the currency amount is actually greater than 0.">;
	customElection string (0..1) <"The elective amount when expressed as a custom election by the party.">;
		[synonym ISDA_Create_1_0 value partyA_specify]
	noAmount number (0..1) <"The elective amount when specifies as 0 with no associated currency. The associated PartyElectiveAmount_noAmount data rule enforces that the number is actually equal to 0.">;
		[synonym ISDA_Create_1_0 set to 0 when "partyA_minimum_transfer_amount" exists]
}

choice rule PartyElectiveAmount_choice <"The elective amount is specified either as a currency amount, a custom election or a 0 amount with no associated currency.">
	for PartyElectiveAmount required choice between
	amount and customElection and noAmount
	
data rule PartyElectiveAmount_noAmount <"The noAmount attribute specifies a 0 amount election, with no associated currency.">
	when PartyElectiveAmount -> noAmount exists
	then PartyElectiveAmount -> noAmount = 0	

data rule PartyElectiveAmount_amount <"The amount attribute specifies a currency amount greater than 0.">
	when PartyElectiveAmount -> amount exists
	then PartyElectiveAmount -> amount -> amount > 0	

class PartyNotificationTime <"A class to specify the party notification time elections.">
{
	party Party (1..1) reference <"The party which the notification time election applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	notificationTime BusinessCenterTime (0..1) <"The Notification Time as a time that is qualified by a standard business center.">;
	customNotification string (0..1) <"The Notification Time as a custom election.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

choice rule PartyNotificationTime_choice <"The Notification Time election can be done either as a time that is qualified by a standard business center, or as a custom election.">
	for PartyNotificationTime required choice between
	notificationTime and customNotification

class PartyRegimeElection <"A class to specify the parties' respective elections with respect to the applicable regulatory regime(s) in their capacity as Secured Party (English Law & New York Law) or Obligee (Japanese Law).">
{
	party Party (1..1) reference <"The party which the set of regulatory regime elections specified by the applicableRegime attribute apply to.">;
		[synonym AcadiaSoft_AM_1_0 value party]
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	applicableRegime ApplicableRegime (1..*) <"The set of regulatory regimes and associated provisions applicable to the respective parties to the legal agreement.">;
	additionalRegime AdditionalRegime (0..*) <"The additional regulatory regime(s) that might be specified by the parties to a legal agreement. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, General Principles, (ll): The parties may from time to time agree in writing that other regimes also comprise `Regimes` and that the General Principles be adopted and/or amended to accommodate such additional Regimes.">;
		[synonym ISDA_Create_1_0 value additional_regimes]
}

class PartyTerminationCurrency <"A class to specify the Amendment to Termination Currency election by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t) (A) & (B).">
{
	party Party (1..1) reference <"The party which the Termination Currency determination applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	currency string (1..1) scheme <"The Termination Currency associated with the party that referenced as part of this class. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value partyA_termination_currency, partyB_termination_currency]
}

class PledgorPostingObligations extends PostingObligations <"A class to specify the pledgor(s) collateral posting obligations as specified under the terms of the New York Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	pledgor Party (1..2) reference <"The pledgor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
		[synonym ISDA_Create_1_0 value pledgor]
		/*
		 * TODO - In the case where both parties are specified, ISDA Create specifies the value 'both'.
		 * 2 possible approaches wrt CDM: either develop a mapping logic that will create 2 entries (could be done in the back-end, rather than the syntax)
		 * or create a class abstraction that provides a choice between 1 party or a value of 'BothParties'.
		 * My inclination is for the former, which allows to have a consistent a party representation across the whole document.
		 */
}

class PledgorRightsEvent one of <"A class to specify the Pledgor Rights Event election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (j): Pledgor Rights Event."> // TODO - Look to enhance this definition, as it's not clear what it means in layman terms
{
	includeCoolingOffLanguage boolean (0..1);
		[synonym ISDA_Create_1_0 set to True when "include" exists]
	customElection string (0..1);
		[synonym ISDA_Create_1_0 value specify]
}

class PledgorAdditionalRightsEvent <"A class to specify the Pledgor Additional Rights Event election. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Pledgor Additional Rights Event."> // TODO - Look to enhance this definition, as it's not clear what it means in layman terms
{
	isApplicable boolean (1..1) <"The election of whether the Pledgor Additional Rights Event election is applicable.">;
		[synonym ISDA_Create_1_0 value applicable]
	qualification string (0..1) <"The qualification of the Pledgor Additional Rights Event election, when applicable.">;
		[synonym ISDA_Create_1_0 value specify]
}

abstract class PostingObligations <"A class to specify the collateral posting obligations as specified under the terms of the ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii). This class is then extended so that party to which the posting obligation applies to is qualified according to relevant governing law: Chargor under English Law, Obligor under Japanese Law and Pledgor under New York Law.">
{
	asPermitted boolean (1..1) <"If set to True, the Control Agreement is a Credit Support Document with respect to the party(ies). ISDA 2016 Credit Support Annex for Initial Margin, paragraph 6, (e).">;
		[synonym ISDA_Create_1_0 
			set to True when "partyA_type" = "control_agreement",
			set to False when "partyA_eligible_currencies" exists,
			set to True when "partyB_type" = "control_agreement",
			set to False when "partyB_eligible_currencies" exists]
	eligibleCollateral EligibleCollateral (0..*) <"The eligible collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation. ISDA 2016 Credit Support Annex for Initial Margin, Eligible Credit Support (IM) Schedule.">;
		[synonym ISDA_Create_1_0 value partyA_eligible_currencies]
		[synonym ISDA_Create_1_0 value partyB_eligible_currencies]
	excludedCollateral string (0..1) <"The excluded collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii)(B)(i).">; // This representation matches the current ISDA Create one, but should be aligned with the eligibleCollateral representation once that one is confirmed
		[synonym ISDA_Create_1_0 value partyA_control_agreement_specify, partyB_control_agreement_specify]
	additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">;
		[synonym ISDA_Create_1_0 value partyA_additional_language, partyB_additional_language]
}

data rule PostingObligations_asPermitted <"The eligible collateral should be specified when the Control Agreement is a Credit Support Document with respect to the party(ies).">
	when PostingObligations -> asPermitted = True
	then PostingObligations -> eligibleCollateral is absent

data rule PostingObligations_eligibleCollateral <"The eligible collateral shouldn't be specified when the Control Agreement isn't deemed a Credit Support Document with respect to the party(ies).">
	when PostingObligations -> asPermitted = False
	then PostingObligations -> eligibleCollateral exists

data rule PledgorAdditionalRightsEvent_applicable <"If the Pledgor Additional Rights Event election is applicable, its meaning needs to be specified.">
	when PledgorAdditionalRightsEvent -> isApplicable = True
	then PledgorAdditionalRightsEvent -> qualification exists

class Regime <"A class to specify the regulatory regimes elections by the respective parties a legal agreement. 2016 ISDA Credit Support Annex for Initial Margin: Regime.">
	[synonym AcadiaSoft_AM_1_0 value Regime]
{
	partyElection PartyRegimeElection (2..2 ) <"The parties' regulatory regime elections.">;
}

abstract class RegimeTerms <"An abstract class that is used by the ApplicableRegime and the AdditionalRegime classes to specify the regulatory regime terms which are referred to as part of certain legal agreements, such as such as the ISDA 2016 and 2018 CSA for Initial Margin.">
{
	isApplicable boolean (1..1) <"A boolean flag to specify whether the regulatory regime is applicable.">;
		[synonym ISDA_Create_1_0 
			set to True when "partyA_secured_party" = "applicable",
			set to False when "partyA_secured_party" = "not_applicable",
			set to True when "partyB_secured_party" = "applicable",
			set to False when "partyB_secured_party" = "not_applicable"]
	simmException SimmException (1..1) <"The SIMM exception to the regulatory regime clause of the ISDA 2016 and 2018 CSA for Initial Margin as either a normalized value specified as part of an enumeration or a customized value specified of type string. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, Regime: SIMM Exception.">;
	retrospectiveEffect boolean (1..1) <"ISDA 2016 CSA for Initial Margin, paragraph 13 (b)(i): if `Retrospective Effect` is specified as applicable to a Regime (a `Retrospective Regime`) then all Covered Transactions (IM) under all other Regimes with an earlier Regime Effective Time will, to the extent that they would have been Covered Transactions (IM) under such Retrospective Regime had such Transactions been entered into at or after the Regime Effective Time of the Retrospective Regime, be deemed to be Covered Transactions (IM) for such Retrospective Regime.">;
		[synonym AcadiaSoft_AM_1_0 value retrospectiveEffect]
		[synonym ISDA_Create_1_0 
			set to True when "partyA_retrospective" = "applicable",
			set to False when "partyA_retrospective" = "not_applicable",
			set to True when "partyB_retrospective" = "applicable",
			set to False when "partyB_retrospective" = "not_applicable"]
	additionalType AdditionalTypeEnum (1..1) <"The Additional Type of transaction that can require the collection or delivery of initial margin under the specified regulatory regime for the purposes of Covered Transactions, as specified in ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(B).">;
}

class SensitivityMethodology one of <"A class to specify the methodology according to which sensitivities to (i) equity indices, funds and ETFs, and (ii) commodity indices are computed. This specification is done either through a normalized election as part of the specifiedMethodology, or through a custom election via the customMethodology attribute. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (gg)(2).">
{
	specifiedMethodology SensitivitiesEnum (0..1) <"The methodology according to which sensitivities will be computed, when specified through a normalized election.">;
		[synonym ISDA_Create_1_0 value sensitivities_equity_indices_funds_etfs, sensitivities_commodity_indices]
	customMethodology string (0..1) <"The methodology according to which sensitivities will be computed, when specified through a custom election.">;
		[synonym ISDA_Create_1_0 value sensitivities_equity_indices_funds_etfs_specify, sensitivities_to_commodity_indices_specify]
}

class SimmCalculationCurrency <"A class to specify the ISDA SIMM Calculation Currency as either the Base Currency or an alternative currency. ISDA 2016 CSA for Initial Margin, Paragraph 13, General Principles (ee)(3). | ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles (ee)(3).">
{
	party Party (1..1) reference <"The party which the SIMM Calculation Currency qualification applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	isBaseCurrency boolean (1..1);
		[synonym ISDA_Create_1_0 value partyA_use_base_currency, partyB_use_base_currency]
	currency string (0..1) scheme <"The currency in which the ISDA SIMM Calculation is denominated. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value partyA_use_other_currency, partyB_use_other_currency]
		[synonym AcadiaSoft_AM_1_0 value currency]
}

data rule SimmCalculationCurrency_baseCurrency <"When the SIMM calculation currency is specified as the CSA base currency the currency attribute should not be specified.">
	when SimmCalculationCurrency -> isBaseCurrency = True
	then SimmCalculationCurrency -> currency is absent

//data rule SimmCalculationCurrency_otherCurrency <"When the SIMM calculation currency is specified as distinct from the CSA base currency the currency attribute should be specified and be different from the CSA Base Currency.">
//	when CsaInitialMargin2018 -> method -> calculationCurrency -> isBaseCurrency = False
//	then CsaInitialMargin2018 -> method -> calculationCurrency -> currency exists
//	and CsaInitialMargin2018 -> method -> calculationCurrency -> currency <> CsaInitialMargin2018 -> baseCurrency

class SimmException one of <"A class to specify the SIMM exception to the regulatory regime clause of the ISDA 2016 and 2018 CSA for Initial Margin as either a normalized value specified as part of an enumeration or a customized value specified of type string. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, Regime: SIMM Exception.">
{
	standardisedException SimmExceptionEnum (0..1) <"The Standard Initial Margin Model exception when specified by the party according to one of the enumerated values.">;
		[synonym AcadiaSoft_AM_1_0 set to "NotApplicable" when "simmException" = "False"]
		[synonym ISDA_Create_1_0 set to "NotApplicable" when "partyA_SIMM" = "not_applicable",
			set to "NotApplicable" when "partyB_SIMM" = "not_applicable",
			set to "Applicable" when "partyA_SIMM" = "applicable",
			set to "Applicable" when "partyB_SIMM" = "applicable"]
	asSpecified string (0..1) <"The Standard Initial Margin Model exception when specified as a customized approach by the party.">;
		[synonym ISDA_Create_1_0 value partyA_SIMM_applicable_specify, partyB_SIMM_applicable_specify]
}

class SimmVersion <"A class to specify the ISDA SIMM version that applies to the ISDA 2018 CSA for Initial Margin. According to the ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles (ee) (1) provisions, the SIMM version is either not specified, or references a version used by one of the parties to the agreement.">
{
	isSpecified boolean (1..1) <"A boolean attribute to determine whether the SIMM version is specified for the purpose of the legal agreement.">;
	version SpecifiedSimmVersion (0..1) <"When the SIMM version is specified, it will reference the party that uses such version.">;
}

data rule SimmVersion_version1 <"The version attribute should be absent when the SIMM version is stated as not specified for the CSA.">
	when SimmVersion -> isSpecified = False
	then SimmVersion -> version is absent

data rule SimmVersion_version2 <"The version attribute should be specified when the SIMM version is stated as specified for the CSA.">
	when SimmVersion -> isSpecified = True
	then SimmVersion -> version exists

class SpecifiedSimmVersion <"A class to specify the ISDA SIMM version applicable to one of the parties to the CSA agreement that will then be the relevant version for that CSA.">
{
	party Party (1..1) reference <"The party which the specified SIMM version applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	simmVersion string (1..1) <"The applicable ISDA SIMM version.">;
}

class Substitution <"A class to specify the conditions under which the Pledgor can substitute posted collateral. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Substitution.">
{
	needsConsent boolean (1..1) <"The election as to whether the Pledgor must obtain the Secured Party’s consent for any collateral substitution. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(ii): Consent.">;
		[synonym ISDA_Create_1_0 set to True when "is_applicable" exists,
			set to False when "is_not_applicable" exists] // This second mapping is a guess and needs to be confirmed through an example
	specificConsentLanguage string (0..1) <"Specific consent language might be specified by the parties.">;
		[synonym ISDA_Create_1_0 value specify]
}

class TerminationCurrencyAmendment <"A class to specify the Amendment to Termination Currency elections by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t): Amendment to Termination Currency.">
{
	isApplicable boolean (1..1) <"The qualification of whether the Amendment to Termination Currency is deemed applicable by the parties.">;
		[synonym ISDA_Create_1_0 set to True when path = "is_applicable.applicable",
			set to False when path = "annex_date_is_applicable.not_applicable"]
	effectiveDate AmendmentEffectiveDate (0..1) <"The effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision.">;
	currencyElection PartyTerminationCurrency (0..1) <"The parties' election with respect to the Termination Currency.">;
	bothPartiesCurrency string (0..1) scheme <"The currency election in the case of a Termination Event where there are two Affected Parties.">;
		[synonym ISDA_Create_1_0 value both_parties_termination_currency]
}

data rule TerminationCurrencyAmendment_applicable <"The actual Amendment to Termination Currency elections only apply when the Amendment to Termination Currency is deemed applicable by the parties.">
	when TerminationCurrencyAmendment -> isApplicable = False
	then TerminationCurrencyAmendment -> effectiveDate is absent
	and TerminationCurrencyAmendment -> currencyElection is absent
	and TerminationCurrencyAmendment -> bothPartiesCurrency is absent

class AmendmentEffectiveDate one of <"A class to specify the effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision. ">
{
	date date (0..1) <"The effective date of the Amendment to Termination Currency when specified as an actual date.">;
	specificDate AmendmentEffectiveDateEnum (0..1) <"The effective date of the Amendment to Termination Currency when specified as a specific date (e.g. the annex date).">;
	customProvision string (0..1)<"The effective date of the Amendment to Termination Currency when specified as a non normalized custom provision.">;
}

class TerminationEvents <"A class to specify the Termination Events that will be deemed an Access Condition. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii).">
{
	party Party (1..1) reference <"The party which the set of Access Condition elections apply to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	illegality boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_illegality" = "applicable",
			set to False when "partyA_illegality" = "not_applicable",
			set to True when "partyB_illegality" = "applicable",
			set to False when "partyB_illegality" = "not_applicable"]
	forceMajeure boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_force_majeure" = "applicable",
			set to False when "partyA_force_majeure" = "not_applicable",
			set to True when "partyB_force_majeure" = "applicable",
			set to False when "partyB_force_majeure" = "not_applicable"]
	taxEvent boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_tax_event" = "applicable",
			set to False when "partyA_tax_event" = "not_applicable",
			set to True when "partyB_tax_event" = "applicable",
			set to False when "partyB_tax_event" = "not_applicable"]
	taxEventUponMerger boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_tax_event_upon_merger" = "applicable",
			set to False when "partyA_tax_event_upon_merger" = "not_applicable",
			set to True when "partyB_tax_event_upon_merger" = "applicable",
			set to False when "partyB_tax_event_upon_merger" = "not_applicable"]
	creditEventUponMerger boolean (1..1);
		[synonym ISDA_Create_1_0
			set to True when "partyA_credit_event_upon_merger" = "applicable",
			set to False when "partyA_credit_event_upon_merger" = "not_applicable",
			set to True when "partyB_credit_event_upon_merger" = "applicable",
			set to False when "partyB_credit_event_upon_merger" = "not_applicable"]
	additionalTerminationEvent boolean (1..1);
		[synonym ISDA_Create_1_0
			set to True when "partyA_additional_termination_events" = "applicable",
			set to False when "partyA_additional_termination_events" = "not_applicable",
			set to True when "partyB_additional_termination_events" = "applicable",
			set to False when "partyB_additional_termination_events" = "not_applicable"]
	additionalLanguage string (0..1);
}

class Threshold <"A class to specify the unsecured credit exposure that each party to the agreement is prepared to accept before asking for collateral. This threshold is specified either as an amount or as a custom election.">
{
	partyElections PartyElectiveAmount (2..2) <"The parties' Threshold elections.">;
}
