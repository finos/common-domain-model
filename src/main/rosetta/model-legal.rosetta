namespace "org.isda.cdm"
version "%project.version%"

class AdditionalRegime extends RegimeTerms <"A class to specify the additional regulatory regime(s) that might be specified by the parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin.">
{
	regime string (1..1) <"The additional regulatory regime as specified by the parties.">;
		[synonym ISDA_Create_1_0 value regime_name]
}

class AdditionalRepresentation <"A class to specify the Additional Representation. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (o): Additional Representation(s).">
{
	/*
	 *  TODO - The ISDA Create template for 2016 CSA #44 provide for each part to specify an applicable boolean election alongside with a common specification in the form of a string field.  Having compared two documents which  have distinct elections with respect to the former attribute, I don’t see such election reflected in the pdf resulting document.
	 */
	partyElection AdditionalRepresentationElection (2..2) <"A qualification as to whether the Additional Representation is applicable.">;
	customElection string (0..1) <"A supplemental custom election that might be specified by the parties for the purpose of specifying the Additional Representation.">;
		[synonym ISDA_Create_1_0 value specify]
}

class AdditionalRepresentationElection <"A class to specify the parties' Additional Representation(s) election.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	isApplicable boolean (1..1) <"The Additional Representation is applicable when True, and not applicable when False.">;
		[synonym ISDA_Create_1_0 set to True when path = "partyA_additional_representations.applicable",
			set to False when path = "partyB_additional_representations.applicable",
			set to True when path = "partyA_additional_representations.not_applicable",
			set to False when path = "partyB_additional_representations.not_applicable"]
}

class AdditionalRightsEvent <"A class to specify the Pledgor/Obligor/Chargor Additional Rights Event election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (k): Chargor Additional Rights Event. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Obligor Additional Rights Event. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Pledgor Additional Rights Event.">
{
	isApplicable boolean (1..1) <"The election of whether the Pledgor Additional Rights Event election is applicable.">;
		[synonym ISDA_Create_1_0 value applicable]
	qualification string (0..1) <"The qualification of the Pledgor Additional Rights Event election, when applicable.">;
		[synonym ISDA_Create_1_0 value specify]
}

data rule AdditionalRightsEvent_applicable <"If the Pledgor/Obligor/ChargorAdditional Rights Event election is applicable, its meaning needs to be specified.">
	when AdditionalRightsEvent -> isApplicable = True
	then AdditionalRightsEvent -> qualification exists

class AdditionalType <"The specification of the Additional Type of transaction that can require the collection or delivery of initial margin under a given regulatory regime for the purposes of Covered Transactions, as specified in ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(B).">
{
	standardValue AdditionalTypeEnum (1..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a standard value.">;
		[synonym ISDA_Create_1_0 value additional_type]
	customValue string (0..1) <"The qualification of the Additional Type of transaction that can require the collection or delivery of initial margin when specified as a custom value by the parties to the legal agreement.">;
		[synonym ISDA_Create_1_0 value additional_type_specify]
}

data rule AdditionalType_customValue <"The specification of a custom value by the parties to the legal agreement takes place alongside the qualification of the `Other` value as part of the AdditionalTypeEnum.">
	when AdditionalType -> standardValue = AdditionalTypeEnum.Other
	then AdditionalType -> customValue exists

data rule AdditionalType_standardValue <"The specification of a standard value by the parties to the legal agreement is done through the qualification of a value distinct than `Other` as part of the AdditionalTypeEnum, and implies that the customerValue is not being qualified.">
	when AdditionalType -> standardValue <> AdditionalTypeEnum.Other
	then AdditionalType -> customValue is absent	

class AmendmentEffectiveDate one of <"A class to specify the effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision. ">
{
	date date (0..1) <"The effective date of the Amendment to Termination Currency when specified as an actual date.">;
	specificDate AmendmentEffectiveDateEnum (0..1) <"The effective date of the Amendment to Termination Currency when specified as a specific date (e.g. the annex date).">;
	customProvision string (0..1)<"The effective date of the Amendment to Termination Currency when specified as a non normalized custom provision.">;
}

class ApplicableRegime extends RegimeTerms <"A class to specify the applicable regulatory regime(s) that parties to a legal agreement, such as the ISDA 2016 and 2018 CSA for Initial Margin, might be subject to.">
{
	regime RegulatoryRegimeEnum (1..1) <"The applicable regulatory regime, as specified through an enumeration.">;
		[synonym AcadiaSoft_AM_1_0 value regime]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.AustraliaMarginRules when "australia" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.CanadaMarginRules when "canada" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.CFTC_MarginRules when "cftc" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.EMIR_MarginRules when "emir" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.HongKongMarginRules when "hongkong" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.JapanMarginRules when "japan" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SEC_MarginRules when "sec" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SingaporeMarginRules when "singapore" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.SwitzerlandMarginRules when "switzerland" exists]
		[synonym ISDA_Create_1_0 set to RegulatoryRegimeEnum.US_PrudentialMarginRules when "prudential" exists]
}

class CalculationCurrencyElection <"A class to specify the SIMM Calculation Currency elections by each party to the agreement.">
{
	partyElection SimmCalculationCurrency (2..2 ) <"The parties' SIMM Calculation Currency election.">;
}

class CalculationDateLocation <"A class to specify the Calculation Date Location election for the respective parties to the legal agreement. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(i): Calculation Date Location. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location.">
{
	partyElection CalculationDateLocationElection (2..2) <"The parties' calculation date location election.">;
}

class CalculationDateLocationElection <"A class to specify each of the party elections with respect to the Calculation Date Location. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(i): Calculation Date Location. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	businessCenter BusinessCenterEnum (0..1) scheme <"The Calculation Date Location when specified as a business center which corresponds to the FpML list of business centers or can be mapped to it.">;
		[synonym ISDA_Create_1_0 value partyA_location, partyB_location]
	customLocation string (0..1) <"The Calculation Date Location when specified a location which doesn't correspond to the FpML list of business centers or cannot be mapped to it.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

choice rule CalculationDateLocationElection_choice <"The Calculation Date Location is specified either as a standard business center or as a custom location.">
	for CalculationDateLocationElection required choice between
	businessCenter and customLocation

class ChargorPostingObligations <"A class to specify the chargor(s) collateral posting obligations as specified under the terms of the English Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	partyElection PostingObligationsElection (1..2) reference <"The chargor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
}

class CollateralManagementAgreement <"A class to specify the Collateral Management Agreement election by the respective parties to a Japanese Law ISDA CSA. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(i): Collateral Management Agreement.">
{
	partyElection CollateralManagementAgreementElection (2..2 ) <"The parties' Collateral Management Agreement election.">;
}

class CollateralManagementAgreementElection <"A class to specify the Collateral Management Agreement election by each party as the Obligee. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b(i): Collateral Management Agreement.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	collateralManagementAgreement string (1..1) <"The designated Collateral Management Agreement with respect to the elective party as the Obligee.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

class CollateralManagementArrangement <"A class to specify the Collateral Management Arrangement elections in accordance with the ISDA Japanese Law CSA. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m): Collateral Management Arrangement.">
{
	partyElection CollateralManagementArrangementElection (2..2 ) <"The party-specific election with respect to the Custodian and Segregated Account details and the Custodian Risk provision.">;
	collateralManagerEvent CustodianEvent (1..1) <"The Collateral Management Event applicability and end-date terms. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(ii): Collateral Manager Event.">;
	isCreditSupportDocument boolean (1..1) <"Unless specified as True, the Collateral Management Agreement is not a Credit Support Document under the agreement with respect to a party. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 6 (e): The Collateral Management Agreement as a Credit Support Document.">;
		[synonym ISDA_Create_1_0 set to True when path = "collateral_management_agreement_as_a_credit_support_document.definition.creditSupportDocument",
			set to False when path = "collateral_management_agreement_as_a_credit_support_document.definition.not_specified"]
	hasCollateralManagementLanguage boolean (1..1) <"Collateral Management Agreement language is specified when True, not specified when False.">;
		[synonym ISDA_Create_1_0 set to True when path = "relationship_with_the_collateral_management_agreement.include_language.include",
			set to False when path = "relationship_with_the_collateral_management_agreement.include_language.exclude"] 
}

class CollateralManagementArrangementElection <"A class to specify the election to specify the Collateral Management Arrangement.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	collateralManagerRisk CustodianRisk (1..1) <"The qualification of the Collateral Manager Risk. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(i): Collateral Manager Risk.">;
		[synonym ISDA_Create_1_0 value collateral_manager_risk]
}

class AppropriatedCollateralValuation <"A class to specify the Valuation of Appropriate Collateral that is applicable to the English Law ISDA CSA. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (u): Valuation of Appropriated Collateral.">
{
	isSpecified boolean (1..1) <"The qualification of whether the Valuation of Appropriate Collateral provision is applicable (True) or not applicable (False).">;
	election string (0..1) <"The parties' election that qualify the Valuation of Appropriate Collateral in the case where it is deemed applicable.">;
}

data rule AppropriatedCollateralValuation_specified <"The Valuation of Appropriate Collateral election must be specified when it is applicable.">
	when AppropriatedCollateralValuation -> isSpecified = True
	then AppropriatedCollateralValuation -> election exists

data rule AppropriatedCollateralValuation_notSpecified <"The Valuation of Appropriate Collateral election cannot be specified when it is not applicable.">
	when AppropriatedCollateralValuation -> isSpecified = False
	then AppropriatedCollateralValuation -> election is absent

class CollateralManager <"A class to specify the Collateral Manager election by the respective parties to a Japanese Law ISDA CSA. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(ii): Collateral Manager.">
{
	partyElection CollateralManagerElection (2..2 ) <"The parties' Collateral Manager election.">;
}

class CollateralManagerElection <"A class to specify the Collateral Manager election by each party as the Obligee. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b(ii): Collateral Manager.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	collateralManager string (1..1) <"The designated Collateral Manager with respect to the elective party as the Obligee.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

class CollateralRounding <"A class to specify the rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(C): Rounding. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding.">
{
	deliveryAmount number (1..1) <"The rounding methodology applicable to the Delivery Amount in terms of nearest integral multiple of Base Currency units.">;
		[synonym ISDA_Create_1_0 value delivery_amount]
	returnAmount number (1..1) <"The rounding methodology applicable to the Return Amount in terms of nearest integral multiple of Base Currency units.">;
		[synonym ISDA_Create_1_0 value return_amount]
}

class Conditions <"A class to specify each party's election with respect to the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(ii). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (e): Conditions Precedent and Secured Party’s Rights and Remedies.">
{
	partyElection ConditionsElections (2..2) <"The parties' Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA) election.">;
}

class ConditionsElections <"A class to specify the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(ii). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (e): Conditions Precedent and Secured Party’s Rights and Remedies.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	illegality boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_illegality" = "applicable",
			set to False when "partyA_illegality" = "not_applicable",
			set to True when "partyB_illegality" = "applicable",
			set to False when "partyB_illegality" = "not_applicable"]
	forceMajeure boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_force_majeure" = "applicable",
			set to False when "partyA_force_majeure" = "not_applicable",
			set to True when "partyB_force_majeure" = "applicable",
			set to False when "partyB_force_majeure" = "not_applicable"]
	taxEvent boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_tax_event" = "applicable",
			set to False when "partyA_tax_event" = "not_applicable",
			set to True when "partyB_tax_event" = "applicable",
			set to False when "partyB_tax_event" = "not_applicable"]
	taxEventUponMerger boolean (1..1);
		[synonym ISDA_Create_1_0 
			set to True when "partyA_tax_event_upon_merger" = "applicable",
			set to False when "partyA_tax_event_upon_merger" = "not_applicable",
			set to True when "partyB_tax_event_upon_merger" = "applicable",
			set to False when "partyB_tax_event_upon_merger" = "not_applicable"]
	creditEventUponMerger boolean (1..1);
		[synonym ISDA_Create_1_0
			set to True when "partyA_credit_event_upon_merger" = "applicable",
			set to False when "partyA_credit_event_upon_merger" = "not_applicable",
			set to True when "partyB_credit_event_upon_merger" = "applicable",
			set to False when "partyB_credit_event_upon_merger" = "not_applicable"]
	additionalTerminationEvent boolean (1..1);
		[synonym ISDA_Create_1_0
			set to True when "partyA_additional_termination_events" = "applicable",
			set to False when "partyA_additional_termination_events" = "not_applicable",
			set to True when "partyB_additional_termination_events" = "applicable",
			set to False when "partyB_additional_termination_events" = "not_applicable"]
	additionalLanguage string (0..1);
		[synonym ISDA_Create_1_0 value specify]
}

class ConditionsPrecedent <"A class to specify the two set of elections that may overwrite the default Condition Precedent provision as specified in Paragraph 4, (a) of the ISDA 2016 Credit Support Annex for Initial Margin and the ISDA 2016 Credit Support Annex for Variation Margin. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e): Conditions Precedent. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Conditions Precedent. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e): Conditions Precedent. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (e): Conditions Precedent and Secured Party’s Rights and Remedies.">
{
	customProvisions string (0..1) <"The custom provisions that might be specified by the parties to the agreement for the purpose of overwriting the default Condition Precedent provision as specified in ISDA 2016 Credit Support Annex for Initial Margin and the ISDA 2016 Credit Support Annex for Variation Margin, Paragraph 4, (a). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e)(i). | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(i). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(i). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, : Conditions Precedent and Secured Party’s Rights and Remedies.">;
		[synonym ISDA_Create_1_0 value conditions_precedent path "conditions_precedent"]
	conditions Conditions (1..1) <"The parties' election with respect to the Termination Events that will be deemed an Access Condition (Initial Margin CSA) or a Specified Condition (Variation Margin CSA). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(ii). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(ii). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (e): Conditions Precedent and Secured Party’s Rights and Remedies.">;
		[synonym ISDA_Create_1_0 value access_conditions]
}

class ContactElection <"A class to specify the parties' election to specify contact information, in relation to elections such as the Addresses for Transfer or the Demand and Notices as specified in the ISDA Credit Support Annex agreement.">
{
	partyElection PartyContactInformation (2..2 ) <"The parties' contact information election.">;
}

class CreditNotation <"A class to specify the credit notation as the combination of agency, notation, scale and debt type qualifications.">
	[synonym FpML_5_10 value CreditNotation]
{
	agency CreditRatingAgencyEnum (1..1) scheme <"The credit agency to which the other variables (notation, scale, debt type) refer to.">;
		[synonym FpML_5_10 value agency meta creditRatingAgencyScheme]
	notation string (1..1) scheme <"The credit rating notation. As it varies among credit rating agencies, FpML doesn't specify a default scheme.">;
		[synonym FpML_5_10 value notation meta creditRatingNotationScheme]
	scale string (0..1) scheme <"The credit rating scale, with a typical distinction between short term, long term. FpML doesn't specify a default scheme, which is hence not specified as an enumeration as part of the CDM.">;
		[synonym FpML_5_10 value scale meta creditRatingScaleScheme]
	debt CreditRatingDebt (0..1) <"The credit rating debt type (e.g. long term, high yield, deposits, ...) associated with the credit rating notation and scale.">;
		[synonym FpML_5_10 value debt]
}

class CreditNotations one of <"The credit rating notation higher level construct, which provides the ability to specify multiple rating notations.">
	[synonym FpML_5_10 value CreditNotations]
{
	creditNotation CreditNotation (1..1) <"This attribute is specified when only one credit notation is determined.">;
		[synonym FpML_5_10 value creditNotation]
	creditNotations MultipleCreditNotations (2..*) <"This attribute provides the ability to specify several credit notations, alongside an 'any' or 'all' or all condition.">;
}

class CreditRatingDebt one of <"The credit rating debt type(s) associated with the credit rating notation and scale. When several debt types are specified, they must be qualified through an 'any' or 'all'.">
	[synonym FpML_5_10 value CreditRatingDebt]
{
	debtType string (0..1) scheme <"This attribute is to be specified when only one debt type is specified. FpML doesn't specify values in relation to the associated scheme, which is hence not specified as an enumeration as part of the CDM.">;
		[synonym FpML_5_10 value debtType meta debtTypeScheme]
	debtTypes MultipleDebtTypes (0..1) <"This attribute provides the ability to specify several debt types, alongside an 'any' or 'all' or all condition. As an example, Baa1 rating is required for any long term debt and deposit.">;
}

class CreditSupportObligationsInitialMargin <"A class to specify the Credit Support Obligations applicable to the Initial Margin Credit Support Annex and which are common among the English, Japanese and New York governing laws. This excludes the Other Eligible Support election (which only applies to the Japanese Law and New York Law agreements) and the Transfer Timing election (which only applies to the English Law and the New York Law agreements). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d): Credit Support Obligations. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations.">
{
	threshold Threshold (1..1) <"The amount of net exposure that a party is willing to bear in relation to the other party before it requires asking for collateral. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(A): Threshold. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(A): Threshold. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(A): Threshold.">;
		[synonym ISDA_Create_1_0 value threshold]
	minimumTransferAmount MinimumTransferAmount (1..1) <"The net amount of exposure reached before collateral has to be posted or returned. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(B): Minimum Transfer Amount. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(B): Minimum Transfer Amount. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(B): Minimum Transfer Amount.">;
		[synonym ISDA_Create_1_0 value minimum_transfer_amount]
	rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(C): Rounding. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(C): Rounding.">;
		[synonym ISDA_Create_1_0 value rounding]
}

class CreditSupportObligationsVariationMargin <"A class to specify the Credit Support Obligations applicable to the Variation Margin Credit Support Annex and which are common among the English, Japanese and New York governing laws. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c): Credit Support Obligations.">
{
	eligibleCollateral EligibleCollateralVariationMargin (1..1) <"The eligible collateral elections by the parties to the Credit Support Annex for Variation Margin. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(ii): Eligible Collateral (VM).">;
	ineligibleCreditSupport IneligibleCreditSupport (1..1) <"The parties to which the provisions of Paragraph 11(g) of the ISDA 2016 Credit Support Annex for Variation Margin will apply to. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(iii): Legally Ineligible Credit Support (VM).">;
	majorCurrency string (1..*) scheme <"The set of currencies that are specified as Major Currency for the purpose of applying the FX Haircut Percentage. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(v)(B): FX Haircut Percentage.">;
	// In relation to the above, are the parties expected to always specify the major currencies, or should we have default values specified in the template, deemed to apply if no election by the parties? The current model assumes the former.
	fxHaircut number (1..1) <"The FX haircut percentage that applies to each party (as the pledgor/chargor/obligor) and item of Eligible Collateral unless this item is denominated in a Major Currency or in the Base Currency. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(v)(B): FX Haircut Percentage.">;
	// Same question as above: does the template specifies 8% as a default value?
	minimumTransferAmount MinimumTransferAmount (1..1) <"The net amount of exposure reached before collateral has to be posted or returned. ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (c)(vii)(A): Minimum Transfer Amount.">;
	rounding CollateralRounding (1..1) <"The rounding methodology applicable to the Delivery Amount and the Return Amount in terms of nearest integral multiple of Base Currency units. ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (c)(vii)(B): Rounding.">;
}

abstract class Csa2016 <"An abstract class to specify the provisions for the 2016 ISDA Credit Support Annex that are common among the corresponding governing laws and across Initial and Variation Margin.">
{
	baseCurrency string (1..1) scheme <"The base currency for the Credit Support Annex document as elected by the parties to the agreement. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes. ISDA 2016 CSA for Initial Margin, Paragraph 13 (a) | ISDA 2016 CSA for Variation Margin, Paragraph 13 (a).">;
		[synonym ISDA_Create_1_0 value base_currency]
	additionalObligations string (0..1) <"The additional obligations that might be specified by the parties to an ISDA CSA. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (b)(ii). | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(ii). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(ii). | ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (b)(ii).">;
		[synonym ISDA_Create_1_0 set to "additional_obligations.additional_obligations_specify" when path = "additional_obligations.additional_obligations.specify"]
	conditionsPrecedent ConditionsPrecedent (1..1) <"The set of elections specified in Paragraph 13 that may overwrite the default Condition Precedent provision as specified in ISDA 2016 Credit Support Annex for Initial Margin, Paragraph 4, (a), and the set of provisions that are deemed Access Condition (Initial Margin) and Specified Condition (Variation Margin). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (e): Conditions Precedent. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Conditions Precedent. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e): Conditions Precedent. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (e): Conditions Precedent and Secured Party’s Rights and Remedies.">;
	substitution Substitution (1..1) <"The conditions under which the Pledgor/Obligor/Chargor can substitute posted collateral. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (f): Substitution. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g): Substitution. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Substitution. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (f): Substitution.">;
	disputeResolution DisputeResolution (1..1) <"The election terms under which a party disputes (i) the Calculation Agent’s calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (h): Dispute Resolution. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (g): Dispute Resolution.">;
	additionalRepresentation AdditionalRepresentation (1..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (o): Additional Representation(s). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (k): Additional Representation(s).">;
	demandsAndNotices ContactElection (1..1) <"The address where the demands, specifications and notices will be communicated to within each of the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (q): Demands and Notices. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (m): Demands and Notices.">;
		[synonym ISDA_Create_1_0 value demands_and_notices]
	addressesForTransfer ContactElection (1..1) <"The address for transfer as specified by the respective parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (r): Addresses for Transfers. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (n): Addresses for Transfers.">;
		[synonym ISDA_Create_1_0 value addresses_for_transfers]
	bespokeProvision string (0..1) <"The bespoke provision that might be specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (u)(iii): Other Provisions. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (p): Other Provisions.">;
		[synonym ISDA_Create_1_0 value specify path "bespoke_provisions"]
}

abstract class CsaInitialMargin2016 extends Csa2016 <"An abstract class to specify the provisions for the 2016 ISDA Credit Support Annex for Initial Margin that are common among the applicable governing laws.">
{
	regime Regime (1..1) <"The Regime Table provision as specified by the ISDA 2016 CSA for Initial Margin, Paragraph 13 General Principles, which determines the regulatory regime(s) applicable to each of the parties to the CSA in their capacity as Secured Party (English Law & New York Law) or Obligee (Japanese Law), with one set of values per counterparty. As specified in the CSA, the applicability of a regime shall not be construed as a representation, admission or acknowledgement that either party is actually regulated under such regime.">;
	oneWayProvisions OneWayProvisions (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false) as specified by ISDA 2016 CSA for Initial Margin, Paragraph 13, (aa).">;
	method Method (1..1) <"The specification of the ISDA SIMM Method for all Covered Transactions with respect to all Regimes. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee).">;
	identifiedCrossCurrencySwap boolean (1..1) <"The qualification of whether cross-currency swaps need to be identified in the Confirmation so that the obligations to exchange principal be disregarded for the purpose of determining the Delivery Amount or Return Amount. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (gg)(1).">;
		[synonym ISDA_Create_1_0 set to True when path = "cross_currency_swap.yey_ney.true",
			set to False when path = "cross_currency_swap.yey_ney.false"]
	sensitivityToEquity SensitivityMethodology (1..1) <"The methodology to compute sensitivities to equity indices, funds and ETFs for the purpose of the ISDA 2016 and 2018 CSA for Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (gg)(2)(A).">;
		[synonym ISDA_Create_1_0 value sensitivities_to_equity_indices_funds_etfs]
	sensitivityToCommodity SensitivityMethodology (1..1) <"The methodology to compute sensitivities to commodity indices for the purpose of the ISDA 2016 and 2018 CSA for Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (gg)(2)(B).">;
		[synonym ISDA_Create_1_0 value sensitivities_to_commodity_indices]
	fxHaircutCurrency FxHaircutCurrency (1..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">;
		[synonym ISDA_Create_1_0 value fx_haircut_percentage]
	creditSupportObligations CreditSupportObligationsInitialMargin (1..1) <"The Credit Support Obligations applicable to the Initial Margin Credit Support Annex and which are common among the English, Japanese and New York governing laws. This excludes the Other Eligible Support election (which only applies to the Japanese Law and New York Law agreements) and the Transfer Timing election (which only applies to the English Law and the New York Law agreements). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c): Credit Support Obligations. ">;
	calculationDateLocation CalculationDateLocation (1..1) <"The specified location where the credit exposure will be calculated by the respective parties. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(i): Calculation Date Location. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(i): Calculation Date Location.">;
		[synonym ISDA_Create_1_0 value calculation_date_location]
	notificationTime NotificationTime (1..1) <"The time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iii).">;
		[synonym ISDA_Create_1_0 value notification_time]
	terminationCurrency TerminationCurrencyAmendment (1..1) <"The Amendment to Termination Currency elections by the parties to the agreement. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (v): Amendment to Termination Currency. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t): Amendment to Termination Currency. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t): Amendment to Termination Currency.">;
		[synonym ISDA_Create_1_0 value amendment_to_termination_currency]
}

class CsaInitialMargin2016JapaneseLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the Japanese Law version of the ISDA 2016 Credit Support Annex for Initial Margin.">
{
	obligorPostingObligations ObligorPostingObligations (1..1) <"The obligor's posting obligations. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value obligor_posting_obligations]
	collateralManagementAgreeement CollateralManagementAgreement (1..1) <"ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(i): Collateral Management Agreement.">;
		[synonym ISDA_Create_1_0 value collateral_management_agreement]
	collateralManager CollateralManager (1..1) <"ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(ii): Collateral Manager.">;
		[synonym ISDA_Create_1_0 value collateral_manager]
	otherEligibleSupport string (0..1) <"The Other Eligible Support election. If not specified, this election is deemed as not applicable. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iv).">;
		[synonym ISDA_Create_1_0 set to "other_eligible_support.eligible_support_specify" when "other_eligible_support.other_eligible_support.specify" exists]
	cashSettlementDay string (0..1) <"Cash Settlement Day has the meaning specified in ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, Paragraph 4(b)(i), unless otherwise specified as part of this attribute. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(iv): Cash Settlement Day.">;
		[synonym ISDA_Create_1_0 value specify path "cash_settlement_day"]
	securitiesSettlementDay string (0..1) <"Securities Settlement Day has the meaning specified in ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, Paragraph 12, unless otherwise specified as part of this attribute. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(v): Securities Settlement Day.">;
		[synonym ISDA_Create_1_0 value specify path "securities_settlement_day"]
	obligorRightsEvent RightsEvent (1..1) <"The Obligor Rights Event election. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Obligor Rights Event.">;
		[synonym ISDA_Create_1_0 value obligor_rights_event]
	obligorAdditionalRightsEvent AdditionalRightsEvent (0..1) <"The Obligor Additional Rights Event election. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Obligor Additional Rights Event.">;
		[synonym ISDA_Create_1_0 value obligor_additional_rights_event]
	automaticSetOff boolean (1..1) <"The Automatic Set-Off provision applies when the value is set to True. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (l): Modification to Obligor’s Rights and Remedies.">;
		[synonym ISDA_Create_1_0 set to True when path = "automatic_setoff.right.applicable",
			set to False when path = "automatic_setoff.right.not_applicable"]
	collateralManagementArrangement CollateralManagementArrangement (1..1) <"ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m): Collateral Management Arrangement.">;
	distributionAndInterestPayment DistributionAndInterestPayment (1..1) <"The Distributions and Interest Payment as part of the Japanese Law ISDA CSA. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Distributions and Interest Payment (IM).">;
	otherEligibleAndPostedSupport OtherEligibleAndPostedSupport (1..1) <"The Other Eligible Support elections associated with margin agreements. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM).">;
	otherCsa RelatedAgreement (1..1) <"The qualification of whether Other CSA are specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (s)(i): Other CSA.">;
		[synonym ISDA_Create_1_0 value other_csa]
	japaneseLawCsa RelatedAgreement (1..1) <"The qualification of whether Japanese Law CSA (VM) are specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (s)(ii): Japanese Law CSA (VM).">;
		[synonym ISDA_Create_1_0 value japanese_law_csa_vm]
	trustSchemeAddendum boolean (1..1) <"The qualification of whether Trust Scheme Addendum is applicable (True) or not applicable (False).">;
		[synonym ISDA_Create_1_0 set to True when path = "trust_scheme_addendum.is_applicable.applicable",
			set to False when path = "trust_scheme_addendum.is_applicable.not_applicable"]
}

class CsaInitialMargin2016NewYorkLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the New York Law version of the ISDA 2016 Credit Support Annex for Initial Margin.">
{
	pledgorPostingObligations PledgorPostingObligations (1..1) <"The pledgor's posting obligations. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value pledgor_posting_obligations]
	otherEligibleSupport string (0..1) <"The Other Eligible Support election. If not specified, this election is deemed as not applicable. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(iv).">;
		[synonym ISDA_Create_1_0 set to "other_eligible_support.eligible_support_specify" when "other_eligible_support.other_eligible_support.specify" exists]
	transferTiming string (0..1) <"The time by which the transfer of collateral must take place when different from the Regular Settlement Day as a result of parties' election. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vii): Transfer Timing.">;
		[synonym ISDA_Create_1_0 value settlement_day_specify path "transfer_timing"]
	pledgorRightsEvent RightsEvent (1..1) <"The Pledgor Rights Event election. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (j): Pledgor Rights Event.">;
		[synonym ISDA_Create_1_0 value pledgor_rights_event]
	pledgorAdditionalRightsEvent AdditionalRightsEvent (0..1) <"The Pledgor Additional Rights Event election. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (k): Pledgor Additional Rights Event.">;
		[synonym ISDA_Create_1_0 value pledgor_additional_rights_event]
	deliveryInLieuRight boolean (1..1) <"The Delivery in Lieu Right provision applies when the value is set to True. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m): Modification to Pledgor’s Rights and Remedies.">;
		[synonym ISDA_Create_1_0 set to True when path = "delivery_in_lieu_right.right.applicable",
			set to False when path = "delivery_in_lieu_right.right.not_applicable"]
	custodyArrangements CustodyArrangements (1..1) <"The Custodian and Segregated Account details in respect of each party as the Pledgor. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Custody Arrangements.">;
	otherEligibleAndPostedSupport OtherEligibleAndPostedSupport (1..1) <"The Other Eligible Support elections associated with margin agreements. ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM).">;
	japaneseSecuritiesProvisions boolean (1..1) <"ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (v): Japanese Securities Provisions (Shichiken).">;
		[synonym ISDA_Create_1_0 set to True when path = "japanese_securities_provisions.is_applicable.applicable",
			set to False when path = "japanese_securities_provisions.is_applicable.not_applicable"]
}

class CsdInitialMargin2016EnglishLaw extends CsaInitialMargin2016 <"A class to specify the provisions that are specific to the English Law version of the ISDA 2016 Credit Support Deed for Initial Margin.">
{
	chargorPostingObligations ChargorPostingObligations (1..1) <"The chargor's posting obligations. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (ii).">;
		[synonym ISDA_Create_1_0 value chargor_posting_obligations]
	transferTiming string (0..1) <"The time by which the transfer of collateral must take place when different from the Regular Settlement Day as a result of parties' election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vii): Transfer Timing.">;
		[synonym ISDA_Create_1_0 value settlement_day_specify path "transfer_timing"]
	chargorRightsEvent RightsEvent (1..1) <"The Chargor Rights Event election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (j): Chargor Rights Event.">;
		[synonym ISDA_Create_1_0 value chargor_rights_event]
	chargorAdditionalRightsEvent AdditionalRightsEvent (0..1) <"The Chargor Additional Rights Event election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (k): Chargor Additional Rights Event.">;
		[synonym ISDA_Create_1_0 value chargor_additional_rights_event]
	deliveryInLieuRight boolean (1..1) <"The Delivery in Lieu Right provision applies when the value is set to True. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (m): Modification to Chargor’s Rights and Remedies.">;
		[synonym ISDA_Create_1_0 set to True when path = "delivery_in_lieu_right.right.applicable",
			set to False when path = "delivery_in_lieu_right.right.not_applicable"]
	custodyArrangements CustodyArrangements (1..1) <"The Custodian and Segregated Account details in respect of each party as the Pledgor. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (n): Custody Arrangements.">;
	processAgent ProcessAgent (1..1) <"The Process Agent that might be appointed by the parties to the agreement in accordance with the ISDA 2016 English Law CSA, paragraph 11(h). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (t): Process Agent.">; 
		[synonym ISDA_Create_1_0 value process_agent]
	appropriatedCollateralValuation AppropriatedCollateralValuation (1..1) <"The election for the Valuation of Appropriate Collateral. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (u): Valuation of Appropriated Collateral.">; 
	japaneseSecuritiesProvisions boolean (1..1) <"ISDA 2016 English Credit Support Annex for Initial Margin, paragraph 13, General Principles, (x): Japanese Securities Provisions (Shichiken).">;
		[synonym ISDA_Create_1_0 set to True when path = "japanese_securities_provisions.is_applicable.applicable",
			set to False when path = "japanese_securities_provisions.is_applicable.not_applicable"]
}

abstract class CsaVariationMargin2016 extends Csa2016 <"An abstract class to specify the provisions for the 2016 ISDA Credit Support Annex for Variation Margin that are common among the applicable governing laws.">
{
	creditSupportObligations CreditSupportObligationsVariationMargin (1..1) <"The Credit Support Obligations applicable to the Variation Margin Credit Support Annex. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c): Credit Support Obligations.">;
	valuationAgent Party (1..1) reference <"The Valuation Agent as specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (d)(i): Valuation Agent.">;
	valuationDateLocation CalculationDateLocation (1..1) <"The business location elected by the respective parties to the agreement as the Valuation Date Location. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (d)(ii): Valuation Date Location.">;
	valuationTime BusinessCenterTime (1..*) <"ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (d)(iii): Valuation Time.">;
	// In relation to the above, need to confirm that several valuation times can be specified, as suggested by the sample document which has been used, although this sounds peculiar if not expressed as one valuation time election per party
	notificationTime int (1..1) <"The extend of time by which a demand for the Transfer of Eligible Credit Support (VM) or Posted Credit Support (VM) needs to be made after the relevant Valuation Time in order for the transfer to take place. This time is expressed in a number of minutes. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (d)(iv): Notification Time.">;
	// TODO - Look to express this elapse time as an interval time, to be projected as a a java Interval type https://trello.com/c/hclQUeS0 
	holdingAndUsingPostedCollateral HoldingAndUsingPostedCollateral (1..1) <"The elections for the holding and using of posted collateral by the respective parties to the Credit Support Annex for Variation Margin. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (h): Holding and Using Posted Collateral (VM).">; 
	creditSupportOffsets boolean (1..1) <"When True the Credit Support Offsets provisions in Paragraph 11(j) of the ISDA 2016 Credit Support Annex for Variation Margin apply. They don't apply when False. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (j): Credit Support Offsets.">;
	otherCsa RelatedAgreement (1..1) <"The qualification of whether Other CSA are specified by the parties to the agreement. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (o): Other CSA.">;
}

class CsaVariationMargin2016NewYorkLaw extends CsaVariationMargin2016 <"A class to specify the provisions that are specific to the New York Law version of the ISDA 2016 Credit Support Annex for Variation Margin.">
{
	otherEligibleSupport string (0..1) <"The Other Eligible Support election. If not specified, this election is deemed as not applicable. ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (c)(vi): Other Eligible Support (VM).">;
	transferTiming string (1..1) <"The time by which the transfer of collateral must take place after the Notification Time on the Regular Settlement Day. ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (c)(viii): Transfer Timing.">;
	otherEligibleAndPostedSupport OtherEligibleAndPostedSupport (1..1) <"The Other Eligible Support elections associated with margin agreements. ISDA 2016 New York Law Credit Support Annex for Variation Margin, paragraph 13, (l): Other Eligible Support (VM) and Other Posted Support (VM).">;
}

class CustodianEvent <"A class to specify the Custodian Event (English Law & New York Law ISDA CSA) and the Collateral Manager Event (Japanese Law ISDA CSA) in terms of applicability and end-date. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (n)(iii): Custodian Event. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(ii): Collateral Manager Event. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): Custodian Event.">
{
	isApplicable boolean (1..1) <"The qualification as to whether the Custodian Event (English Law & New York Law ISDA CSA) or the Collateral Manager Event (Japanese Law ISDA CSA) is applicable.">;
		[synonym ISDA_Create_1_0 set to True when path = "custodian_event.definition.applicable",
			set to True when path = "collateral_manager_event.definition.applicable",
			set to False when path = "custodian_event.definition.not_applicable",
			set to False when path = "collateral_manager_event.definition.not_applicable"]
	endDate CustodianEventEndDate (1..1) <"The qualification of the Custodian Event (English Law & New York Law ISDA CSA) or Collateral Manager Event (Japanese Law ISDA CSA) End Date.">;
		[synonym ISDA_Create_1_0 value ce_end_date]
}

class CustodianEventEndDate <"A class to specify the Custodian Event (English Law & New York Law ISDA CSA) or Collateral Manager Event (Japanese Law ISDA CSA) End Date. Its qualification is function of three elective periods: either (i) a specified number of days after the occurrence of the Custodian Event (the daysAfterCustodianEvent attribute), (ii) or the number of days prior to the date on which the Control Agreement will terminate, with in this latter case the further qualification of the number of days prior to the Release Date if only one party has effectively provided the Timely Statement to the other party. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (n)(iii): CE End Date. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(ii): CME End Date. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): CE End Date.">
{
	daysAfterCustodianEvent CustomisableOffset (1..1) <"The parties' election to specify the number of days after the occurrence of the Custodian Event (English Law & New York Law ISDA CSA) or the Collateral Management Event (Japanese Law ISDA CSA) for the purpose of qualifying the CE/CME End Date.">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `after`
	releaseDate CustomisableOffset (1..1) <"The parties' election to specify the number of days prior to the termination of the Control Agreement (English Law & New York Law ISDA CSA) or the Collateral Management Event (Japanese Law ISDA CSA) for the purpose of qualifying the CE/CME End Date, in the case where advance notice is given.">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `release`
	dateOfTimelyStatement CustomisableOffset (1..1) <"The parties' election to specify the number of days one party has effectively provided the Timely Statement to the other party.">;
		// TODO - Need for a mapping logic that will map the elements in ce_end_date which prefix is `timely` or `date_of_timely`
}

class CustodianRisk <"A class to specify the Custodian Risk (English Law and New York Law ISDA CSA) and the Collateral Manager Risk (Japanese Law ISDA CSA) election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (n)(ii): Custodian (IM) Risk. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(i): Collateral Manager Risk. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Custodian (IM) Risk.">
{
	isSpecified boolean (1..1) <"The qualification as to whether the risk is deemed as Specified.">;
		[synonym ISDA_Create_1_0 set to True when "partyA_custodian_risk" = "other",
			set to False when "partyA_custodian_risk" = "not_specified",
			set to True when "partyB_custodian_risk" = "other",
			set to False when "partyB_custodian_risk" = "not_specified",
			set to True when "partyA_collateral_manager_risk" = "other",
			set to False when "partyA_collateral_manager_risk" = "not_specified",
			set to True when "partyB_collateral_manager_risk" = "other",
			set to False when "partyB_collateral_manager_risk" = "not_specified"]
	qualification string (0..1) <"The Custodian Risk (English Law and New York Law ISDA CSA) or Collateral Manager Risk (Japanese Law ISDA CSA) qualification. This attribute is optional because the Custodian Risk provision can be deemed as 'Specified', although not be qualified through this attribute.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

data rule CustodianRisk_specified <"The Custodian Risk (English Law and New York Law ISDA CSA) or Collateral Manager Risk (Japanese Law ISDA CSA) should only be qualified if that risk is deemed 'specified'.">
	when CustodianRisk -> isSpecified = False
	then CustodianRisk -> qualification is absent

class CustodianTerms <"A class to specify the requirements applicable to the custodian with respect to the holding of posted collateral.">
	[synonym FpML_5_10 value CustodianTerms]
{
	/*
	 * This class leverages work in the context of the FpML representation for the ISDA Standard CSA for the purpose of normalizing this election
	 * Those provisions go beyond what is specified as part of the 2016 CSA, which election has not yet been normalized
	 */
	minimumAssets Money (0..1) <"The minimal level of assets requirement with respect to the custody agent.">;
		[synonym FpML_5_10 value minimumAssets]
	minimumRating CreditNotations (0..1) <"The minimal rating requirement with respect to the custody agent.">;
		[synonym FpML_5_10 value minimumRating]
	designation string (0..1) <"The custodian designation.">;
		[synonym FpML_5_10 value initialDesignation]
}

class CustomisableOffset <"A class to specify an offset either as a normalized [multiplier, period, dayType] or as a custom provision of type string.">
{
	offset Offset (0..1);
	customProvision string (0..1);	
}

class CustodyArrangements <"A class to specify the Custody Arrangements for the English Law and New York Law ISDA Credit Support Annex. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Custody Arrangements.">
{
	partyElection CustodyArrangementsElection (2..2 ) <"The party-specific election with respect to the Custodian and Segregated Account details and the Custodian Risk provision.">;
	custodianEvent CustodianEvent (1..1) <"When specified as True, means that the Custodian Events specified in Paragraph 13 General Principles, (m)(iii) will constitute an Additional Termination Event. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (m)(iii): Custodian Event.">;
		[synonym ISDA_Create_1_0 set to True when path = "custodian_event.definition.applicable",
			set to True when path = "custodian_event.definition.not_applicable"]
	isCreditSupportDocument boolean (1..1) <"Unless specified as True, the Control Agreement is not a Credit Support Document under the agreement with respect to a party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 6 (e): The Control Agreement as a Credit Support Document.">;
		[synonym ISDA_Create_1_0 set to True when path = "control_agreement_as_a_credit_support_document.definition.creditSupportDocument",
			set to False when path = "control_agreement_as_a_credit_support_document.definition.not_specified"]
	hasControlAgreementLanguage boolean (1..1) <"Control Agreement language is specified when True.">;
		[synonym ISDA_Create_1_0 set to True when path = "relationship_with_the_control_agreement.include_language.include",
			set to False when path = "relationship_with_the_control_agreement.include_language.exclude"]
	otherProvisions string (0..1) <"ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(vii): Other Provisions.">;
}

class CustodyArrangementsElection <"A class to specify the Custody Arrangements election by each party to the agreement: custody agent and account(s) identification, as well as custodian risk qualification.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	custodian LegalEntity (1..1) <"The custody agent. While ISDA Create only specifies the custodian's name, specifying the legal entity as part of the CDM is deemed more appropriate, while this will still provide the ability to accommodate situations where only the entity name is available, as the entityId attribute is optional as part of the LegalEntity class.">;
	segregatedCashAccount Account (1..1) <"The identification of the segregated cash account for the purpose of holding cash collateral.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create suffix to identify cash vs. security with custodian_and_segregated_account_details as a path
	segregatedSecurityAccount Account (1..1) <"The identification of the segregated security account for the purpose of holding security collateral.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create suffix to identify cash vs. security with custodian_and_segregated_account_details as a path
	custodianRisk CustodianRisk (1..1) <"The qualification of the Custodian Risk. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Custodian (IM) Risk.">;
		[synonym ISDA_Create_1_0 value custodian_risk]
}

class DeliveryAmount one of <"A class to specify the application of Interest Amount with respect the Delivery Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">
{
	standardElection DeliveryAmountElectionEnum (0..1) <"The standard election as specified by an enumeration.">;
		[synonym ISDA_Create_1_0 value delivery_amount]
	customElection string (0..1) <"The custom election that might be specified by the parties to the agreement.">;
		[synonym ISDA_Create_1_0 value specify_delivery_amount]
}

class DisputeResolution one of <"A class to specify the election terms under which a party disputes (i) the Calculation Agent’s calculation of a Delivery Amount or a Return Amount, or (ii) the Value of any Transfer of Eligible Credit Support or Posted Credit Support. Parties can specify such election either through a business center time or through a custom election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (h): Dispute Resolution. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g): Dispute Resolution.">
{
	resolutionTime BusinessCenterTime (0..1) <"The time by which the dispute needs to be resolved, failure of which would trigger a recalculation alongside a process that is specified as part of the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (g)(i): Resolution Time. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (g)(i): Resolution Time.">;
		[synonym ISDA_Create_1_0 value resolution_time]
	otherTerms string (0..1) <"The custom Resolution Time election that might be specified by the parties.">;
		[synonym ISDA_Create_1_0 value specify path "resolution_time"]
}

class DistributionAndInterestPayment <"A class to specify the Distributions and Interest Payment provisions applicable to the Japanese Law ISDA 2016 CSA for Initial Margin and the New York Law ISDA 2016 CSA for Variation Margin. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Distributions and Interest Payment (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i): Distributions and Interest Payment (VM).">
{
	interestRate EligibleCurrencyInterestRate (1..*) <"The interest rate associated with initial or variation margin collateral, depending upon the type of credit agreement that this election is associated with. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(i): Interest Rate (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(i): Interest Rate (VM).">;
		[synonym ISDA_Create_1_0 value interest_rate]
	interestPaymentTransfer boolean (1..1) <"The Interest Payment Transfer is applicable when True. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Transfer of Interest Payment (IM) or application of Interest Amount (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(ii): Transfer of Interest Payment (VM) or application of Interest Amount (VM).">;
		[synonym ISDA_Create_1_0 set to True when path = "interest_transfer.interest_transfer.applicable",
			set to True when path = "interest_transfer.interest_transfer.not_applicable"]
	interestPaymentNetting boolean (1..1) <"The Interest Payment Netting is applicable when True. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii): Transfer of Interest Payment (IM) or application of Interest Amount (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(ii): Transfer of Interest Payment (VM) or application of Interest Amount (VM).">;
		[synonym ISDA_Create_1_0 set to True when path = "interest_payment_netting.interest_payment_netting.applicable",
			set to True when path = "interest_payment_netting.interest_payment_netting.not_applicable"]
	interestAmount InterestAmount (1..1) <"The application of Interest Amount with respect to the Delivery Amount and the Return Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">;
		[synonym ISDA_Create_1_0 value return_amount_delivery_amount]
	interestAdjustment InterestAdjustment (1..1) <"To election to specify whether the Interest Adjustment is applicable and what its periodicity is. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(ii): Transfer of Interest Payment (VM) or application of Interest Amount (VM).">;
		[synonym ISDA_Create_1_0 value interest_adjustment]
	negativeInterest boolean (1..1) <"Negative Interest is applicable when True, and not applicable when False. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): Other Interest Elections. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(iii): Other Interest Elections.">;
		[synonym ISDA_Create_1_0 set to True when path = "negative_interes.negative_interest.applicable",
			set to False when path = "negative_interes.negative_interest.not_applicable"]
	dailyInterestCompounding boolean (1..1) <"Daily interest compounding is applicable when True, and not applicable when False. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iii): Other Interest Elections. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(iii): Other Interest Elections.">;
		[synonym ISDA_Create_1_0 set to True when path = "daily_interest_compounding.daily_interest_compounding.applicable",
			set to False when path = "daily_interest_compounding.daily_interest_compounding.not_applicable"]
	alternativeProvision string (0..1 ) <"When the alternative provision clause is specified, it means that the ISDA CSA Japanese Law provisions specified in Paragraph 6(c)(ii) don't apply and are overwritten by this election. When it is not specified, it means that the ISDA CSA Japanese Law provisions specified in Paragraph 6(c)(ii) apply. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(iv): Alternative to Interest Amount (IM) and Interest Payment (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (i)(iv): Alternative to Interest Amount (VM) and Interest Payment (VM).">;	
		[synonym ISDA_Create_1_0 value specify path "alternative_to_interest_amounts_and_interest_payment"]
}


class ElectiveAmountElection <"A class to specify the party elective amounts which can be used for the purpose of specifying elections such as the ISDA CSA Threshold and Minimum Transfer Amount.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	amount Money (0..1) <"The elective amount when expressed as a currency amount. The associated PartyElectiveAmount_amount data rule enforces that the currency amount is actually greater than 0.">;
	customElection string (0..1) <"The elective amount when expressed as a custom election by the party.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
	noAmount number (0..1) <"The elective amount when specified as 0 with no associated currency. The associated PartyElectiveAmount_noAmount data rule enforces that the number is actually equal to 0.">;
		[synonym ISDA_Create_1_0 set to 0 when "partyA_minimum_transfer_amount" exists,
			set to 0 when "partyA_minimum_transfer_amount" exists]
}

choice rule ElectiveAmountElection_choice <"The elective amount is specified either as a currency amount, a custom election or a 0 amount with no associated currency.">
	for ElectiveAmountElection required choice between
	amount and customElection and noAmount
	
data rule ElectiveAmountElection_noAmount <"The noAmount attribute specifies a 0 amount election, with no associated currency.">
	when ElectiveAmountElection -> noAmount exists
	then ElectiveAmountElection -> noAmount = 0	

data rule ElectiveAmountElection_amount <"The amount attribute specifies a currency amount greater than 0.">
	when ElectiveAmountElection -> amount exists
	then ElectiveAmountElection -> amount -> amount > 0

class EligibilityToHoldCollateral <"A class to specify the conditions under which a party and its custodian(s) are entitled to hold collateral. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (h)(i): Eligibility to Hold Posted Collateral (VM); Custodians (VM).">
	[synonym FpML_5_10 value EligibilityToHoldCollateral]
	/*
	 * This class leverages work in the context of the FpML representation for the ISDA Standard CSA for the purpose of normalizing this election
	 * Those provisions go beyond what is specified as part of the 2016 CSA, which election has not yet been normalized
	 */
{
	partyTerms HoldingPostedCollateralEnum (1..*) <"The condition(s) required by a party from the other party to hold its posted collateral.">;
		[synonym FpML_5_10 value holdingPostedCollateral]
	custodianTerms CustodianTerms (0..1) <"The restrictions that might be required by a party from the other party's custodian agent to hold its posted collateral.">;
		[synonym FpML_5_10 value custodianTerms]
	eligibleCountry string (0..*) scheme <"The restrictions that might be required by a party from the other party in terms of country(ies) where collateral can be held.">;
		[synonym FpML_5_10 value eligibleCountry]
}

class EligibleCollateral <"The collateral eligibility as a function of the types of asset, the maturity and rating terms.">
	[synonym FpML_5_10 value EligibleCollateral]
{
	/*
	 * ISDA Create specifies the eligible collateral as well as the valuation % as a string. Let's discuss if there is room a for better approach.
	 * The below approach which combines the asset, the tenor and the credit rating qualification is extremely comprehensive and was vetted extensively as part of the
	 * FpML standard CSA initiative.
	 */
	eligibleAsset CollateralAssetDefinitionsEnum (1..1) <"The eligible asset is specified by reference to the ISDA 2003 Collateral Assets definitions.">;
		[synonym FpML_5_10 value eligibleAsset]
		[synonym ISDA_Create_1_0 value items] // This mapping will not actually work as such because ISDA Create is specified as a string
	lowerMaturity Period (0..1) <"The (optional) lower maturity band of the eligible collateral assets.">;
		[synonym FpML_5_10 value lowerMaturity]
	higherMaturity Period (0..1) <"The (optional) higher maturity band of the eligible collateral assets.">;
		[synonym FpML_5_10 value higherMaturity]
	minimumRating CreditNotations (0..1) <"The (optional) minimal rating terms that can be specified as part of the eligible collateral.">;
		[synonym FpML_5_10 value minimumRating]
	valuationPercentage number (0..1) <"Specifies the haircut associated to the collateral asset, expressed as a value comprised between 0 and 1. A value of 0.9 means that 90% of the value of the applicable asset is considered as collateral. Absence of this element implies that the valuation percentage is 1, i.e. 100%.">;
		[synonym FpML_5_10 value valuationPercentage]
		[synonym ISDA_Create_1_0 value valuation_percentage] // This mapping will not work in a number of cases, as the ISDA Credit field is specified as a string
}

data rule EligibleCollateral_valuationPercentage <"The valuation percentage should be specified as a value between 0 and 1.">
	when EligibleCollateral -> valuationPercentage exists
	then EligibleCollateral -> valuationPercentage >= 0
	and EligibleCollateral -> valuationPercentage <= 1

class EligibleCollateralVariationMarginElection <"A class to specify the parties' elections with respect to the eligible collateral for each of the respective parties when acting as a pledgor/chargor/obligor. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(ii): Eligible Collateral (VM).">
{
	party Party (1..1) reference <"The elective party.">;
	eligibleCollateral EligibleCollateral (1..*) <"The eligible collateral for the specified party (acting as the Pledgor in the New York Law agreement, as the Chargor in the English Law deed and as the Obligor in the Japanese Law agreement). ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(ii): Eligible Collateral (VM).">;
}

class EligibleCollateralVariationMargin <"A class to specify the eligible collateral elections by the respective parties to the Credit Support Annex for Variation Margin. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(ii): Eligible Collateral (VM).">
{
	partyElection EligibleCollateralVariationMarginElection (2..2) <"The parties' eligible collateral elections.">;
}

class EligibleCurrencyInterestRate <"A class to specify the interest rate associated with initial margin collateral. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n): Distributions and Interest Payment (IM).">
{
	currency string (1..1) scheme <"The eligible currency.">;
		[synonym ISDA_Create_1_0 value eligible_currency]
	interestRate number (1..1) <"The interest rate associated with the eligible currency.">;
		[synonym ISDA_Create_1_0 value interest_rate]
	actual365Currency string (1..1); // TODO - Need to clarify with Linklater what this attribute is
		[synonym ISDA_Create_1_0 value a365_currency]
}

class FxHaircutCurrency one of <"A class to specify the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation, as being either the Termination Currency or an FX Designated Currency.">
{
	isTerminationCurrency boolean (1..1) <"The reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is the Termination Currency when the Boolean value is set to True.">;
		[synonym ISDA_Create_1_0 set to True when path = "is_termination_currency.terminationCurrency",
			set to False when path = "is_termination_currency.fxCurrency"]
	fxDesignatedCurrency string (0..1) scheme <"When specified, the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value fx_designated_currency]
}

data rule FxHaircutCurrency_terminationCurrency <"The FX Designated Currency shouldn't be specified when the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is specified as the Termination Currency.">
	when FxHaircutCurrency -> isTerminationCurrency = True
	then FxHaircutCurrency -> fxDesignatedCurrency is absent

data rule FxHaircut_fxDesignatedCurrency <"The FX Designated Currency should be specified when the reference currency for the purpose of specifying the FX Haircut relating to a posting obligation is specified as not being the Termination Currency.">
	when FxHaircutCurrency -> isTerminationCurrency = False
	then FxHaircutCurrency -> fxDesignatedCurrency exists

class HoldingAndUsingPostedCollateral <"A class to specify the elections for the holding and using of posted collateral by the respective parties to the Credit Support Annex for Variation Margin. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (h): Holding and Using Posted Collateral (VM).">
{
	partyElection HoldingAndUsingPostedCollateralElection (2..2) <"The parties' elections for the holding and using of posted collateral.">;
}

class HoldingAndUsingPostedCollateralElection <"A class to specify the parties' elections related to the holding and using of posted collateral. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (h): Holding and Using Posted Collateral (VM).">
	[synonym FpML_5_10 value HoldingAndUsingPostedCollateral]
{
	party Party (1..1) reference <"The elective party.">;
		[synonym FpML_5_10 value partyReference]
	eligibilityToHoldCollateral EligibilityToHoldCollateral (1..1) <"The specification of the conditions under which a party and its custodian(s) are entitled to hold posted collateral.">;
		[synonym FpML_5_10 value eligibilityToHoldCollateral]
	useOfPostedCollateral boolean (1..1) <"Specifies whether the party to the agreement has the right to rehypothecate the collateral held (True), i.e. whether the condition specified in Paragraph 6, (c) of the ISDA 2016 Credit Support Annex for Variation Margin apply. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (h)(ii): Use of Posted Collateral (VM).">;
		[synonym FpML_5_10 value useOfPostedCollateral]
}

class IneligibleCreditSupport <"A class to specify the parties to which the provisions of Paragraph 11(g) of the ISDA 2016 Credit Support Annex for Variation Margin will apply to. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(iii): Legally Ineligible Credit Support (VM).">
{
	specifiedParty Party (0..2) reference <"The parties to which the provisions of Paragraph 11(g) of the ISDA 2016 Credit Support Annex for Variation Margin will apply to, as the Secured Party. ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (c)(iii): Legally Ineligible Credit Support (VM).">;
}

class InterestAdjustment <"A class to specify whether the Interest Adjustment is applicable and what its periodicity is. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">
{
	isApplicable boolean (1..1) <"The Interest Adjustment is applicable when True and not applicable when False">;
		[synonym ISDA_Create_1_0 set to True when path = "is_applicable.applicable",
			set to False when path = "is_applicable.not_applicable"]
	periodicity InterestAdjustmentPeriodicity (1..1) <"The qualification of the Interest Adjustment periodicity.">;		
}

class InterestAdjustmentPeriodicity one of <"A class to specify the Interest Adjustment periodicity either through a standardized election or a custom one. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">
{
	standardElection InterestAdjustmentPeriodicityEnum (0..1) <"The Interest Adjustment periodicity when specified through a standardized election.">;
	customElection string (0..1) <"The Interest Adjustment periodicity when specified through a custom election.">;
}

class InterestAmount <"A class to specify the application of Interest Amount with respect to the Delivery Amount and the Return Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">
{
	returnAmount ReturnAmount (1..1) <"The application of Interest Amount with respect the Return Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">;
	deliveryAmount DeliveryAmount (1..1) <"The application of Interest Amount with respect the Delivery Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">;
}

class LegalDocument extends LegalDocumentBase rosettaKey one of <"A class to specify the elections and variables that characterize a legal document.">
{
	csdInitialMargin2016EnglishLaw CsdInitialMargin2016EnglishLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Deed for Initial Margin, English Law.">;
	csaInitialMargin2016JapaneseLaw CsaInitialMargin2016JapaneseLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Initial Margin, Japanese Law.">;
	csaInitialMargin2016NewYorkLaw CsaInitialMargin2016NewYorkLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Initial Margin, New York Law.">;
	csaVariationMargin2016NewYorkLaw CsaVariationMargin2016NewYorkLaw (0..1) <"The elections and variables specified in Paragraph 13 of the ISDA 2016 Credit Support Annex for Variation Margin, New York Law.">;
}

class LegalDocumentAmendment <"A legal document amendment, which can be specified by either referencing a legal document that is described elsewhere in the document, or by specifying the identity of that amended document.">
{
	agreementDate date (1..1) <"The date on which the legal document has been agreed between the parties.">;
		[synonym FpML_5_10 value agreementDate]
	documentIdentifier Identifier (0..*) <"The legal document identifier. Several identifiers can be specified.">;
		[synonym FpML_5_10 value partyDocumentIdentifier]
}

/*
 * TODO ISDA Create information other than elections should be accessed from the API  /api/v1/subAccounts/{subAccountId}/negotiations/{negotiationId}
 * Need to work with Victor Parashiv @ Linklater to confirm how to access that API, which requires an AccountId and NegociationId
 * See email sent to Pierre on 28 Feb 2019
 */
abstract class LegalDocumentBase <"A class describing the legal document. The purpose of this abstract class is to provide the ability to associate a 'one of' feature to the LegalDocument class as a way to enforce a systematic choice between its attributes.">
{
	agreementDate date (1..1) <"The date on which the legal document has been agreed between the parties. This corresponds to the Date of Deed in an English Law document.">;
		[synonym FpML_5_10 value agreementDate path "legalDocumentHeader"]
		[synonym ISDA_Create_1_0 value execution_date]
	effectiveDate date (0..1) <"The date on which the agreement is effective, if different from the agreement date. It is expected that it will most often correspond to the agreement date, although there could be situations where the parties will explicitly agree on a distinct effective date.">;
		[synonym FpML_5_10 value effectiveDate path "legalDocumentHeader"]
	identifier Identifier (0..*) <"The legal document identifier. Several identifiers can be specified.">;
		[synonym FpML_5_10 value partyDocumentIdentifier path "legalDocumentHeader"]
		[synonym ISDA_Create_1_0 value id]
	lineage Lineage (0..1) <"The lineage into prior versions of this legal document or into other legal documents that might be referenced by it.">;
	documentType LegalDocumentType (1..1) <"The type of legal document, identified via a set of distinct attributes: name, publisher, governing law and version, e.g. ISDA 2013 Standard Credit Support Annex English Law.">;
		[synonym FpML_5_10 value documentType]
	contractualparty Party (2..2) reference <"The contractual parties to the legal document, which reference information is positioned as part of the partInformation attribute.">;
		[synonym FpML_5_10 value party]
	otherParty PartyRole (0..*) <"The role(s) that other party(ies) may have in relation to the legal agreement, further to the contractual parties.">;
	umbrellaAgreement UmbrellaAgreement (0..1) <"The parties to the agreement that might be associated to it as part of what is termed an umbrella agreement.">;
		[synonym ISDA_Create_1_0 value umbrella_agreement_and_principal_identification]
	partyInformation Party (2..*) <"The information about the parties involved with the legal documents, which consists of both the contractual parties and the parties that might be involved in relation to some other roles (e.g. custody agent).">;
}

class LegalDocumentType <"A class to specify the type of legal document, which is extended by each legal document instance, such as the ISDA 2016 CSA for Initial Margin.">
	[synonym FpML_5_10 value LegalDocumentType]
{
	name LegalDocumentNameEnum (1..1) <"The legal document name, e.g. Credit Support Annex for Variation Margin.">;
		[synonym FpML_5_10 value name]
		[synonym AcadiaSoft_AM_1_0 value documentName]
		[synonym ISDA_Create_1_0 value documentType]
	publisher LegalDocumentPublisherEnum (1..1) <"The legal document publisher, e.g. ISDA.">;
		[synonym FpML_5_10, AcadiaSoft_AM_1_0, ISDA_Create_1_0 value publisher]
	governingLaw LegalDocumentStyleEnum (0..1) <"The legal document reference style, e.g. English Law, New York Law or Japanese Law.">;
		[synonym FpML_5_10 value style]
		[synonym AcadiaSoft_AM_1_0 value governingLaw]
		[synonym ISDA_Create_1_0 value governingLaw]
	vintage string (0..1) <"In the case where successive definitions of the legal document have been developed, the vintage identification. This is typically (but not necessarily) done by referencing the year, e.g. 2013 in the case of the ISDA 2013 Standard Credit Support Annex.">;
		[synonym FpML_5_10, FpML_5_10 value version]
		[synonym AcadiaSoft_AM_1_0 value csaVersion]
}

class Method <"A class to specify the ISDA SIMM as the Method for all Covered Transactions with respect to all Regimes. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee).">
{
	version SimmVersion (1..1) <"The qualification of the ISDA SIMM version that is specified for all Covered Transactions as specified by ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles, (ee)(1).">;
		// TODO - Need to clarify how the specification of the version of ISDA SIMMTM applicable to the relevant Secured Party is specified as part of ISDA Create
		// ISDA Create for the 2016 IM NY Law only has the election for the SIMM calculation currency 
	calculationCurrency SimmCalculationCurrency (1..2) <"The SIMM Calculation Currency, as specified for each of the parties to the CSA Initial Margin. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ee)(3).">;
		[synonym ISDA_Create_1_0 value simm_calculation_currency]
}

class MinimumTransferAmount <"A class to specify amount of exposure reached before collateral has to be posted or returned. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(B): Minimum Transfer Amount. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(B): Minimum Transfer Amount. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(B): Minimum Transfer Amount.">
{
	partyElection ElectiveAmountElection (2..2) <"The parties' minimum transfer amount elections.">;
}

class MultipleCreditNotations <"A class to specify the specify multiple credit notations alongside a conditional 'any' or 'all' qualifier.">
{
	condition QuantifierEnum (1..1) <"An enumerated element, to qualify whether All or Any credit notation applies.">;
		[synonym FpML_5_10 value condition]
	creditNotation string (2..*) scheme <"At least two credit notations much be specified.">;
		[synonym FpML_5_10 value debtType meta creditNotation]
}

class MultipleDebtTypes <"A class to specify the ability to specify multiple credit debt types alongside a conditional 'any' or 'all' qualifier.">
{
	condition QuantifierEnum (1..1) <"An enumerated attribute, to qualify whether All or Any debt type applies.">;
		[synonym FpML_5_10 value condition]
	debtType string (2..*) scheme <"The type of debt, e.g. long term debt, deposit, ... FpML doesn't specific a scheme value, hence no enumeration is specified as part of the CDM. At least two debt types much be specified.">;
		[synonym FpML_5_10 value debtType meta debtTypeScheme]
}

class NotificationTime <"A class to specify the time by which a demand for the Transfer of Eligible Credit Support (IM) or Posted Credit Support (IM) needs to be made in order for the transfer to take place in accordance with the Transfer Timing provisions. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (d)(iii): Notification Time. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(iii): Notification Time. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iii): Notification Time.">
{
	partyElections NotificationTimeElection (2..2) <"The parties' Notification Time election.">;
}

class NotificationTimeElection <"A class to specify the party notification time election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (d)(iii): Notification Time. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (e)(iii): Notification Time. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(iii): Notification Time.">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	notificationTime BusinessCenterTime (0..1) <"The Notification Time as a time that is qualified as a standard business center.">;
	customNotification string (0..1) <"The Notification Time as a custom election.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

choice rule NotificationTimeElection_choice <"The Notification Time is specified either as a time that is qualified by a standard business center or as a custom election.">
	for NotificationTimeElection required choice between
	notificationTime and customNotification

class ObligorPostingObligations <"A class to specify the obligor(s) collateral posting obligations as specified under the terms of the Japanese Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	partyElection PostingObligationsElection (1..2) reference <"The obligor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
		[synonym ISDA_Create_1_0 value obligor]
}

class OneWayProvisions <"A class to specify whether One Way Provisions apply in relation to the ISDA CSA for Initial Margin and, if yes, to specify the Posting Party. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles (aa): One Way Provisions.">
{
	isApplicable boolean (1..1) <"The determination of whether the One Way Provisions are applicable (true) or not applicable (false).">;
		[synonym ISDA_Create_1_0 set to True when "one_way_provisions" = "applicable",
			set to False when "one_way_provisions" = "not_applicable"]
	postingParty Party (0..1) reference <"The Posting Party for the purposes of One Way Provisions. This party is specified in the case where the One Way Provision is deemed applicable.">;
		[synonym ISDA_Create_1_0 value posting_party]
}

data rule OneWayProvisions_postingPartyReference_exists <"This Posting Party must be specified in the case where the One Way Provision is deemed applicable.">
	when OneWayProvisions -> isApplicable = True
	then OneWayProvisions -> postingParty exists

data rule OneWayProvisions_postingPartyReference_absent <"This Posting Party must not be specified in the case where the One Way Provision is not deemed applicable.">
	when OneWayProvisions -> isApplicable = False
	then OneWayProvisions -> postingParty is absent

// TODO - Need to associate a more meaningful definition to the below class and attribute.  I couldn't find anything as part of the CSA terms.
class OtherEligibleAndPostedSupport <"A class to specify the Other Eligible Support elections associated with Japanese and New York Law Initial and Variation margin agreements. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM). | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p): Other Eligible Support (IM) and Other Posted Support (IM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (l): Other Eligible Support (VM) and Other Posted Support (VM).">
{
	applicableValue boolean (1..1) <"ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(i): Value. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(i): Value.">;
		[synonym ISDA_Create_1_0 set to True when path = "support_value.is_applicable.applicable",
			set to False when path = "support_value.is_applicable.not_applicable"]
	applicableTransfer boolean (1..1) <"ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(ii): Transfer. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (p)(ii): Transfer.">;
		[synonym ISDA_Create_1_0 set to True when path = "support_transfer.is_applicable.applicable",
			set to False when path = "support_transfer.is_applicable.not_applicable"]
}

class PartyDocumentIdentifier <"A class defining a legal document identifier issued by the indicated party.">
	[synonym FpML_5_10 value PartyDocumentIdentifier]
{
	id string (0..1) id;
		[synonym FpML_5_10 value id]
	partyReference string (1..1) <"Reference to the party that issued the document identifier.">;
		[synonym FpML_5_10 value partyReference]
	documentIdentifier Identifier (1..*) scheme <"While FpML specifies the document identifier with a value and an associated scheme, the CDM makes use of the Identifier, which has an explicit issuer. The issuer of this identifier is not necessarily the same as the party reference.">;
		[synonym FpML_5_10 value documentId meta legalDocumentIdScheme]
}

class PartyTerminationCurrency <"A class to specify the Termination Currency election  by the respective parties to the agreement.">
{
	partyElection TerminationCurrencyElection (2..2) <"The parties' Termination Currency election.">;
}

class PledgorPostingObligations <"A class to specify the pledgor(s) collateral posting obligations as specified under the terms of the New York Law ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	partyElection PostingObligationsElection (1..2) reference <"The pledgor party(ies) to which the posting obligations apply to, which can be either one of the parties to the legal agreement, or both of those.">;
		[synonym ISDA_Create_1_0 value pledgor]
}

class PostingObligationsElection <"A class to specify the collateral posting obligations as specified under the terms of the ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii).">
{
	party Party (1..1) reference <"The elective party.">;
		[synonym ISDA_Create_1_0 value chargor, pledgor, obligor]
		/*
		 * TODO - In the case where both parties are specified, ISDA Create specifies the value 'both', instead of 1 set of entries for party A, and 1 set for partyB.
		 * The proposed approach is to create 2 entries at level of ChargorPostingObligations/ObligorPostingObligations/PledgorPostingObligations through back-end logic (i.e. not in the syntax).
		 */
	asPermitted boolean (1..1) <"If set to True, the Control Agreement is a Credit Support Document with respect to the party(ies). ISDA 2016 Credit Support Annex for Initial Margin, paragraph 6, (e).">;
		[synonym ISDA_Create_1_0 
			set to True when "partyA_type" = "control_agreement",
			set to False when "partyA_eligible_currencies" exists,
			set to True when "partyB_type" = "control_agreement",
			set to False when "partyB_eligible_currencies" exists]
	eligibleCollateral EligibleCollateral (0..*) <"The eligible collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation. ISDA 2016 Credit Support Annex for Initial Margin, Eligible Credit Support (IM) Schedule.">;
		[synonym ISDA_Create_1_0 value partyA_eligible_currencies]
		[synonym ISDA_Create_1_0 value partyB_eligible_currencies]
	excludedCollateral string (0..1) <"The excluded collateral as specified in relation to the pledgor/chargor/obligor(s) posting obligation. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (ii)(B)(i).">; // This representation matches the current ISDA Create one, but should be aligned with the eligibleCollateral representation once that one is confirmed
		[synonym ISDA_Create_1_0 value partyA_control_agreement_specify, partyB_control_agreement_specify]
	additionalLanguage string (0..1) <"The additional language that might be specified by the parties to the legal agreement.">;
		[synonym ISDA_Create_1_0 value partyA_additional_language, partyB_additional_language]
}

data rule PostingObligationsElection_asPermitted <"The eligible collateral should be specified when the Control Agreement is a Credit Support Document with respect to the party(ies).">
	when PostingObligationsElection -> asPermitted = True
	then PostingObligationsElection -> eligibleCollateral is absent

data rule PostingObligationsElection_eligibleCollateral <"The eligible collateral shouldn't be specified when the Control Agreement isn't deemed a Credit Support Document with respect to the party(ies).">
	when PostingObligationsElection -> asPermitted = False
	then PostingObligationsElection -> eligibleCollateral exists

class ProcessAgent <"A class to specify the Process Agent that might be appointed by the parties to the agreement in accordance with the ISDA 2016 English Law CSA, paragraph 11(h). ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (t): Process Agent.">
{
	partyElection ProcessAgentElection (2..2 ) <"The parties' Process Agent election.">;
}

class ProcessAgentElection <"A class to specify the parties' respective elections with respect to the Process Agent as part of the English Law ISDA CSA.">
{
	party Party (1..1) reference <"The elective party.">;
		[synonym AcadiaSoft_AM_1_0 value party]
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	isApplicable boolean (1..1) <"The qualification of whether the Process Agent is applicable (True) or not applicable (False).">;
		[synonym ISDA_Create_1_0 set to True when path = "partyA_process_agent.other",
			set to True when path = "partyB_process_agent.other",
			set to False when path = "partyA_process_agent.not_applicable",
			set to False when path = "partyB_process_agent.not_applicable"]
	specification string (0..1) <"The Process Agent specification, when applicable.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

data rule ProcessAgentElection_applicable <"The Process Agent must be specified when it is applicable.">
	when ProcessAgentElection -> isApplicable = True
	then ProcessAgentElection -> specification exists

data rule ProcessAgentElection_notApplicable <"The Process Agent cannot be specified if deemed not applicable.">
	when ProcessAgentElection -> isApplicable = False
	then ProcessAgentElection -> specification is absent

class Regime <"A class to specify the regulatory regimes elections by the respective parties a legal agreement. 2016 ISDA Credit Support Annex for Initial Margin: Regime.">
	[synonym AcadiaSoft_AM_1_0 value Regime]
{
	partyElection RegimeElection (2..2 ) <"The parties' regulatory regime election.">;
}

class RegimeElection <"A class to specify the parties' respective elections with respect to the applicable regulatory regime(s) in their capacity as Secured Party (English Law & New York Law) or Obligee (Japanese Law).">
{
	party Party (1..1) reference <"The elective party.">;
		[synonym AcadiaSoft_AM_1_0 value party]
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	applicableRegime ApplicableRegime (1..*) <"The set of regulatory regimes and associated provisions applicable to the respective parties to the legal agreement.">;
	additionalRegime AdditionalRegime (0..*) <"The additional regulatory regime(s) that might be specified by the parties to a legal agreement. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, General Principles, (ll): The parties may from time to time agree in writing that other regimes also comprise `Regimes` and that the General Principles be adopted and/or amended to accommodate such additional Regimes.">;
		[synonym ISDA_Create_1_0 value additional_regimes]
}

abstract class RegimeTerms <"An abstract class that is used by the ApplicableRegime and the AdditionalRegime classes to specify the regulatory regime terms which are referred to as part of certain legal agreements, such as such as the ISDA 2016 and 2018 CSA for Initial Margin.">
{
	isApplicable boolean (1..1) <"A boolean flag to specify whether the regulatory regime is applicable.">;
		[synonym ISDA_Create_1_0 
			set to True when "partyA_secured_party" = "applicable",
			set to False when "partyA_secured_party" = "not_applicable",
			set to True when "partyB_secured_party" = "applicable",
			set to False when "partyB_secured_party" = "not_applicable"]
	simmException SimmException (1..1) <"The SIMM exception to the regulatory regime clause of the ISDA 2016 and 2018 CSA for Initial Margin as either a normalized value specified as part of an enumeration or a customized value specified of type string. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, Regime: SIMM Exception.">;
	retrospectiveEffect boolean (1..1) <"ISDA 2016 CSA for Initial Margin, paragraph 13 (b)(i): if `Retrospective Effect` is specified as applicable to a Regime (a `Retrospective Regime`) then all Covered Transactions (IM) under all other Regimes with an earlier Regime Effective Time will, to the extent that they would have been Covered Transactions (IM) under such Retrospective Regime had such Transactions been entered into at or after the Regime Effective Time of the Retrospective Regime, be deemed to be Covered Transactions (IM) for such Retrospective Regime.">;
		[synonym AcadiaSoft_AM_1_0 value retrospectiveEffect]
		[synonym ISDA_Create_1_0 
			set to True when "partyA_retrospective" = "applicable",
			set to False when "partyA_retrospective" = "not_applicable",
			set to True when "partyB_retrospective" = "applicable",
			set to False when "partyB_retrospective" = "not_applicable"]
	additionalType AdditionalTypeEnum (1..1) <"The Additional Type of transaction that can require the collection or delivery of initial margin under the specified regulatory regime for the purposes of Covered Transactions, as specified in ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (b)(B).">;
}

class RelatedAgreement <"A class to specify a related legal agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (s): Other CSA and Japanese Law CSA (VM). | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (o): Other CSA.">
{
	isSpecified boolean (1..1) <"The qualification of whether some other related agreement is specified (True) or not (False).">;
		[synonym ISDA_Create_1_0 set to True when path = "specify_other_csa.true",
			set to True when path = "specify_japanese_law_csa_vm.true",
			set to False when path = "specify_other_csa.false",
			set to False when path = "specify_japanese_law_csa_vm.false"]
	/*
	 * TODO - The below string representation corresponds to the current ISDA CSA representation.  
	 * We should consider adjusting it to provide the ability to cross-reference another agreement when electronically represented.
	 * In the CDM the adjustment would consist of having this attribute to be of type LegalAgreement, with an associated reference qualifier.
	 * As part of this, we should provide an unbounded cardinality.
	 */		
	legalDocument string (0..1) <"The specification of this other agreement, when the qualification is True.">;
		[synonym ISDA_Create_1_0 value specify]
}

data rule RelatedAgreement_notSpecified <"The related legal agreement should not be referenced if it is deemed as not specified as part of the boolean attribute.">
	when RelatedAgreement -> isSpecified = False
	then RelatedAgreement -> legalDocument is absent

data rule RelatedAgreement_specified <"The related legal agreement should be referenced if it is deemed as specified as part of the boolean attribute.">
	when RelatedAgreement -> isSpecified = True
	then RelatedAgreement -> legalDocument exists

class ReturnAmount one of <"A class to specify the application of Interest Amount with respect the Return Amount. ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (n)(ii).">
{
	includesDefaultLanguage boolean (0..1) <"Default language is included when True, and excluded when False.">;
		[synonym ISDA_Create_1_0 set to True when path = "return_amount.include",
			set to False when path = "return_amount.exclude"]
	customElection string (0..1) <"Custom election that might be specified by the parties to the agreement.">;
		[synonym ISDA_Create_1_0 value specify_return_amount]
}

class RightsEvent <"A class to specify the Pledgor/Obligor/Chargor Rights Event election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (j): Chargor Rights Event. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (j): Obligor Rights Event. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (j): Pledgor Rights Event.">
{
	includeCoolingOffLanguage boolean (1..1) <"The Pledgor/Obligor/Chargor Rights Event election includes cooling off language when the attribute is set of True.">;
		[synonym ISDA_Create_1_0 set to True when "cooling_off_language.include" exists,
			set to False when "cooling_off_language.not_specified" exists]
	customElection string (0..1) <"A custom Pledgor/Obligor/Chargor Rights Event election might be specified by the parties.">;
		[synonym ISDA_Create_1_0 value specify]
}

data rule RightsEvent_includeCoolingOffLanguage <"When the Rights Event election includes cooling off language, no custom election should be specified.">
	when RightsEvent -> includeCoolingOffLanguage = True
	then RightsEvent -> customElection is absent

data rule RightsEvent_customElection <"When the Rights Event is specified through a custom election, no standard cooling off language should be specified.">
	when RightsEvent -> customElection exists
	then RightsEvent -> includeCoolingOffLanguage = False

class SensitivityMethodology one of <"A class to specify the methodology according to which sensitivities to (i) equity indices, funds and ETFs, and (ii) commodity indices are computed. This specification is done either through a normalized election as part of the specifiedMethodology, or through a custom election via the customMethodology attribute. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (gg)(2).">
{
	specifiedMethodology SensitivitiesEnum (0..1) <"The methodology according to which sensitivities will be computed, when specified through a normalized election.">;
		[synonym ISDA_Create_1_0 value sensitivities_equity_indices_funds_etfs, sensitivities_commodity_indices]
	customMethodology string (0..1) <"The methodology according to which sensitivities will be computed, when specified through a custom election.">;
		[synonym ISDA_Create_1_0 value sensitivities_equity_indices_funds_etfs_specify, sensitivities_to_commodity_indices_specify]
}

class SimmCalculationCurrency <"A class to specify the ISDA SIMM Calculation Currency as either the Base Currency or an alternative currency. ISDA 2016 CSA for Initial Margin, Paragraph 13, General Principles (ee)(3). | ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles (ee)(3).">
{
	party Party (1..1) reference <"The party which the SIMM Calculation Currency qualification applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	isBaseCurrency boolean (1..1) <"The SIMM Calculation Currency (also known as SIMM Reporting Currency) means the Base Currency when True. It means a different currency when False. In that latter case, the SIMM Calculation Currency is specified as part of the currency attribute.">;
		[synonym ISDA_Create_1_0 value partyA_use_base_currency, partyB_use_base_currency]
	currency string (0..1) scheme <"The currency in which the ISDA SIMM Calculation is denominated, when different from the Base Currency. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value partyA_use_other_currency, partyB_use_other_currency]
		[synonym AcadiaSoft_AM_1_0 value currency]
}

data rule SimmCalculationCurrency_baseCurrency <"When the SIMM calculation currency is specified as the Base Currency, the currency attribute should not be specified.">
	when CsaInitialMargin2016 -> method -> calculationCurrency -> isBaseCurrency = True
	then CsaInitialMargin2016 -> method -> calculationCurrency -> currency is absent

data rule SimmCalculationCurrency_notBaseCurrency <"When the SIMM calculation currency is specified as distinct from the Base Currency, the currency attribute should be specified.">
	when SimmCalculationCurrency -> isBaseCurrency = False
	then SimmCalculationCurrency -> currency exists

class SimmException one of <"A class to specify the SIMM exception to the regulatory regime clause of the ISDA 2016 and 2018 CSA for Initial Margin as either a normalized value specified as part of an enumeration or a customized value specified of type string. ISDA 2016 Credit Support Annex for Initial Margin paragraph 13, Regime: SIMM Exception.">
{
	standardisedException SimmExceptionEnum (0..1) <"The Standard Initial Margin Model exception when specified by the party according to one of the enumerated values.">;
		[synonym AcadiaSoft_AM_1_0 set to "NotApplicable" when "simmException" = "False"]
		[synonym ISDA_Create_1_0 set to "NotApplicable" when "partyA_SIMM" = "not_applicable",
			set to "NotApplicable" when "partyB_SIMM" = "not_applicable",
			set to "Applicable" when "partyA_SIMM" = "applicable",
			set to "Applicable" when "partyB_SIMM" = "applicable"]
	asSpecified string (0..1) <"The Standard Initial Margin Model exception when specified as a customized approach by the party.">;
		[synonym ISDA_Create_1_0 value partyA_SIMM_applicable_specify, partyB_SIMM_applicable_specify]
}

class SimmVersion <"A class to specify the ISDA SIMM version that applies to the ISDA 2018 CSA for Initial Margin. According to the ISDA 2018 CSA for Initial Margin, Paragraph 13, General Principles (ee) (1) provisions, the SIMM version is either not specified, or references a version used by one of the parties to the agreement.">
{
	isSpecified boolean (1..1) <"A boolean attribute to determine whether the SIMM version is specified for the purpose of the legal agreement.">;
	version SpecifiedSimmVersion (0..1) <"When the SIMM version is specified, it will reference the party that uses such version.">;
}

data rule SimmVersion_version1 <"The version attribute should be absent when the SIMM version is stated as not specified for the CSA.">
	when SimmVersion -> isSpecified = False
	then SimmVersion -> version is absent

data rule SimmVersion_version2 <"The version attribute should be specified when the SIMM version is stated as specified for the CSA.">
	when SimmVersion -> isSpecified = True
	then SimmVersion -> version exists

class SpecifiedSimmVersion <"A class to specify the ISDA SIMM version applicable to one of the parties to the CSA agreement that will then be the relevant version for that CSA.">
{
	party Party (1..1) reference <"The party which the specified SIMM version applies to.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	simmVersion string (1..1) <"The applicable ISDA SIMM version.">;
}

class Substitution <"A class to specify the conditions under which the Pledgor can substitute posted collateral. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f): Substitution. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (f): Substitutions.">
{
	needsConsent boolean (1..1) <"The election as to whether the Pledgor/Obligor/Chargor must obtain the Secured Party’s consent for any collateral substitution. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (f)(ii): Consent. | ISDA 2016 Credit Support Annex for Variation Margin, paragraph 13, (f)(ii): Consent.">;
		[synonym ISDA_Create_1_0 set to True when path = "consent.is_applicable",
			set to True when path = "consent.is_applicable.standard",
			set to True when path = "consent.is_applicable.other",
			set to False when path = "consent.is_applicable.not_applicable"]
	specificConsentLanguage string (0..1) <"Specific consent language might be specified by the parties.">;
		[synonym ISDA_Create_1_0 value specify path "consent"] 
}

class TerminationCurrencyAmendment <"A class to specify the Amendment to Termination Currency elections by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t): Amendment to Termination Currency.">
{
	isApplicable boolean (1..1) <"The qualification of whether the Amendment to Termination Currency is deemed applicable by the parties (True) or not (False).">;
		[synonym ISDA_Create_1_0 set to True when path = "is_applicable.applicable",
			set to False when path = "is_applicable.not_applicable"]
	effectiveDate AmendmentEffectiveDate (0..1) <"The effective date of the Amendment to Termination Currency. This date can be specified as either an actual date, a specific date (e.g. the annex date) or as a custom provision.">;
	terminationCurrency PartyTerminationCurrency (0..1) <"The parties' election with respect to the Termination Currency.">;
	bothPartiesCurrency string (0..1) scheme <"The currency election in the case of a Termination Event where there are two Affected Parties.">;
		[synonym ISDA_Create_1_0 value both_parties_termination_currency]
}

data rule TerminationCurrencyAmendment_applicable <"The Amendment to Termination Currency elections only apply when the Amendment to Termination Currency is deemed applicable by the parties.">
	when TerminationCurrencyAmendment -> isApplicable = False
	then ( TerminationCurrencyAmendment -> effectiveDate 
	and TerminationCurrencyAmendment -> terminationCurrency
	and TerminationCurrencyAmendment -> bothPartiesCurrency	) is absent // TODO - Logic to be confirmed by testing NotApplicable
	
class TerminationCurrencyElection <"A class to specify the Amendment to Termination Currency election by the parties to the agreement. ISDA 2016 Credit Support Annex for Initial Margin, paragraph 13, General Principles, (t) (A) & (B).">
{
	party Party (1..1) reference <"The elective party.">;
		// TODO - Need for a mapping logic that will parse the ISDA Create prefix to identify partyA vs. partyB entries
	currency string (1..1) scheme <"The Termination Currency associated with the party that referenced as part of this class. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">;
		[synonym ISDA_Create_1_0 value partyA_termination_currency, partyB_termination_currency]
}

class Threshold <"A class to specify the unsecured credit exposure that each party to the agreement is prepared to accept before asking for collateral. This threshold is specified either as an amount or as a custom election. ISDA 2016 English Law Credit Support Deed for Initial Margin, paragraph 13, General Principles, (c)(vi)(A): Threshold. | ISDA 2016 Japanese Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (d)(vi)(A): Threshold. | ISDA 2016 New York Law Credit Support Annex for Initial Margin, paragraph 13, General Principles, (c)(vi)(A): Threshold.">
{
	partyElection ElectiveAmountElection (2..2) <"The parties' Threshold election.">;
}

class UmbrellaAgreement <"A class to specify a set of legal entities which are part of a legal agreement beyond the two contracting parties to that agreement. This data represent reflects the ISDA Create representation.">
{
	language string (1..1) <"The language associated with the umbrella agreement, and which applies to all the parties to the umbrella agreement.">;
		[synonym ISDA_Create_1_0 value language]
	parties UmbrellaAgreementEntity (2..*) <"The parties to the umbrella agreement.">;
		[synonym ISDA_Create_1_0 value principal_identification_schedule]
}

class UmbrellaAgreementEntity extends LegalEntity <"A class to specify the legal entities that are part of the umbrella agreement.">
{
	terms string (0..1) <"The terms that might be associated with each party to the umbrella agreement.">;
		[synonym ISDA_Create_1_0 value additional]
}