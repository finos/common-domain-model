namespace cdm.base.staticdata.asset.common : <"Basic static asset concepts that apply across asset classes: taxonomy etc.">
version "${project.version}"

import cdm.base.staticdata.party.*

import cdm.synonyms.config.*

type AssetPool: <"Characterizes the asset pool behind an asset backed bond.">

	version string (0..1) <"The asset pool version.">
	effectiveDate date (0..1) <"Optionally it is possible to specify a version effective date when a version is supplied.">
	initialFactor number (1..1) <"The part of the mortgage that is outstanding on trade inception, i.e. has not been repaid yet as principal. It is expressed as a multiplier factor to the mortgage: 1 means that the whole mortgage amount is outstanding, 0.8 means that 20% has been repaid.">
	currentFactor number (0..1) <"The part of the mortgage that is currently outstanding. It is expressed similarly to the initial factor, as factor multiplier to the mortgage. This term is formally defined as part of the 'ISDA Standard Terms Supplement for use with credit derivatives transactions on mortgage-backed security with pas-as-you-go or physical settlement'.">
	condition EffectiveDate: <"FpML specifies that it is possible to specify a version effective date when a versionId is supplied.">
		if version is absent
		then effectiveDate is absent

//TODO: To be deprecated from model, now replaced by Debt as an attribute of Security which extends ProductBase.
type Bond extends IdentifiedProduct: <"A class to specify a bond as having a product identifier. As a difference versus the FpML standard, the CDM structure of this class only includes the productIdentifier class, which consists of an identifier, productTaxonomy, and source of the identifier. The reason for this approach is to avoid the potential for conflicting information between the information associated with the contractual product and the reference information maintained by the relevant service provider.">

//TODO: To be deprecated from model, now replaced by Debt as an attribute of Security which extends ProductBase.
type ConvertibleBond extends IdentifiedProduct: <"A class to specify a convertible bond as having a product identifier. As a difference versus the FpML standard, the CDM structure of this class only includes the productIdentifier class, which consists of an identifier, productTaxonomy, and source of the identifier. The reason for this approach is to avoid the potential for conflicting information between the information associated with the contractual product and the reference information maintained by the relevant service provider.">

//TODO: To be deprecated from model, now replaced by Equity as an attribute of Security which extends ProductBase.
type Equity extends IdentifiedProduct: <"A class to specify an equity as having a product identifier. As a difference versus the FpML standard, the CDM structure of this class only includes the productIdentifier class, which consists of an identifier, productTaxonomy, and source of the identifier. The reason for this approach is to avoid the potential for conflicting information between the information associated with the contractual product and the reference information maintained by the relevant service provider.">

type ProductIdentifier: <"Comprises of an identifier and a source. The associated metadata key denotes the ability to associate a hash value to the ProductIdentifier instantiations for the purpose of model cross-referencing, in support of functionality such as the event effect and the lineage.">
	[metadata key]
	identifier string (1..1) <"Provides an identifier associated with a specific product.  The identifier is unique within the public source specified in the source attribute.">
		[metadata scheme]
		[synonym Workflow_Event value "identifier"]
	source ProductIdTypeEnum (1..1) <"Defines the source of the identifier.">
	    [synonym Workflow_Event value "source"]

//can this be deprecated?
type IdentifiedProduct: <"An abstract class to specify a product which terms are abstracted through reference data.">
	productIdentifier ProductIdentifier (1..1)

type ProductBase: <"Serves as an abstract class to specify a product using a productIdentifier.">
	productIdentifier ProductIdentifier (1..*)

type Index extends ProductBase: <"Identifies an index by referencing a product identifier.">

type Commodity extends ProductBase: <"Identifies a commodity by referencing a product identifier.">

type Loan extends ProductBase: <"Identifies a loan by referencing a product identifier and through an optional set of attributes.">
	borrower LegalEntity (0..*) <"Specifies the borrower. There can be more than one borrower. It is meant to be used in the event that there is no Bloomberg Id or the Secured List isn't applicable.">
	lien string (0..1) <"Specifies the seniority level of the lien.">
		[metadata scheme]
	facilityType string (0..1) <"Specifies the type of loan facility (letter of credit, revolving, ...).">
		[metadata scheme]
	creditAgreementDate date (0..1) <"Specifies the credit agreement date is the closing date (the date where the agreement has been signed) for the loans in the credit agreement. Funding of the facilities occurs on (or sometimes a little after) the Credit Agreement date. This underlier attribute is used to help identify which of the company's outstanding loans are being referenced by knowing to which credit agreement it belongs. ISDA Standards Terms Supplement term: Date of Original Credit Agreement.">
	tranche string (0..1) <"Denotes the loan tranche that is subject to the derivative transaction. It will typically be referenced as the Bloomberg tranche number. ISDA Standards Terms Supplement term: Bloomberg Tranche Number.">
		[metadata scheme]

type ProductTaxonomy: <"Specifies the product taxonomy, which is composed of a taxonomy value and a taxonomy source.">
	taxonomyValue string (1..1) <"The taxonomy value.">
	taxonomySource TaxonomySourceEnum (1..1) <"The taxonomy source.">

type CollateralTaxonomy: <"Specifies the collateral taxonomy, which is composed of a taxonomy value and a taxonomy source.">
	taxonomyValue CollateralTaxonomyValue (1..1) <"The taxonomy value.">
	taxonomySource TaxonomySourceEnum (1..1) <"The taxonomy source.">

	condition EmirCollateral: <"If the Taxonomy Source is specified as EMIR Collateral then the enumeration must be EMIR Collateral.">
		if taxonomySource <> TaxonomySourceEnum -> EmirEligibleCollateral
		then taxonomyValue -> emirEligibleCollateral is absent

	condition usCollateral: <"If the Taxonomy Source is specified as US Collateral then the enumeration must be US Collateral.">
		if taxonomySource <> TaxonomySourceEnum -> UsEligibleCollateral
		then taxonomyValue -> usEligibleCollateral is absent

	condition taxonomyValue: <"If the Taxonomy Value is specified as a string then the taxonomy Source cannot be either EMIR Collateral or US Collateral.">
		if taxonomyValue -> taxonomyValue exists
		then taxonomySource <> TaxonomySourceEnum -> EmirEligibleCollateral and	taxonomySource <> TaxonomySourceEnum -> UsEligibleCollateral

type CollateralTaxonomyValue: <"Specifies the collateral taxonomy value, either as a specified enumeration or as a string.">
	emirEligibleCollateral EmirEligibleCollateralEnum (0..*) <"Official Journal of the European Union COMMISSION DELEGATED REGULATION (EU) 2016/2251 of 4 October 2016 - Eligible Collateral asset classes as listed under Chapter1/General Provisions on Risk Management Procedures/ SECTION2 Eligibility Article 4 Eligible Collateral">
	usEligibleCollateral UsCollateralEnum (0..*) <"US Federal Regulations 1221.6 Eligible Collateral (a) Non-cleared swaps and non-cleared security-based swaps with a swap entity For a non-cleared swap or non-cleared security-based swap with a swap entity, a covered swap entity shall collect initial margin only required pursuant to this part solely in the form of the following types of collateral">
	taxonomyValue string (0..1) <"The taxonomy value when not specified as an enumeration.">

type Security extends ProductBase: <"Identifies a security by referencing a product identifier and by specifying the sector.">
	securityType SecurityTypeEnum (1..1) <"Identifies the type of security using an enumerated list.">
	debtType DebtType (0..1) <"Identifies the type of debt and selected debt economics.">
	equityType EquityTypeEnum (0..1) <"Identifies the type of equity.">
	fundType FundProductTypeEnum (0..1) <"Identifies the type of fund.">

condition DebtSubType:
		if securityType <> SecurityTypeEnum -> Debt
		then debtType is absent

condition EquitySubType:
		if securityType <> SecurityTypeEnum -> Equity
		then equityType is absent

condition FundSubType:
		if securityType <> SecurityTypeEnum -> Fund
		then fundType is absent

type DebtType: <"Specifies the type of debt instrument.">
	debtClass DebtClassEnum (0..1) <"Specifies the characteristics of a debt instrument.">
	debtEconomics DebtEconomics (0..*) <"Specifies selected financial terms of a debt instrument.">

type DebtEconomics: <"Specifies selected economics of a debt instrument.">
	debtSeniority DebtSeniorityEnum (0..1) <"Specifies the order of repayment in the event of a sale or bankruptcy of the issuer or a related party (eg guarantor).">
	debtInterest DebtInterestEnum (0..1) <"Specifies the general rule for periodic interest rate payment.">
	debtPrincipal DebtPrincipalEnum (0..1) <"Specifies the general rule for repayment of principal.">

type CollateralIssuerType: <"A class to allow specification of the type of entity issuing the collateral.">

	issuerType IssuerTypeEnum (1..1) <"Origin of entity issuing the collateral.">
	supraNationalType SupraNationalIssuerTypeEnum (0..1) <"Debt issued by international organisations and multilateral banks.">
	quasiGovernmentType QuasiGovernmentIssuerType (0..1) <"Debt issues by institutions or bodies, typically constituted by statute, with a function mandated by the government and subject to government supervision inclusive of profit- and non-profit making bodies. Includes the US Agencies and GSEs and the EU concept of public sector entities. Excluding any entities which are also Regional Government">
	regionalGovernmentType RegionalGovernmentIssuerType (0..1) <"Regional government, local authority or municipal.">
	specialPurposeVehicleType SpecialPurposeVehicleIssuerType (0..1)

	condition SupraNationalSubType:
		if issuerType <> IssuerTypeEnum -> SupraNational
		then supraNationalType is absent

	condition QuasiGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> QuasiGovernment
		then quasiGovernmentType is absent

	condition RegionalGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> RegionalGovernment
		then regionalGovernmentType is absent

	condition SpecialPurposeVehicleSubType:
		if issuerType <> IssuerTypeEnum -> SpecialPurposeVehicle
		then specialPurposeVehicleType is absent

type QuasiGovernmentIssuerType: <"A class to allow specification of different types of Quasi Government collateral.">

	sovereignEntity boolean (1..1) <"True if sovereign entity (e.g. not separate legal personality from sovereign) or false if non-sovereign entity (e.g. separate legal personality from sovereign).">
	sovereignRecourse boolean (0..1) <"Applies to non-sovereign entity (e.g. separate legal personality from sovereign).  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// TODO: other (non-exhaustive) criteria e.g. GSEs with/without government support, EMIR public sector entities, US agencies
	condition NonSovereignEntityRecourse:
		if sovereignRecourse exists then sovereignEntity = False

type RegionalGovernmentIssuerType: <"A class to allow specification of different type of Regional government collateral.">

	sovereignRecourse boolean (1..1) <"Applies to regional governments, local authorities or municipals.  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// TODO: other (non-exhaustive) criteria e.g. US municipals, Canadian provinces, German Lander

type SpecialPurposeVehicleIssuerType: <"A class to allow specification of different types of special purpose vehicle (SPV) collateral.">
	creditRisk CreditRiskEnum (0..1) <"Tranched or untranched credit risk."> // Not required?  If tranched credit risk then specified ProductCriteria.agencyRating.
	// TODO: other (non-exhaustive) criteria e.g. by asset class (RMBS, CMBS, CLO, by regional risk focus, by guarantor (e.g. Ginnie Mae)). Can the existing FpML framework be leveraged.  See type MortgageBackedSecurity

type ExternalProductType: <"Provides a classification of the type of product that is external to the product qualifications used in the CDM. Product types mapped from external models are mapped to this data type.">
	value string (1..1) <"Stores the product type value.">
	    [metadata scheme]
	externalProductTypeSource ExternalProductTypeSourceEnum (1..1) <"Denotes the enumerated value to identify the source for the specified product type.">

type AssetType: <"A class to allow specification of the asset product type.">
	assetType AssetTypeEnum (1..1) <"The type of collateral asset.">
	securityType SecurityTypeEnum (0..1) <"Filter based on the type of security.">
	debtType DebtType (0..1) <"Filter based on the type of bond.">
	equityType EquityTypeEnum (0..1) <"Filter based on the type of equity.">
	fundType FundProductTypeEnum (0..1) <"Filter based on the type of fund.">
	// otherType string (0..1) <"Description of collateral when specified as Product Type - Other">

	condition SecuritySubType:
		if assetType <> AssetTypeEnum -> Security
		then ( securityType and debtType and equityType ) is absent
	
	condition BondSubType:
		if securityType <> SecurityTypeEnum -> Debt
		then debtType is absent

	condition EquitySubType:
		if securityType <> SecurityTypeEnum -> Equity
		then equityType is absent

	condition FundSubType:
		if securityType <> SecurityTypeEnum -> Fund
		then fundType is absent

	// condition OtherSubType:
	// 	if securityType <> SecurityTypeEnum -> Other
	// 	then otherType is absent
	