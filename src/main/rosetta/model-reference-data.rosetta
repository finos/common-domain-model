namespace "org.isda.cdm"
version "${project.version}"

class Account <"A class to specify an account as an account number alongside, optionally. an account name, an account type, an account beneficiary and a servicing party.">
	[synonym FpML_5_10 value Account]
{
	id (0..1);
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 meta id]
	accountNumber string (1..1) scheme <"The account number.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value accountId meta accountIdScheme]
		[synonym ISDA_Create_1_0 value id, partyA_cash, partyA_securities ]
	accountName string (0..1) scheme <"The name by which the account is known.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value accountName meta accountNameScheme]
		[synonym ISDA_Create_1_0 value name]
	accountType AccountTypeEnum (0..1) scheme <"The type of account, e.g. client, house.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value accountType meta accountTypeScheme]
	accountBeneficiary Party (0..1) reference <"A reference to the party beneficiary of the account.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value accountBeneficiary meta href]
	servicingParty Party (0..1) reference <"The reference to the legal entity that services the account, i.e. in the books of which the account is held.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value servicingParty meta href]
}

class Address <"A class to specify a post or street address.">
	[synonym FpML_5_10 value Address]
{
	street string (1..*) <"The set of street and building number information that identifies a postal address within a city.">;
		[synonym FpML_5_10 value streetAddress]
	city string (1..1) <"The city component of the postal address.">;
		[synonym FpML_5_10 value city]
	state string (0..1) <"A country subdivision used in postal addresses in some countries. For example, US states, Canadian provinces, Swiss cantons, ...">;
		[synonym FpML_5_10 value state]
	country string (1..1) scheme <"The ISO 3166 standard code for the country within which the postal address is located.">;
		[synonym FpML_5_10 value country meta countryScheme]
	postalCode string (1..1) <"The code, required for computerized mail sorting systems, that is allocated to a physical address by a national postal authority.">;
		[synonym FpML_5_10 value postalCode]
}

class BusinessUnit <"A class to specify an organizational unit.">
	[synonym FpML_5_10 value BusinessUnit]
{
	id (0..1);
		[synonym FpML_5_10 meta id]
	name string (1..1) <"A name used to describe the organizational unit">;
		[synonym FpML_5_10 value name]
	identifier Identifier (0..1) <"An identifier used to uniquely identify the organizational unit">;
	contactInformation ContactInformation (0..1) <"The contact information for such business unit, when different from the contact information associated with the party.">;
		[synonym FpML_5_10 value contactInfo]
}

class ContactInformation <"A class to specify contact information associated with a party: telephone, postal/street address, email and web page.">
	[synonym FpML_5_10 value ContactInformation]
{
	telephone TelephoneNumber (0..*) <"The telephone number.">;
		[synonym FpML_5_10 value telephone]
	address Address (0..*) <"The street/postal address.">;
		[synonym FpML_5_10 value address]
	email string (0..*) <"The email address.">;
		[synonym FpML_5_10 value email]
	webPage string (0..*) <"The web page. This attribute is not specified as part of the FpML ContactInformation complex type.">;
}

class LegalEntity <"A class to specify a legal entity, with a required name and an optional entity identifier (such as the LEI).">
{
	id (0..1);
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 meta id path "referenceEntity"]
	entityId string (0..*) scheme <"A legal entity identifier (e.g. RED entity code).">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityId meta entityIdScheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityId path "referenceEntity" meta entityIdScheme]
		[synonym ISDA_Create_1_0 value lei]
	name string (1..1) scheme <"The legal entity name.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value partyName meta entityNameScheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityName meta entityNameScheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityName path "referenceEntity" meta entityNameScheme]
		[synonym CME_SubmissionIRS_1_0 value SID path "Hdr"]
		[synonym ISDA_Create_1_0 value partyA_custodian_name path "custodian_and_segregated_account_details", partyB_custodian_name path "custodian_and_segregated_account_details"]
		[synonym ISDA_Create_1_0 value principal_name]
}

class NaturalPerson <"A class to represent the attributes that are specific to a natural person.">
	[synonym FpML_5_10 value Person]
{
	id (0..1);
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 meta id]
	honorific string (0..1) <"An honorific title, such as Mr., Ms., Dr. etc.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value honorific]
	firstName string (1..1) <"The natural person's first name. It is optional in FpML.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value firstName]
	middleName string (0..*);
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value middleName]
	initial string (0..*);
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value initial]
	surname string (1..1) <"The natural person's surname.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value surname]
	suffix string (0..1) <"Name suffix, such as Jr., III, etc.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value suffix]
	dateOfBirth date (0..1) <"The natural person's date of birth.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value dateOfBirth]
}

choice rule NaturalPerson_choice <"Choice rule to represent an FpML choice construct.">
	for NaturalPerson optional choice between
	middleName and initial

class NaturalPersonRole <"A class to specify the role(s) that natural person(s) may have in relation to the contract.">
{
	personReference NaturalPerson (1..1) reference <"A reference to the natural person to whom the role refers to.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value personReference meta href]
	role NaturalPersonRoleEnum (0..1) scheme <"FpML specifies a person role that is distinct from the party role.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value role meta personRoleScheme]
}

class Party <"A class to specify a party, without a qualification as to whether this party is a legal entity or a natural person, although the model provides the ability to associate a person (or set of persons) to a party, which use case would imply that such party would be a legal entity (even if not formally specified as such). ">
	[synonym FpML_5_10 value Party]
{
	id (0..1);
		[synonym FpML_5_10, CME_ClearedConfirm_1_17, CME_SubmissionIRS_1_0 meta id]
		[synonym DTCC_11_0, DTCC_9_0 meta id maps 2]
	partyId string (1..*) scheme <"The identifier associated with a party, e.g. the 20 digits LEI code.">;
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17, CME_SubmissionIRS_1_0 value partyId meta partyIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value partyId maps 2 meta partyIdScheme]
		[synonym ISDA_Create_1_0 value id]
	name string (0..1) scheme <"The party name.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value partyName meta entityNameScheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityName meta entityNameScheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value entityName path "referenceEntity" meta entityNameScheme]
		[synonym CME_SubmissionIRS_1_0 value SID path "Hdr"]
		[synonym ISDA_Create_1_0 value name]
		[synonym ISDA_Create_1_0 value partyA_name, partyB_name]
	person NaturalPerson (0..*) <"The person(s) who might be associated with the party as part of the execution, contract or legal document.">;
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17, CME_SubmissionIRS_1_0 value person]
	account Account (0..1) <"The account that might be associated with the party. At most one account can be specified, as it is expected that this information is used in the context of a contract or legal document where only one account per party can be associated with such object.">;
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17, CME_SubmissionIRS_1_0, ISDA_Create_1_0 value account]
}

class PartyContactInformation <"A class to specify contact information within a party: address and, optionally, associated business unit and person. This class also support the ISDA CSA representation as a single string, through the address attribute.">
	[synonym FpML_5_10 value PartyContactInformation]
{
	partyReference Party (1..1) reference <"The reference to the party to which the contact information refers to.">;
		[synonym FpML_5_10 value partyReference meta href]
	contactInformation ContactInformation (0..1) <"The postal/street address, telephone number, email address and/or web page. If the contact information is specific to the associated business unit(s), it should be associated with those.">;
		[synonym FpML_5_10 value contactInfo]
	businessUnit BusinessUnit (0..*) <"Optional organization unit information used to describe the organization units (e.g. trading desks) involved in a transaction or business process, incl. the contact information (when relevant).">;
		[synonym FpML_5_10 value businessUnit]
	person NaturalPerson (0..*) <"Optional information about people involved in a transaction or business process. (These are employees of the party.)">;
		[synonym FpML_5_10 value person]
	address string (0..1) <"The address specified as a string to support non-normalized contact information, such as in the case of ISDA Create.">;
		[synonym ISDA_Create_1_0 value partyA_specify, partyB_specify]
}

choice rule PartyContactInformation_choice <"The address attribute corresponds to a non-normalized alternative representation to the contactInformation.">
	for PartyContactInformation optional choice between
	contactInformation and address	

class PartyRole <"A class to specify the role(s) that party(ies) may have in relation to the execution, contract or other legal agreement.">
{
	partyReference Party (1..1) reference <"A reference to the party to which the role refers to.">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value determiningParty path "trade" meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value barrierDeterminationAgent path "trade" meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value hedgingParty path "trade" meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value brokerPartyReference path "trade" meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value partyReference path "trade.tradeHeader.partyTradeIdentifier.relatedParty" meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0 value partyReference path "tradeHeader.partyTradeInformation.relatedParty" meta href]
		[synonym ISDA_Create_1_0 value partyA]
	role PartyRoleEnum (1..1) <"The party role.">;
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0 set to PartyRoleEnum.DeterminingParty when path = "trade.determiningParty"]
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0 set to PartyRoleEnum.BarrierDeterminationAgent when path = "trade.barrierDeterminationAgent"]
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0 set to PartyRoleEnum.HedgingParty when path = "trade.hedgingParty"]
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0 set to PartyRoleEnum.ArrangingBroker when path = "trade.brokerPartyReference"]
		[synonym CME_ClearedConfirm_1_17 value role path "trade.tradeHeader.partyTradeIdentifier.relatedParty"]
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0 value role path "tradeHeader.partyTradeInformation.relatedParty"]
	ownershipPartyReference Party (0..1) reference <"A reference to the party that has ownership of this party role information. FpML specifies that For shared trade information, this attribute will reference the originator of the data (for example, an execution facility or clearing house).">;
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0 value partyReference path "tradeHeader.partyTradeInformation" meta href]
}

class TelephoneNumber <"A class to specify a telephone number as a type of phone number (e.g. work, personal, ...) alongside with the actual number.">
{
	type TelephoneTypeEnum (0..1) <"The type of telephone number, e.g. work, mobile.">;
	number string (1..1) <"The actual telephone number.">;
}