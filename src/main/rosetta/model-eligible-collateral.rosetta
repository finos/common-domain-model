namespace org.isda.cdm
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.math.*
import cdm.base.staticdata.asset.common.*
import cdm.base.staticdata.party.*

// Note: All Eligible Collateral modelling/representation is still under development and discussion with ISDA Working groups

type AgencyRatingCriteria: <"A class to specify multiple credit notations alongside a conditional 'any' or 'all' qualifier.">
	qualifier QuantifierEnum (1..1) <"Indicates whether all or any agency ratings apply.">
	creditNotation CreditNotation (1..*)
		[metadata scheme]
	mismatchResolution CreditNotationMismatchResolutionEnum (0..1)
	referenceAgency CreditRatingAgencyEnum (0..1)

	condition ReferenceAgency: <"If the mismatch resolution is ReferenceAgency, ensure that the reference agency is specified.">
		if AgencyRatingCriteria -> mismatchResolution = CreditNotationMismatchResolutionEnum -> ReferenceAgency
		then AgencyRatingCriteria -> referenceAgency exists

type CollateralIssuerType:
	issuerType IssuerTypeEnum (1..1)
	
	supraNationalType SupraNationalIssuerTypeEnum (0..1)
	quasiGovernmentType QuasiGovernmentIssuerType (0..1) <"Government or quasi-government entity.">
	regionalGovernmentType RegionalGovernmentIssuerType (0..1) <"Regional government, local authority or municipal.">
	specialPurposeVehicleType SpecialPurposeVehicleIssuerType (0..1)
	fundType FundIssuerType (0..1)
	
	condition SupraNationalSubType:
		if issuerType <> IssuerTypeEnum -> SupraNational		
		then supraNationalType is absent
	
	condition QuasiGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> QuasiGovernment
		then quasiGovernmentType is absent
	
	condition RegionalGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> RegionalGovernment
		then regionalGovernmentType is absent
	
	condition SpecialPurposeVehicleSubType:
		if issuerType <> IssuerTypeEnum -> SpecialPurposeVehicle
		then specialPurposeVehicleType is absent
		
	condition FundSubType:
		if issuerType <> IssuerTypeEnum -> Fund
		then fundType is absent

type QuasiGovernmentIssuerType: <"Government or quasi-government entities.">
	sovereignEntity boolean (1..1) <"True if sovereign entity (e.g. not separate legal personality from sovereign) or false if non-sovereign entity (e.g. separate legal personality from sovereign)."> // sovereignEntity: US Treasury, GILTs, Bank of England. nonSovereignEntity: Ginnie Mae
	sovereignRecourse boolean (0..1) <"Applies to non-sovereign entity (e.g. separate legal personality from sovereign).  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// Eligible sovereigns specified by IssuerCriteria.issuerCountryOfOrigin
	// Eligible issuers specified by IssuerCriteria.issuerName
	// TODO: other (non-exhaustive) criteria e.g. GSEs with/without government support, EMIR public sector entities, US agencies
	
	condition NonSovereignEntityRecourse:
		if sovereignRecourse exists then sovereignEntity = False

type RegionalGovernmentIssuerType: <"Regional government, local authority or municipal.">
	sovereignRecourse boolean (1..1) <"Applies to regional governments, local authorities or municipals.  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// Eligible sovereigns specified by IssuerCriteria.issuerCountryOfOrigin
	// Eligible issuers specified by IssuerCriteria.issuerName
	// TODO: other (non-exhaustive) criteria e.g. US municipals, Canadian provinces, German Lander

type SpecialPurposeVehicleIssuerType: <"A special purpose vehicle (SPV), also called a special purpose entity (SPE), is a subsidiary created by a parent company to isolate financial risk. Its legal status as a separate company makes its obligations secure even if the parent company goes bankrupt.">
	creditRisk CreditRiskEnum (0..1) <"Tranched or untranched credit risk."> // Not required?  If tranched credit risk then specified ProductCriteria.agencyRating. 
	// Eligible jurisdictions of incorporation is specified by IssuerCriteria.issuerCountryOfOrigin
	// TODO: other (non-exhaustive) criteria e.g. by asset class (RMBS, CMBS, CLO, by regional risk focus, by guarantor (e.g. Ginnie Mae))

type FundIssuerType:
	fundCountryOfOrigin string (0..*) <"Fund country of origin.  Note that fund manager country of origin is specified by IssuerCriteria.issuerCountryOfOrigin.">
		[metadata scheme]
	// exchangeTraded is specified by EquityTypeEnum.ExchangeTradedFund
	// TODO: other (non-exhaustive) criteria e.g. UCITS, Mutual funds, Hedge funds, Private Equity)

type CollateralProductType:
	productType CollateralProductTypeEnum (1..1)
	
	securityType SecurityTypeEnum (0..1) 
	bondType BondTypeEnum (0..1)
	equityType EquityTypeEnum (0..1)

	condition SecuritySubType:
		if productType <> CollateralProductTypeEnum -> Security
		then ( securityType and bondType and equityType ) is absent 
	
	condition BondSubType:
		if securityType <> SecurityTypeEnum -> Bond
		then bondType is absent
	
	condition EquitySubType:
		if securityType <> SecurityTypeEnum -> Equity
		then equityType is absent

type CollateralValuationPercentage:
    valuationPercentage number (1..1) <"Percentage value of asset to be used as collateral once discounted haircut take into consideration, expressed as a valuation comprised between 0.0 and 100.0. As an example a 0.5% haircut would be expressed as 99.5.">
	fxHaircutPercentage number (0..1) <"FX haircut applied to a specific asset which is agreed between the parties (for example, if pledgor eligible collateral is not denominated in the termination currency or under other specified cases in collateral support documents both for initial margin and variation margin), the percentage value is expressed as a discount haircut to the value of the collateral comprised between 0.0 and 100.0 - as an example an 8% FX haircut would be expressed as 8.0.">

type EligibleCollateral: <"Set of criteria used to specify eligible collateral.">
	criteria EligibleCollateralCriteria (1..*)

type EligibleCollateralCriteria: <"Criteria used to specify eligible collateral.">
	issuer IssuerCriteria (0..*) <"Filter based on the issuer.">
	product ProductCriteria (0..*) <"Filter based on the product.">
	valuationPercentage CollateralValuationPercentage (0..1) <"Filter based on the valuation percentage or FX haircut percentage.">

type IssuerCriteria: <"Criteria used to specify eligible collateral issuers.">
	issuerType CollateralIssuerType (0..*) <"Filter based on the type of entity issuing the asset.">
    issuerCountryOfOrigin string (0..*) <"Filter based on the issuing entity country of origin."> // Same as list of eligible Sorvereigns
		[metadata scheme]
	issuerName LegalEntity (0..*) <"Filter based on the issuing entity name or LEI.">
	issuerAgencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk and creditors claim in event of default associated with asset issuer.">
	sovereignAgencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk of country.">
	counterpartyOwnIssuePermitted boolean (0..1) <"Filter based on whether it is permitted for the the underlying asset to be issued by the posting entity or part of their corporate family."> // Explore options to infer this from one way risk
	
type ProductCriteria: <"Criteria used to specify eligible collateral assets.">
	collateralProductType CollateralProductType (0..*) <"Filter based on the asset product type.">
	denominatedCurrency string (0..*) <"Filter based on the underlying asset denominated currency.">
	    [metadata scheme]
	agencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk and creditors claim in event of default associated with specific instrument.">
	maturityType MaturityTypeEnum (0..1) <"Specifies whether the maturity range is the remaining or original maturity.">
	maturityRange PeriodRange (0..1) <"Filter based on the underlying asset maturity.">
	identifier ProductIdentifier (0..*) <"Filter based on specific instrument identifiers (e.g. specific ISINs, CUSIPs etc).">
    seasoned boolean (0..1) <"Filter based on whether underlying security is seasoned.">
	sinkable boolean (0..1) <"Filter based on whether underlying security is sinkable.">
	indexLinked boolean (0..1) <"Filter based on whether underlying security is index linked.">

/* 
// Previous version, temporarily kept for reference purposes
type EligibleCollateral: <"The collateral eligibility as a function of the types of asset, the maturity and rating terms.">
	[synonym FpML_5_10 value "EligibleCollateral"]
	// ISDA Create specifies the eligible collateral as well as the valuation % as a string. Let's discuss if there is room a for better approach.
	// The below approach which combines the asset, the tenor and the credit rating qualification is extremely comprehensive and was vetted extensively as part of the
	// FpML standard CSA initiative.
	// It should however be noted that one of the limitations of this approach is that the ISDA 2003 Collateral Assets Definitions have not been effectively used by
	// market participants as far as we understand.
	eligibleAsset CollateralAssetDefinitionsEnum (1..1) <"The eligible asset is specified by reference to the ISDA 2003 Collateral Assets Definitions.">
		[synonym FpML_5_10 value "eligibleAsset"]
		// TDOD: not sure how we can map ISDA_CREATE cases where items is a specific stock, i.e. "items": "BT stock"
		[synonym ISDA_Create_1_0 value "items" path "partyA_eligible_currencies"]
		[synonym ISDA_Create_1_0 value "items" path "partyB_eligible_currencies"]
	lowerMaturity Period (0..1) <"The (optional) lower maturity band of the eligible collateral assets.">
		[synonym FpML_5_10 value "lowerMaturity"]
	higherMaturity Period (0..1) <"The (optional) higher maturity band of the eligible collateral assets.">
		[synonym FpML_5_10 value "higherMaturity"]
	minimumRating CreditNotations (0..1) <"The (optional) minimal rating terms that can be specified as part of the eligible collateral.">
		[synonym FpML_5_10 value "minimumRating"]
	valuationPercentage number (0..1) <"Specifies the haircut associated to the collateral asset, expressed as a value comprised between 0 and 1. A value of 0.9 means that 90% of the value of the applicable asset is considered as collateral. Absence of this element implies that the valuation percentage is 1, i.e. 100%.">
		[synonym FpML_5_10 value "valuationPercentage"]
		// This mapping will not work in a number of cases, as the ISDA Credit field is specified as a string
		[synonym ISDA_Create_1_0 value "valuation_percentage" path "partyA_eligible_currencies"]
		[synonym ISDA_Create_1_0 value "valuation_percentage" path "partyB_eligible_currencies"]

	condition ValuationPercentage: <"The valuation percentage should be specified as a value between 0 and 1.">
		if valuationPercentage exists
		then valuationPercentage >= 0
			and valuationPercentage <= 1
*/
