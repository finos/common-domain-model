namespace org.isda.cdm
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.math.*
import cdm.base.staticdata.asset.common.*
import cdm.base.staticdata.party.*

// Note: All Eligible Collateral modelling/representation is still under development and discussion with ISDA Working groups

type AgencyRatingCriteria: <"A class to specify multiple credit notations alongside a conditional 'any' or 'all' qualifier.">
	qualifier QuantifierEnum (1..1) <"Indicates whether all or any agency ratings apply.">
	creditNotation CreditNotation (1..*)
	mismatchResolution CreditNotationMismatchResolutionEnum (0..1)
	referenceAgency CreditRatingAgencyEnum (0..1)

	condition ReferenceAgency: <"If the mismatch resolution is ReferenceAgency, ensure that the reference agency is specified.">
		if AgencyRatingCriteria -> mismatchResolution = CreditNotationMismatchResolutionEnum -> ReferenceAgency
		then AgencyRatingCriteria -> referenceAgency exists

type CollateralIssuerType: <"A class to allow specification of the type of entity issuing the collateral.">
	issuerType IssuerTypeEnum (1..1) <"Origin of entity issuing the collateral.">
	supraNationalType SupraNationalIssuerTypeEnum (0..1) <"Debt issued by international organisations and multilateral banks.">
	quasiGovernmentType QuasiGovernmentIssuerType (0..1) <"Debt issues by institutions or bodies, typically constituted by statute, with a function mandated by the government and subject to government supervision inclusive of profit- and non-profit making bodies. Includes the US Agencies and GSEs and the EU concept of public sector entities. Excluding any entities which are also Regional Government">
	regionalGovernmentType RegionalGovernmentIssuerType (0..1) <"Regional government, local authority or municipal.">
	specialPurposeVehicleType SpecialPurposeVehicleIssuerType (0..1)
	// fundType FundIssuerType (0..1) // NO LONGER REQUIRED - specified fund country of origin which now sits under ProductCriteria.productCountryOfOrigin
	
	condition SupraNationalSubType:
		if issuerType <> IssuerTypeEnum -> SupraNational		
		then supraNationalType is absent
	
	condition QuasiGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> QuasiGovernment
		then quasiGovernmentType is absent
	
	condition RegionalGovernmentSubType:
		if issuerType <> IssuerTypeEnum -> RegionalGovernment
		then regionalGovernmentType is absent
	
	condition SpecialPurposeVehicleSubType:
		if issuerType <> IssuerTypeEnum -> SpecialPurposeVehicle
		then specialPurposeVehicleType is absent
		
	// condition FundSubType:
	// 	if issuerType <> IssuerTypeEnum -> Fund
	// 	then fundType is absent

type QuasiGovernmentIssuerType: <"A class to allow specification of different types of Quasi Government collateral.">
	sovereignEntity boolean (1..1) <"True if sovereign entity (e.g. not separate legal personality from sovereign) or false if non-sovereign entity (e.g. separate legal personality from sovereign).">
	sovereignRecourse boolean (0..1) <"Applies to non-sovereign entity (e.g. separate legal personality from sovereign).  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// Eligible sovereigns specified by IssuerCriteria.issuerCountryOfOrigin
	// Eligible issuers specified by IssuerCriteria.issuerName
	// TODO: other (non-exhaustive) criteria e.g. GSEs with/without government support, EMIR public sector entities, US agencies
	
	condition NonSovereignEntityRecourse:
		if sovereignRecourse exists then sovereignEntity = False

type RegionalGovernmentIssuerType: <"A class to allow specification of different type of Regional government collateral.">
	sovereignRecourse boolean (1..1) <"Applies to regional governments, local authorities or municipals.  True if entity has recourse to sovereign (e.g. debt guaranteed by government).  False if entity does not have recourse to sovereign.">
	// Eligible sovereigns specified by IssuerCriteria.issuerCountryOfOrigin
	// Eligible issuers specified by IssuerCriteria.issuerName
	// TODO: other (non-exhaustive) criteria e.g. US municipals, Canadian provinces, German Lander

type SpecialPurposeVehicleIssuerType: <"A class to allow specification of different types of special purpose vehicle (SPV) collateral.">
	creditRisk CreditRiskEnum (0..1) <"Tranched or untranched credit risk."> // Not required?  If tranched credit risk then specified ProductCriteria.agencyRating. 
	// Eligible jurisdictions of incorporation is specified by IssuerCriteria.issuerCountryOfOrigin
	// TODO: other (non-exhaustive) criteria e.g. by asset class (RMBS, CMBS, CLO, by regional risk focus, by guarantor (e.g. Ginnie Mae)). Can the existing FpML framework be leveraged.  See type MortgageBackedSecurity

// type FundIssuerType:
// 	fundCountryOfOrigin string (0..*) <"Fund country of origin.  Note that fund manager country of origin is specified by IssuerCriteria.issuerCountryOfOrigin.">
// 		[metadata scheme]
// NOW Captured under ProductCriteria.productCountryOfOrigin
	// exchangeTraded is specified by EquityTypeEnum.ExchangeTradedFund

type ProductType: <"A class to allow specification of the asset product type.">
	productType ProductTypeEnum (1..1) <"The type of collateral product.">
	securityType SecurityTypeEnum (0..1) <"Filter based on the type of security.">
	bondType BondType (0..1) <"Filter based on the type of bond."> 
	equityType EquityTypeEnum (0..1) <"Filter based on the type of equity.">
	fundType FundProductTypeEnum (0..1) <"Filter based on the type of fund.">
	// productCountryOfOrigin string (0..*) <"Filter based on the issuing entity country of origin."> // Now sits under ProductCriteria, consistent with IssuerCriteria
	// 	[metadata scheme]

	condition SecuritySubType:
		if productType <> ProductTypeEnum -> Security
		then ( securityType and bondType and equityType ) is absent 
	
	condition BondSubType:
		if securityType <> SecurityTypeEnum -> Bond
		then bondType is absent
	
	condition EquitySubType:
		if securityType <> SecurityTypeEnum -> Equity
		then equityType is absent

	condition FundSubType:
		if securityType <> SecurityTypeEnum -> Fund
		then fundType is absent


type CollateralValuationPercentage:
    valuationPercentage number (1..1) <"Percentage value of asset to be used as collateral once discounted haircut take into consideration,expressed as a valuation. As an example a 0.5% haircut would be expressed as 99.5% and represented as a decimal number 0.995">
	fxHaircutPercentage number (0..1) <"FX haircut applied to a specific asset which is agreed between the parties (for example, if pledgor eligible collateral is not denominated in the termination currency or under other specified cases in collateral support documents both for initial margin and variation margin).The percentage value is expressed as the discount haircut to the value of the collateral- as an example an 8% FX haircut would be expressed as 0.08.">

	condition: valuationPercentage > 0 and valuationPercentage <=1
	condition FxHaircutPercentage: <"A data rule to validate that if an FX Haircut Percentage is specified it should be between 0 and 1.">
		if fxHaircutPercentage exists
		then fxHaircutPercentage > 0 and fxHaircutPercentage <=1

type EligibleCollateral: <"Set of criteria used to specify eligible collateral.">
[rootType]
	criteria EligibleCollateralCriteria (1..*)

type EligibleCollateralCriteria: <"Criteria used to specify eligible collateral.">
	issuer IssuerCriteria (0..*) <"Filter based on the issuer.">
	product ProductCriteria (0..*) <"Filter based on the product.">
	treatment CollateralTreatment (1..1) <"Treatment of described collateral">

type CollateralTreatment: <"A class to specify the treatment terms for the eligible collateral criteria specified.">
	valuationPercentage CollateralValuationPercentage (0..1) <"Specification of the valuation treatment for the specified collateral.">
	concentrationLimit ConcentrationLimit (0..*) <"Specification of concentration limits applicable to the collateral criteria.">
	isIncluded boolean (1..1) <"A boolean attribute to specify whether collateral critieria are inclusion (True) or exclusion (False) criteria">

type ConcentrationLimit: <"A class to describe concentration limits that may be applicable to eligible collateral criteria.">
	concentrationLimitType ConcentrationLimitTypeEnum (0..1) <"The type of concentration limit to be applied.">
	concentrationLimitCriteria ConcentrationLimitCriteria (0..1) <"A set of criteria to describe specific assets that the concentration limits apply to.">
	valueCap Money (0..1) <"value of collateral limit cap-represented as a numerical value">
	percentageCap number (0..1) <"perecentage of collateral limit cap-represented as a decimal number - example 25% is 0.25">

type ConcentrationLimitCriteria: <"A class to describe a set of criteria to describe specific assets that the concentration limits apply to.">
	issuer IssuerCriteria (0..*) <"Filter based on the issuer.">
	product ProductCriteria (0..*) <"Filter based on the product.">

type IssuerCriteria: <"Criteria used to specify eligible collateral issuers.">
	issuerType CollateralIssuerType (0..*) <"Filter based on the type of entity issuing the asset.">
    issuerCountryOfOrigin string (0..*) <"Filter based on the issuing entity country of origin."> // Same as list of eligible Sovereigns
		[metadata scheme]
	issuerName LegalEntity (0..*) <"Filter based on the issuing entity name or LEI.">
	issuerAgencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk and creditors claim in event of default associated with asset issuer.">
	sovereignAgencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk of country.">
	counterpartyOwnIssuePermitted boolean (0..1) <"Filter based on whether it is permitted for the the underlying asset to be issued by the posting entity or part of their corporate family."> // Explore options to infer this from one way risk
	
type ProductCriteria: <"Criteria used to specify eligible collateral assets.">
	collateralProductType ProductType (0..*) <"Filter based on the asset product type.">
	productCountryOfOrigin string (0..*) <"Filter based on the issuing entity country of origin."> // Same as list of eligible Sovereigns
		[metadata scheme]
	denominatedCurrency string (0..*) <"Filter based on the underlying asset denominated currency.">
	    [metadata scheme]
	agencyRating AgencyRatingCriteria (0..*) <"Agency rating based on default risk and creditors claim in event of default associated with specific instrument.">
	maturityType MaturityTypeEnum (0..1) <"Specifies whether the maturity range is the remaining or original maturity.">
	maturityRange PeriodRange (0..1) <"Filter based on the underlying asset maturity.">
	productIdentifier ProductIdentifier (0..*) <"Filter based on specific instrument identifiers (e.g. specific ISINs, CUSIPs etc).">
	productTaxonomy ProductTaxonomy (0..*) <"The product taxonomy value(s) associated with a product.">
    seasoned boolean (0..1) <"Filter based on whether underlying security is seasoned."> // IS THIS REQUIRED - ISDA feedback is that this should be removed
	sinkable boolean (0..1) <"Filter based on whether underlying security is sinkable."> // IS THIS REQUIRED - ISDA feedback is that this should be removed
	domesticCurrencyIssued boolean (0..1) <"Security must be denominated in the domestic currency of the issuer.">

	condition Choice: <"If any are specified, only one of ProductType, ProductTaxonomy or ProductIdentifer should exist.">
		optional choice collateralProductType, productTaxonomy, productIdentifier

type BondType: <"Specification of the type of bond">
	bondClass BondClassEnum (0..1) <"Specification of the characteristics of bond">
	bondEconomics BondEconomics (0..*) <"Specification of the financial terms of the bond">

type BondEconomics: <"Specification of the economics of the bond">
	bondSeniority BondSeniorityEnum (0..1) <"Security: Bond Economics: The order of repayment in the event of a sale or bankruptcy of the issuer or a related party (eg guarantor).">
	bondInterest BondInterestEnum (0..1) <"Security: Bond Economics: The terms of the reference rate for calculation of interest on the bond.">
	bondPrincipal BondPrincipalEnum (0..1) <"Security: Bond Economics: The repayment terms for the principal on the bond.">
