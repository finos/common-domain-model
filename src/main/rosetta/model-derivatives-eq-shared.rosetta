namespace org.isda.cdm
version "${project.version}"

import cdm.base.*
import cdm.base.datetime.*
import cdm.base.staticdata.party.*

type AdditionalDisruptionEvents: <"A type for defining the Additional Disruption Events.">
	[synonym FpML_5_10 value "AdditionalDisruptionEvents"]

	changeInLaw boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Change in Law | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Change In Law | If true, then change in law is applicable.">
		[synonym FpML_5_10 value "changeInLaw"]
	failureToDeliver boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Failure to Deliver | Where the underlying is shares and the transaction is physically settled, then, if true, a failure to deliver the shares on the settlement date will not be an event of default for the purposes of the master agreement.">
		[synonym FpML_5_10 value "failureToDeliver"]
	insolvencyFiling boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Insolvency Filing | If true, then insolvency filing is applicable.">
		[synonym FpML_5_10 value "insolvencyFiling"]
	hedgingDisruption boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Hedging Disruption | If true, then hedging disruption is applicable.">
		[synonym FpML_5_10 value "hedgingDisruption"]
	increasedCostOfHedging boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Increased Cost of Hedging | If true, then increased cost of hedging is applicable.">
		[synonym FpML_5_10 value "increasedCostOfHedging"]
	foreignOwnershipEvent boolean (0..1) <"If true, then foreign ownership event is applicable.">
		[synonym FpML_5_10 value "foreignOwnershipEvent"]
	lossOfStockBorrow boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Loss of Stock Borrow | If true, then loss of stock borrow is applicable.">
		[synonym FpML_5_10 value "lossOfStockBorrow"]
	maximumStockLoanRate number (0..1) <"Specifies the maximum stock loan rate for Loss of Stock Borrow. A percentage of 5% is represented as 0.05.">
		[synonym FpML_5_10 value "maximumStockLoanRate"]
	increasedCostOfStockBorrow boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Increased Cost of Stock Borrow | If true, then increased cost of stock borrow is applicable.">
		[synonym FpML_5_10 value "increasedCostOfStockBorrow"]
	initialStockLoanRate number (0..1) <"Specifies the initial stock loan rate for Increased Cost of Stock Borrow. A percentage of 5% is represented as 0.05.">
		[synonym FpML_5_10 value "initialStockLoanRate"]
	determiningPartyReference Party (0..1) <"A reference to the party which determines additional disruption events.">
		[metadata reference]
		[synonym FpML_5_10 value "determiningPartyReference" meta "href"]

	condition MaximumStockLoanRate: <" FpML specifies the maximumStockLoanRate as a RestrictedPercentage, meaning that its value is comprised between 0 and 1.">
		if maximumStockLoanRate exists
		then maximumStockLoanRate >= 0
			and maximumStockLoanRate <= 1

	condition InitialStockLoanRate: <" FpML specifies the initialStockLoanRate as a RestrictedPercentage, meaning that its value is comprised between 0 and 1.">
		if initialStockLoanRate exists
		then AdditionalDisruptionEvents ->initialStockLoanRate >= 0
			and initialStockLoanRate <= 1

type DeterminationMethod: <"The purpose of this class is to be extended by the Price and EquityValuation classes.">

	// TODO Never referenced in CDM
	determinationMethod DeterminationMethodEnum (0..1) <"Specifies the method according to which an amount or a date is determined.">
		[synonym FpML_5_10 value "determinationMethod"]


type DividendCurrency: <"A class to specify the currency in which the dividends will be denominated, i.e. either in the dividend currency or in a currency specified as part of the contract.">

	currency string (0..1) <"The currency in which the dividend is denominated. The list of valid currencies is not presently positioned as an enumeration as part of the CDM because that scope is limited to the values specified by ISDA and FpML. As a result, implementers have to make reference to the relevant standard, such as the ISO 4217 standard for currency codes.">
		[metadata scheme]
		[synonym FpML_5_10 value "currency" meta "currencyScheme"]
	determinationMethod DeterminationMethodEnum (0..1) <"Specifies the method according to which the dividend is determined, e.g. the dividend currency.">
		[synonym FpML_5_10 value "determinationMethod"]
	currencyReference string (0..1) <"Reference to a currency specified elsewhere in the document">
		[metadata reference]
		[synonym FpML_5_10 value "currencyReference" meta "href"]
	condition: one-of

type DividendDateReference: <"A class to specify the dividend date by reference to another date, with the ability to apply and offset. This class doesn't exist in FpML and is meant to simplify the choice constraint associated with the DividendPaymentDate class.">

	dateReference DividendDateReferenceEnum (1..1) <"Specification of the dividend date using an enumeration, with values such as the pay date, the ex-date or the record date.">
		[synonym FpML_5_10 value "dateReference"]
	paymentDateOffset Offset (0..1) <"Only to be used when SharePayment has been specified in the dividendDateReference element. The number of Currency Business Days following the day on which the Issuer of the Shares pays the relevant dividend to holders of record of the Shares.">
		[synonym FpML_5_10 value "paymentDateOffset"]

	condition PaymentDateOffset: <" FpML specifies that paymentDateOffset is only to be used when SharePayment has been specified in the dividendDateReference element.">
		if paymentDateOffset exists
		then dateReference = DividendDateReferenceEnum -> SharePayment

type DividendPaymentDate: <"A class describing the date on which the dividend will be paid/received. This class is also used to specify the date on which the FX rate will be determined, when applicable.">
	[synonym FpML_5_10 value "DividendPaymentDate"]

	dividendDateReference DividendDateReference (0..1)
		[synonym FpML_5_10 value "dividendDateReference"]
	adjustableDate AdjustableDate (0..1)
		[synonym FpML_5_10 value "adjustableDate"]
	condition: one-of

type DividendReturnTerms: <"A class describing the conditions governing the payment of dividends to the receiver of the equity return, with the exception of the dividend payout ratio, which is defined for each of the underlying components.">
	[synonym FpML_5_10 value "DividendConditions"]

	dividendPayout DividendPayout (0..1) <"Specifies the dividend payout ratio associated with the underlier. In FpML 5.10 the payout is positioned at the underlier level, although there is an intent to reconsider this approach and position it at the leg level. This is approach adopted by the CDM.">
		[synonym FpML_5_10 value "dividendPayout"]
	dividendReinvestment boolean (0..1) <"Boolean element that defines whether the dividend will be reinvested or not.">
		[synonym FpML_5_10 value "dividendReinvestment"]
	dividendEntitlement DividendEntitlementEnum (0..1) <"Defines the date on which the receiver of the equity return is entitled to the dividend.">
		[synonym FpML_5_10 value "dividendEntitlement"]
	dividendAmountType DividendAmountTypeEnum (0..1) <"Specifies whether the dividend is paid with respect to the Dividend Period.">
		[synonym FpML_5_10 value "dividendAmount"]
	dividendPaymentDate DividendPaymentDate (0..1) <"Specifies when the dividend will be paid to the receiver of the equity return. Has the meaning as defined in the ISDA 2002 Equity Derivatives Definitions. Is not applicable in the case of a dividend reinvestment election.">
		[synonym FpML_5_10 value "dividendPaymentDate"]
	dividendPeriod DividendPeriodEnum (0..1) <"2002 ISDA Equity Derivatives Definitions: Dividend Period as either the First Period or the Second Period. | ">
		[synonym FpML_5_10 value "dividendPeriod"]
	dividendPeriodEffectiveDate date (0..1) <"2002 ISDA Equity Derivatives Definitions: Dividend Period as such other period determined as provided in the related Confirmation. | ">
		[metadata reference]
		[synonym FpML_5_10 value "dividendPeriodEffectiveDate" meta "href"]
	dividendPeriodEndDate date (0..1) <"2002 ISDA Equity Derivatives Definitions: Dividend Period as such other period determined as provided in the related Confirmation.">
		[metadata reference]
		[synonym FpML_5_10 value "dividendPeriodEndDate" meta "href"]
	extraordinaryDividendsParty Party (0..1) <"Reference to the party which determines if dividends are extraordinary in relation to normal levels.">
		[metadata reference]
		[synonym FpML_5_10 value "extraOrdinaryDividends" meta "href"]
	excessDividendAmount DividendAmountTypeEnum (0..1) <"Determination of Gross Cash Dividend per Share.">
		[synonym FpML_5_10 value "excessDividendAmount"]
	dividendCurrency DividendCurrency (0..1) <"Specifies the currency in which the dividend will be denominated, e.g. the dividend currency, or a specified currency. This class is not specified as such in FpML, which makes use of the CurrencyAndDeterminationMethod.model to specify such terms.">

	/*
	 * More FpML provisions to be confirmed & specified
	 */
	condition DividendPeriod: <"  FpML specifies a choice between dividendPeriod one one end, and dividendPeriodEffectiveDate and dividendPeriodEndDate on the other end.">
		if dividendPeriod exists
		then dividendPeriodEffectiveDate is absent
			and dividendPeriodEndDate is absent

type EquityCorporateEvents: <"A class for defining the merger events and their treatment.">
	[synonym FpML_5_10 value "EquityCorporateEvents"]

	shareForShare ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-S) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Combined Consideration. | The consideration paid for the original shares following the Merger Event consists wholly of new shares.">
		[synonym FpML_5_10 value "shareForShare"]
	shareForOther ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-O) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Other Consideration. | The consideration paid for the original shares following the Merger Event consists wholly of cash/securities other than new shares.">
		[synonym FpML_5_10 value "shareForOther"]
	shareForCombined ShareExtraordinaryEventEnum (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event (S-F-C) shall occur if a Merger Event occurs and the consideration for the relevant Security consists solely of Combined Consideration. | The consideration paid for the original shares following the Merger Event consists of both cash/securities and new shares.">
		[synonym FpML_5_10 value "shareForCombined"]


type EquityValuation extends CashPrice: <"A class for defining how and when an equity option or equity swap is to be valued.">
	[synonym FpML_5_10 value "EquityValuation"]

	determinationMethod DeterminationMethodEnum (1..1) <"Specifies the method according to which an amount or a date is determined.">
		[synonym FpML_5_10 value "determinationMethod"]
	valuationDates AdjustableOrRelativeDates (0..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Pricing Date">
		[synonym FpML_5_10 value "valuationDates" path "valuationRules"]
	valuationDate AdjustableOrRelativeDate (0..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Pricing Date">
		[synonym FpML_5_10 value "valuationDate" path "valuationRules"]
	valuationTime BusinessCenterTime (0..1) <"The specific time of day at which the calculation agent values the underlying. The SpecificTime is the only case when the valuationTime (time + business center location â€“ e.g. 10:00:00 USNY) should be provided. You should be able to provide just the valuationTime without valuationTimeType, which infer that this is a specific time.">
		[synonym FpML_5_10 value "valuationTime" path "valuationRules"]
	valuationTimeType TimeTypeEnum (0..1) <"The time of day at which the calculation agent values the underlying, for example the official closing time of the exchange.">
		[synonym FpML_5_10 value "valuationTimeType" path "valuationRules"]

/*
 * The FpML construct has a a number of other attributes: dividend valuation dates, fall-back provision, ...
 * Also, FpML extends Price while the current CDM approach is just to share the determinationMethod.  To be confirmed as a function of the use cases.
 */
type ExtraordinaryEvents: <"Where the underlying is shares, defines market events affecting the issuer of those shares that may require the terms of the transaction to be adjusted.">
	[synonym FpML_5_10 value "ExtraordinaryEvents"]

	mergerEvents EquityCorporateEvents (0..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Merger Event shall occur if a Merger occurs and the Merger Date is on or before the final Equity Valuation Date | Occurs when the underlying ceases to exist following a merger between the Issuer and another company.">
		[synonym FpML_5_10 value "mergerEvents"]
	tenderOfferEvents EquityCorporateEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Tender Offers | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Tender Offer Event">
		[synonym FpML_5_10 value "tenderOfferEvents"]
	compositionOfCombinedConsideration boolean (0..1) <"2002 ISDA Equity Derivatives Definitions: Composition of Combined Consideration | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Combined Consideration | If present and true, then composition of combined consideration is applicable.">
		[synonym FpML_5_10 value "compositionOfCombinedConsideration"]
	indexAdjustmentEvents IndexAdjustmentEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Adjustments to Indices | ">
		[synonym FpML_5_10 value "indexAdjustmentEvents"]
	additionalDisruptionEvents AdditionalDisruptionEvents (0..1) <"2002 ISDA Equity Derivatives Definitions: Additional Disruption Events | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Additional Disruption Events means each Additional Disruption Event specified in the Confirmation Side Letter. For the avoidance of doubt, each Additional Disruption Event shall be an Extraordinary Event.">
		[synonym FpML_5_10 value "additionalDisruptionEvents"]
	failureToDeliver boolean (0..1) <"If true, failure to deliver is applicable.">
		[synonym FpML_5_10 value "failureToDeliver"]
	representations Representations (0..1)
		[synonym FpML_5_10 value "representations"]
	nationalizationOrInsolvency NationalizationOrInsolvencyOrDelistingEventEnum (0..1) <"2002 ISDA Equity Derivatives Definitions: Nationalization and Insolvency | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Nationalization shall occur if all the Securities or all or substantially all the assets of an Issuer are nationalized, expropriated or are otherwise required to be transferred to any governmental agency, authority, entity or instrumentality thereof. Insolvency Filing means as defined in the Confirmation Side Letter.">
		[synonym FpML_5_10 value "nationalisationOrInsolvency"]
	delisting NationalizationOrInsolvencyOrDelistingEventEnum (0..1) <"2002 ISDA Equity Derivatives Definitions: Delisting | 2018 ISDA CDM Equity Confirmation for Security Equity Swap: Delisting means Delisting (Broad Relisting) or Delisting (Narrow Relisting), as specified in the Relationship Supplement.">
		[synonym FpML_5_10 value "delisting"]
	condition Choice: <"condition to represent an FpML choice construct.">
		required choice additionalDisruptionEvents, failureToDeliver

type IndexAdjustmentEvents: <"Defines the specification of the consequences of Index Events as defined by the 2002 ISDA Equity Derivatives Definitions.">
	[synonym FpML_5_10 value "IndexAdjustmentEvents"]

	indexModification IndexEventConsequenceEnum (1..1) <"Consequence of index modification.">
		[synonym FpML_5_10 value "indexModification"]
	indexCancellation IndexEventConsequenceEnum (1..1) <"Consequence of index cancellation.">
		[synonym FpML_5_10 value "indexCancellation"]
	indexDisruption IndexEventConsequenceEnum (1..1) <"Consequence of index disruption.">
		[synonym FpML_5_10 value "indexDisruption"]


// TODO - #1 Look for further synergies with the option strike
// TODO - #2 Move class to .rosetta file that is not specific to an asset class
// TODO - #3: Create "PriceBase" that all of CashPrice, ExchangeRate etc should extend, and position the "assetIdentifier" attribute in "PriceBase" (currently in "InterestRateBase")
type Price: <"Generic description of the price concept applicable across product types, which can be expressed in a number of ways other than simply cash price">

	cashPrice CashPrice (0..1) <"Price specified in cash terms, e.g. for securities proceeds or fee payment in a contractual product.">
		[synonym FpML_5_10 value "initialPrice" path "rateOfReturn", "underlyer"]
		[synonym FpML_5_10 hint "paymentAmount"]
		[synonym FpML_5_10 hint "fixedAmount"]
	exchangeRate ExchangeRate (0..1) <"Price specified as an exchange rate between two currencies.">
		[synonym FpML_5_10 value "exchangeRate"]
	fixedInterestRate FixedInterestRate (0..1) <"Price specified as a fixed interest rate.">
		// For IR Swaps
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "fixedRateSchedule" path "calculationPeriodAmount->calculation"]
		// For CDS
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "fixedAmountCalculation"]
		// For Repo
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "fixedRateSchedule"]
		// For FRA:
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 hint "fixedRate"]
	interestRateSpread InterestRateSpread (0..1) <"Price specified as a spread on top of a floating interest rate.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "floatingRateCalculation" path "calculationPeriodAmount->calculation"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "floatingRateCalculation" path "interestCalculation"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "floatingRateCalculation"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value "floatingAmountCalculation"]

	condition: one-of


// TODO - Look for further synergies with the SecurityValuation class
type CashPrice: <"Describes a price component as a cash amount, which can either represent cash proceeds for a security purchase or a cashflow that is embedded as part of a contractual product: for instance an upfront fee.">

	grossPrice ActualPrice (0..1) <"Specifies the price of the underlier, before commissions.">
		[synonym FpML_5_10 value "grossPrice"]
	cleanNetPrice ActualPrice (0..1) <"Specifies the clean price of the underlier (e.g. excluding accrued interest), net of commissions.">
		[synonym FpML_5_10 value "cleanNetPrice"]
	netPrice ActualPrice (0..1) <"Specifies the dirty price of the underlier (e.g. including accrued interest), net of commissions.">
		[synonym FpML_5_10 value "netPrice"]
	accruedInterest number (0..1) <"Specifies the accrued interest that are part of the dirty price in the case of a fixed income security or a convertible bond. Expressed in percentage of the notional.">
		[synonym FpML_5_10 value "accruedInterestPrice"]
	cashflowAmount Money (0..1) <"Specifies the price as a cashflow amount embedded as part of a a contractual product.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "paymentAmount"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value "fixedAmount"]
	/*cashflowType CashflowTypeEnum (0..1) <"The qualification of the type of cashflow, e.g. brokerage fee, premium, upfront fee etc. Particularly relevant when it cannot be inferred directly through lineage.">
		// [synonym FpML_5_10, CME_ClearedConfirm_1_17 value "paymentType", "paymentType" path "premium"]
		[synonym FpML_5_10, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17
			set to CashflowTypeEnum -> Fee when "feeLeg" exists,
			set to CashflowTypeEnum -> Premium when "premium" exists]*/
	/*
	 * FpML construct also includes other elements, such as the fxConversion
	 */
	condition NetPrice: <" The netPrice and/or cleanNetPrice must be specified.">
		if CashPrice exists
		then netPrice exists or cleanNetPrice exists or cashflowAmount exists

type PriceReturnTerms:
	[synonym FpML_5_10 value "ReturnLegValuationPrice"]

	valuationPriceInterim EquityValuation (0..1) <"Specifies the interim valuation price(s) of the underlier. This price can be expressed either as an actual amount/currency, as a determination method, or by reference to another value specified in the swap document.">
		[synonym FpML_5_10 value "valuationPriceInterim" path "rateOfReturn"]
	valuationPriceFinal EquityValuation (1..1) <"2018 ISDA CDM Equity Confirmation for Security Equity Swap: Final Price | Specifies the final valuation price of the underlier. This price can be expressed either as an actual amount/currency, as a determination method, or by reference to another value specified in the swap document.">
		[synonym FpML_5_10 value "valuationPriceFinal" path "rateOfReturn"]

// In a subsequent phase of work, a single price should be stored and "reset" after each period. Lineage should then be used to resolve the period start and end prices for the Equity Cash Settlement Amount calculation	
type Representations:
	[synonym FpML_5_10 value "Representations"]

	nonReliance boolean (1..1) <"If true, then non reliance is applicable.">
		[synonym FpML_5_10 value "nonReliance"]
	agreementsRegardingHedging boolean (1..1) <"If true, then agreements regarding hedging are applicable.">
		[synonym FpML_5_10 value "agreementsRegardingHedging"]
	indexDisclaimer boolean (1..1) <"If present and true, then index disclaimer is applicable.">
		[synonym FpML_5_10 value "indexDisclaimer"]
	additionalAcknowledgements boolean (0..1) <"If true, then additional acknowledgements are applicable.">
		[synonym FpML_5_10 value "additionalAcknowledgements"]

