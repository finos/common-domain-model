namespace "org.isda.cdm"
version "${project.version}"

type AssignedIdentifier: <"A class to specify the identifier value and its associated version.">
	identifier string (1..1) <"The identifier value.">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value tradeId meta tradeIdScheme]
		[synonym FpML_5_10 value tradeId path "versionedTradeId" meta tradeIdScheme]
		[synonym Rosetta_Workbench value identifier]
		[synonym DTCC_11_0, DTCC_9_0 value tradeId path "partyTradeIdentifier" maps 2 
			set when "partyTradeIdentifier.tradeId.tradeIdScheme" = "EventProcessingId" and rosettaPath = Event -> eventIdentifier -> assignedIdentifier -> identifier,
			set when "partyTradeIdentifier.tradeId.tradeIdScheme" = "TradeRefNbr" and rosettaPath = Contract -> contractIdentifier -> assignedIdentifier -> identifier meta tradeIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value USITradeId meta tradeIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value UTITradeId meta tradeIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value identifier path "payment" meta paymentIdScheme]
		[synonym CME_ClearedConfirm_1_17 value tradeId meta tradeIdScheme]
		[synonym CME_ClearedConfirm_1_17 value usi meta usiScheme]
		[synonym CME_SubmissionIRS_1_0 value RptID path "TrdCaptRpt"]
		[synonym FpML_5_10 value businessUnitId meta unitScheme]
		[synonym ISDA_Create_1_0 value id]
	version int (0..1) <"The identifier version, which is specified as an integer and is meant to be incremented each time the transaction terms (whether contract or event) change. This version is made option to support the use case where the identifier is referenced without the version. The constraint that a contract and a lifecycle event need to have an associated version is enforced through data rules.">
		[synonym FpML_5_10 value version path "versionedTradeId"]
		[synonym FpML_5_10 value version]

type CalculationAgentModel: <"This class corresponds to the FpML CalculationAgent.model.">
	// TODO Never referenced in CDM
	calculationAgent CalculationAgent (0..1) <"The ISDA calculation agent responsible for performing duties as defined in the applicable product definitions.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value calculationAgent path "trade"]
		[synonym Rosetta_Workbench value calculationAgent]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value calculationAgent path "trade.swaption"]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value calculationAgent path "swaption"]
	calculationAgentBusinessCenter BusinessCenterEnum (0..1) <"The city in which the office through which ISDA Calculation Agent is acting for purposes of the transaction is located. The short-form confirm for a trade that is executed under a Sovereign or Asia-Pacific Master Confirmation Agreement (MCA), does not need to specify the Calculation Agent. However, the confirm does need to specify the Calculation Agent city. This is due to the fact that the MCA sets the value for Calculation Agent but does not set the value for Calculation Agent city.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value calculationAgentBusinessCenter path "trade"]
		[synonym Rosetta_Workbench value calculationAgentBusinessCenter]
		[synonym DTCC_11_0, DTCC_9_0 value calculationAgentBusinessCenter path "Body.OTC_Matching.Trade.FpML.trade"]

type Identifier: <"A class to specify a generic identifier, applicable to CDM artefacts such as executions, contracts, lifecycle events and legal documents. An issuer can be associated with the actual identifier value as a way to properly qualify it.">
	[metadata key]
	[synonym FpML_5_10 meta id]
	[synonym FpML_5_10, CME_SubmissionIRS_1_0 meta id path "versionedTradeId.tradeId"]
	issuerReference Party (0..1) <"The identifier issuer, when specified by reference to a party specified as part of the transaction.">
		[metadata reference]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value issuer meta href]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value partyReference meta href]
		[synonym DTCC_11_0, DTCC_9_0 value partyReference path "partyTradeIdentifier" maps 2 meta href]
		[synonym CME_ClearedConfirm_1_17 value partyReference meta href]
	issuer string (0..1) <"The identifier issuer, when specified explicitly alongside the identifier value (instead of being specified by reference to a party).">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0 value issuer meta issuerIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value USIIssuer meta issuerIdScheme]
		[synonym DTCC_11_0, DTCC_9_0 value UTIIssuer meta issuerIdScheme]
		[synonym CME_ClearedConfirm_1_17 value issuer meta issuerIdScheme]
	assignedIdentifier AssignedIdentifier (1..*) <"The identifier value. This level of indirection between the issuer and the identifier and its version provides the ability to associate multiple identifiers to one issuer, consistently with the FpML PartyTradeIdentifier.">
	condition Identifier_choice: <"The identifier issuer is specified either explicitly or by reference to one of the parties.">
		required choice issuerReference, issuer


/*
 * Only some of the attributes have been incorporated at this point, as some seem questionable (e.g. executionDateTime, which should be associated to the relevant execution event).
 */
type PackageInformation: <"A class defining additional information that may be recorded alongside a transaction package.">
	// TODO Never referenced in CDM
	[synonym FpML_5_10 value PackageInformation]
	relatedParty RelatedParty (0..*) <"This may be used to identify one or more parties that perform a role as part of the transaction.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value relatedParty]
	category CategoryEnum (0..*)
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value category]
	/*
	 * Why should the below be specifically associated with package transactions?
	 * It seems to also be relevant in the context of single trades
	 */
	intentToAllocate boolean (0..1) <"specifies whether the transaction package is anticipated to be allocated.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value intentToAllocate]

type PartyContractInformation: <"A class defining party-specific additional information that may be recorded with respect to a contract.">
	[synonym FpML_5_10 value PartyTradeInformation]
	partyReference Party (1..1) <"The reference to the party that owns this party contract information or, in the case of shared trades information, the reference that originated such information.">
		[metadata reference]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value partyReference meta href]
	naturalPersonRole NaturalPersonRole (0..*) <"The role(s) that natural person(s) may have in relation to the contract.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value relatedPerson]
	relatedParty RelatedParty (0..1)
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value relatedParty]
	accountReference Account (0..1) <"Reference to an account.">
		[metadata reference]
		[synonym CME_ClearedConfirm_1_17 value accountReference meta href]
	category CategoryEnum (0..1) <"The qualification of the trade by the counterparty, e.g. customer or principal. This information is relevant for a number of marketplace processes, such as regulatory reporting or clearing.">
		[metadata scheme]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, CME_ClearedConfirm_1_17 value category meta categoryScheme]

type PercentageRule: <"A class defining a content model for a calculation rule defined as percentage of the notional amount.">
	[synonym FpML_5_10 value PercentageRule]
	paymentPercent number (1..1) <"A percentage of the notional amount.">
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value paymentPercent]
	notionalAmountReference Money (1..1) <"A reference to the notional amount.">
		[metadata reference]
		[synonym FpML_5_10, CME_SubmissionIRS_1_0, DTCC_11_0, DTCC_9_0, CME_ClearedConfirm_1_17 value notionalAmountReference meta href]
